(*
# WebGraph Generator
File responsible for webgraph generation out of collision map images.
*)

{$DEFINE WL_WEBGRAPHGEN_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

type
(*
## TWebGraphGenerator
Record responsible for generating webgraphs from collision map images.
*)
  TWebGraphGenerator = record
    Spacing, MinimumTiles, NodeRadius, MaxConnections: Integer;
    WallCrossings, Disabled: Boolean;
  end;

function TWebGraphGenerator.Hash(): String;
var
  data: TIntegerArray;
begin
  data := [Self.Spacing, Self.MinimumTiles, Self.NodeRadius, Self.MaxConnections, Integer(Self.WallCrossings)];
  Result := HashData(EHashAlgo.CRC32, @data[0], Length(data));
end;


function TWebGraphGenerator.GetDoorDirection(center: TPoint; img: TImage): TPoint;
var
  imgBounds: TBox;
  
  function SafePixelCheck(x, y: Integer): Boolean;
  begin
    Result := imgBounds.Contains([x, y]) and (img.Pixel[x, y] = $0000FF);
  end;
  
begin
  imgBounds := [0, 0, img.Width - 1, img.Height - 1];
  
  if SafePixelCheck(center.X+1, center.Y) or SafePixelCheck(center.X-1, center.Y) then
    Exit([0,1]);

  if SafePixelCheck(center.X, center.Y+1) or SafePixelCheck(center.X, center.Y-1) then
    Exit([1,0]);

  if SafePixelCheck(center.X+1, center.Y+1) or SafePixelCheck(center.X-1, center.Y-1) then
    Exit([1,-1]);

  if SafePixelCheck(center.X+1, center.Y-1) or SafePixelCheck(center.X-1, center.Y+1) then
    Exit([1,1]);

  Result := [0,0];
end;

function TWebGraphGenerator.DoorIsSeparating(center, direction: TPoint; img: TImage): Boolean;
var
  inverted: TPoint;
  i: Integer;
  splitA, splitB: Boolean;
  testPoint: TPoint;
  imgBounds: TBox;
begin
  imgBounds := [0, 0, img.Width - 1, img.Height - 1];
  inverted := direction.Rotate(HALF_PI, [0,0]);

  for i := 1 to 5 do
  begin
    testPoint := Point(center.X, center.Y) + (inverted*i);
    
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitA := True;
          Break;
        end;
      end;
    end;

    testPoint := Point(testPoint.X - direction.X, testPoint.Y - direction.Y);
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitA := True;
          Break;
        end;
      end;
    end;

    testPoint := Point(center.X, center.Y) + (inverted*i) + direction;
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitA := True;
          Break;
        end;
      end;
    end;
  end;

  for i := 1 to 5 do
  begin
    testPoint := Point(center.X, center.Y) - (inverted * i);
    
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitB := True;
          Break;
        end;
      end;
    end;

    testPoint := Point(testPoint.X - direction.X, testPoint.Y - direction.Y);
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitB := True;
          Break;
        end;
      end;
    end;

    testPoint := Point(center.X, center.Y) - (inverted * i) + direction;
    if imgBounds.Contains(testPoint) then
    begin
      case img.Pixel[testPoint.X, testPoint.Y] of
        $333333, $0:
        begin
          splitB := True;
          Break;
        end;
      end;
    end;
  end;

  Result := splitA and splitB;
end;


function TWebGraphGenerator.FindDoors(doors: T2DPointArray; img: TImage): TRSDoorArray;
var
  direction: TPoint;
  i, idx: Integer;
  doorType: ERSDoorType;
  imgBounds: TBox;
  centerPt, testPt1, testPt2: TPoint;
begin
  imgBounds := [0, 0, img.Width - 1, img.Height - 1];
  
  for i := 0 to High(doors) do
  begin
    case Length(doors[i]) of
      4: doorType := ERSDoorType.NORMAL;
      8: doorType := ERSDoorType.WIDE;
      else Continue;
    end;

    centerPt := doors[i].Mean();
    direction := Self.GetDoorDirection(centerPt, img);
    if direction = [0,0] then Continue;

    testPt1 := Point(centerPt.X + direction.X, centerPt.Y + direction.Y);
    testPt2 := Point(centerPt.X - direction.X, centerPt.Y - direction.Y);
    
    if not imgBounds.Contains(testPt1) or not imgBounds.Contains(testPt2) then
      Continue;
      
    if (img.Pixel[testPt1.X, testPt1.Y] <> $FFFFFF) or
       (img.Pixel[testPt2.X, testPt2.Y] <> $FFFFFF) then
      Continue;

    if not Self.DoorIsSeparating(centerPt, direction, img) then Continue;

    idx := Length(Result);
    SetLength(Result, idx+1);
    Result[idx].Center := centerPt;
    Result[idx].DoorType := doorType;
    Result[idx].Direction := direction;
    Result[idx].Before := Result[idx].Center + direction * 2;
    Result[idx].After  := Result[idx].Center - direction * 2;
  end;
end;



(*
## TRSChunkLoader.BuildGraph
```pascal
function TRSChunkLoader._BuildGraph(map: TMufasaBitmap; white, red: TPointArray): TWebGraphV2;
function TRSChunkLoader.BuildGraph(name: String; map: TMufasaBitmap): TWebGraphV2;
```
Magically builds a webgraph for you for a given collision map passed into `map`.

The collision map can only have 4 colors:
- white ($FFFFFF) for walkable space
- black ($000000) for non walkable space
- red ($0000FF) for doors (optional)
- gray ($333333) for objects (optional)

```{note}
This is an internal method. Don't use it if you don't know what you are doing.
```
*)
function TWebGraphGenerator._BuildGraph(map: TImage; white, red: TPointArray): TWebGraph;
var
  i, j, d, len, hi, minLen: Integer;
  cluster, skeleton, nodes, tpa: TPointArray;
  bounds: TBox;
  p, q: TPoint;
  nodesTree: TKDPointTree;
  doors: TRSDoorArray;
  connectionMap: array of TIntegerMatrix;
  connectedDoors: array of record
    After, Before: Boolean;
  end;
  connectA, connectB: TIntegerArray;
begin
  doors := Self.FindDoors(red.Cluster(1), map);
  SetLength(connectedDoors, Length(doors));
  with white.Bounds() do
    SetLength(connectionMap, X2, Y2);

  minLen := Self.MinimumTiles * RSTranslator.TileArea;

  for cluster in white.Cluster(1).SortBySize(True) do
  begin
    if Length(cluster) <= minLen then Continue; //remove very small spaces

    hi := High(connectedDoors);
    connectA := [];
    connectB := [];

    for d := 0 to hi do
    begin
      if connectedDoors[d].After and connectedDoors[d].Before then
      begin
        Result.Doors += doors[d];
        Delete(doors, d, 1);
        Delete(connectedDoors, d, 1);
        hi -= 1;
        d -= 1;
        Continue;
      end;

      if not connectedDoors[d].After and cluster.Contains(doors[d].After) then
        connectA += d;
      if not connectedDoors[d].Before and cluster.Contains(doors[d].Before) then
        connectB += d;
    end;

    bounds := cluster.Bounds();

    if Max(bounds.Width, bounds.Height) < Self.NodeRadius then
    begin
      Result.Nodes += cluster.Median(); //mark spaces less than NodeRadius with a single node and continue

      for d in connectA do
      begin
        Result.Nodes += doors[d].After;
        connectedDoors[d].After := True;
        SetLength(Result.Paths, Length(Result.Nodes));
        Result.Paths[High(Result.Nodes)-1] := [High(Result.Nodes)];
        Result.Paths[High(Result.Nodes)] := [High(Result.Nodes)-1];
      end;

      for d in connectB do
      begin
        Result.Nodes += doors[d].Before;
        connectedDoors[d].Before := True;
        SetLength(Result.Paths, Length(Result.Nodes));
        Result.Paths[High(Result.Nodes)-1] := [High(Result.Nodes)];
        Result.Paths[High(Result.Nodes)] := [High(Result.Nodes)-1];
      end;

      Continue;
    end;

    skeleton := cluster.Erode(1).Skeleton(2, 6);

    nodes := [];
    for tpa in skeleton.Partition(Self.Spacing) do
      nodes += tpa.Cluster(1.5).Means();

    for d in connectA do
    begin
      nodes += doors[d].After;
      connectedDoors[d].After := True;
    end;

    for d in connectB do
    begin
      nodes += doors[d].Before;
      connectedDoors[d].Before := True;
    end;

    nodesTree := TKDPointTree.Create(nodes);

    len := Length(Result.Nodes);
    SetLength(Result.Nodes, len + Length(nodes));
    SetLength(Result.Paths, Length(Result.Nodes));

    for i := 0 to High(nodesTree.Data) do
    begin
      p := nodesTree.Data[i].Split;
      for q in nodesTree.KNearest(p, Self.MaxConnections, True) do
      begin
        if Max(Abs(p.X - q.X), Abs(p.Y - q.Y)) > Self.Spacing * 2 then Continue;

        if map.ColorsInLine(p, q, [$0, $FF, $333333]) then
          if map.ColorsInLineEx(p, q, [$0, $FF, $333333]) then
            if cluster.ExtractBox(TBox.Create(Min(p.X, q.X), Min(p.Y, q.Y), Max(p.X, q.X), Max(p.Y, q.Y)).Expand(Self.Spacing div 2)).AStar(p, q, True) = [] then
              Continue;

        connectionMap[q.X, q.Y] += i;
      end;
    end;

    for i := 0 to High(nodesTree.Data) do
    begin
      p := nodesTree.Data[i].Split;
      Result.Nodes[i+len] := p;
      for j in connectionMap[p.X, p.Y] do
      begin
        Result.Nodes[j+len] := nodesTree.Data[j].Split;

        if not Result.Paths[i+len].Contains(j+len) then
          Result.Paths[i+len] += j+len;
        if not Result.Paths[j+len].Contains(i+len) then
          Result.Paths[j+len] += i+len;
      end;
    end;
  end;

  SetLength(Result.Names, Length(Result.Nodes));
end;


function TWebGraphGenerator.BuildGraph(name: String; map: TImage): TWebGraph;
var
  white, red, gray, merged, graySubset: TPointArray;
  whiteClusters, grayClusters, mergedClusters: T2DPointArray;
  i, j: Integer;
  t: UInt64;
begin
  white := map.FindColor($FFFFFF, 0);
  red := map.FindColor($0000FF, 0);

  WriteLn GetDebugLn('Generating webgraph for region: ' + name);

  t := GetTimeRunning();
  Result := Self._BuildGraph(map, white, red);

  gray := map.FindColor($333333, 0);

  whiteClusters := white.Cluster(1);
  grayClusters := gray.Cluster(1);

  for i := 0 to High(whiteClusters) do
  begin
    if Length(whiteClusters[i]) <= 6 then Continue;

    graySubset := gray.ExtractBox(whiteclusters[i].Bounds().Expand(80));
    merged := whiteclusters[i] + graySubset;
    mergedClusters := merged.Cluster(1);

    for j := 0 to high(mergedClusters) do
      if mergedClusters[j].Contains(whiteClusters[i][0]) then
      begin
        Result.WalkableClusters += mergedClusters[j];
        Break;
      end;
  end;

  Result.WalkableSpace    := white + gray;
  Result.ObjectClusters   := grayClusters;

  WriteLn GetDebugLn('WebGraphGenerator', 'Generating webgraph took ' + ToStr(Round(((GetTimeRunning()-t)/1000), 2)) + ' seconds.', ELogLevel.SUCCESS);
end;

var
(*
## WebGraphGenerator variable
Global {ref}`TWebGraphGenerator` variable.
*)
  WebGraphGenerator: TWebGraphGenerator;

begin
  WebGraphGenerator.Spacing := 18;         //low = better/slower, high = worse/faster
  WebGraphGenerator.MinimumTiles := 4;     //spaces with less tiles than this will be ignored.
  WebGraphGenerator.NodeRadius := 50;      //closed space less than NodeRadius pixels will have a single node.
  WebGraphGenerator.MaxConnections := 6;   //Max connections per node.
  WebGraphGenerator.WallCrossings := True; //Whether to allow wall crossings when there's a close path around it.
end;
