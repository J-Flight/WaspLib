(*
# Recorder
Records video in separate thread for debugging script crashes and issues.

Basic usage:
```pascal
WLRecorder.Start(30); // Start recording with 30 second buffer
WLRecorder.Start(30, 'MyVideos'); // Custom save directory
// Videos are automatically saved on script end
```
*)

{$DEFINE WL_RECORDER_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}
{$loadlib Plugins/wasp-plugins/librecorder/librecorder}

type
(*
## type TSimbaRecorder
```pascal
TSimbaRecorder = record
  Thread: TThread;
  Running: Boolean;
  BufferSeconds: Integer;
  VideoPath: String;
  RecorderTarget: TTarget;
end;
```
Manages video recording with buffered capture.
*)
  TSimbaRecorder = record
    Thread: TThread;
    Running: Boolean;
    BufferSeconds: Integer;
    VideoPath: String;
    RecorderTarget: TTarget;
  end;

var
  WLRecorder: TSimbaRecorder;
  WLRecorder_CurrentFrame: TImage;  
  WLRecorder_FrameCount: Integer;

{%codetools off}
 // Captures frame from target
function WLRecorder_GetFrame({$H-}Sender: TRecorder){$H+}: Pointer;
begin
  if Assigned(WLRecorder_CurrentFrame) then
    WLRecorder_CurrentFrame.Destroy();

  WLRecorder_CurrentFrame := WLRecorder.RecorderTarget.GetImage();
  Inc(WLRecorder_FrameCount);

  Result := WLRecorder_CurrentFrame.Data;
end;

// Checks if recording should stop
function WLRecorder_GetTerminated({$H-}Sender: TRecorder){$H+}: Boolean; 
begin
  Result := not WLRecorder.Running;
end;

// Cleanup on script termination
procedure WLRecorder_Free();
begin
  WLRecorder.Running := False;
  if Assigned(WLRecorder.Thread) then
  begin
    WLRecorder.Thread.WaitForTerminate();
    WLRecorder.Thread.Free();
    WLRecorder.Thread := nil;
  end;

  if Assigned(WLRecorder_CurrentFrame) then
  begin
    WLRecorder_CurrentFrame.Destroy();
    WLRecorder_CurrentFrame := nil;
  end;
  
  WLRecorder.RecorderTarget := nil;
end;
{%codetools on}

begin
  AddOnTerminate(@WLRecorder_Free);
end;

// Thread execution handler
procedure TSimbaRecorder._Execute; 
var
  Recorder: TRecorder;
begin
  try
    Recorder := TRecorder.Create(Self.BufferSeconds,Self.VideoPath, Self.RecorderTarget.Width, Self.RecorderTarget.Height,
      @WLRecorder_GetFrame,
      @WLRecorder_GetTerminated
    ); 
    // Fixed 15 FPS recording rate (DLL limitation)
    Recorder.SetFFMPEG('Plugins/wasp-plugins/librecorder/ffmpeg.exe');
    Recorder.Run(False);
    Recorder.Free();
  except
    WriteLn('[Recorder] Error: ', GetExceptionMessage());
  end;
end;

(*
## TSimbaRecorder.Start
```pascal
procedure TSimbaRecorder.Start(BufferSeconds: Integer = 30; VideoPath: String = 'Videos');
```
Starts recording with specified buffer duration and save path.
*)
procedure TSimbaRecorder.Start(BufferSeconds: Integer = 30; VideoPath: String = 'Videos');
begin
  if Self.Running then
    Exit;
  
  if not DirExists(VideoPath) then
    DirCreate(VideoPath);
  
  Self.BufferSeconds := BufferSeconds;
  Self.VideoPath := VideoPath;
  Self.Running := True;
  
  // Create separate target for recorder thread
  Self.RecorderTarget := new TTarget();
  Self.RecorderTarget.SetWindow(GetSimbaTargetWindow());
  
  Self.Thread := TThread.Create(@Self._Execute);
  Self.Thread.Name := 'RecorderThread_' + IntToStr(Time());
end;

(*
## TSimbaRecorder.IsRecording
```pascal
function TSimbaRecorder.IsRecording(): Boolean;
```
Returns true if currently recording.
*)
function TSimbaRecorder.IsRecording(): Boolean;
begin
  Result := Self.Running;
end;
