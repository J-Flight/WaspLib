(*
# WaspClient
Client to interact with https://waspscripts.com
*)

{$DEFINE WL_WASPCLIENT_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

(*
## TWaspClient
Responsible for connecting to waspscripts.com database.
*)
type
  TWaspClient = record
    Script: record
      ID: String;
      Revision: UInt32;
    end;

    Client: THTTPClient;
    Server, APIKey, Schema: String;

    User: record
      ID: String;
      Username: String;
      RefreshToken: String;
    end;

    Lock: TLock;
    IsSetup: Boolean;
  end;

procedure TWaspClient.Free();
begin
  Self.IsSetup := False;
  Async.ScheduleStop('WaspClient-RefreshToken');
end;

procedure TWaspClient.RefreshSession();
var
  payload, response: String;
  json: TJSONParser;
begin
  if not Self.Lock.TryEnter() then Exit;
  if not Self.IsSetup then Exit;

  payload := '{"refresh_token": "' + Self.User.RefreshToken + '"}';

  response := Self.Client.Post(
    Self.Server + 'auth/v1/token?grant_type=refresh_token', payload
  );

  if Self.Client.ResponseStatus <> EHTTPStatus.OK then
  begin
    WriteLn GetDebugLn('WaspClient', response);
    Self.IsSetup := False;
    Self.Lock.Leave();
    Exit;
  end;

  json := new TJSONParser();
  json.Parse(response);

  Self.Client.RequestHeader['Authorization'] := 'Bearer ' + json.Item['access_token'].AsString;
  Self.User.RefreshToken := json.Item['refresh_token'].AsString;

  {$IFDEF WASP_REFRESH_TOKEN}
  WriteLn GetDebugLn('WaspClient', 'Refresh token was refreshed: ' + Self.User.RefreshToken);
  {$ENDIF}

  with json.Item['user'] do
  begin
    Self.User.ID := Item['id'].AsString;
    Self.User.Username := Item['user_metadata'].Item['custom_claims'].Item['global_name'].AsString;
  end;
  Self.Lock.Leave();
end;

procedure TWaspClient.Setup();
begin
  Self.Server := 'https://db.waspscripts.dev/';
  Self.APIKey := 'eyJ0eXAiOiJKV1QiLCJhbGciOiJIUzI1NiJ9.eyJpc3MiOiJzdXBhYmFzZSIsImlhdCI6MTc1MTA0MTIwMCwiZXhwIjo0OTA2NzE0ODAwLCJyb2xlIjoiYW5vbiJ9.C_KW5x45BpIyOQrnZc7CKYKjHe0yxB4l-fTSC4z_kYY';

  Self.Client := new THTTPClient();
  Self.Client.RequestHeader['Content-Type'] := 'application/json';
  Self.Client.RequestHeader['apikey'] := Self.APIKey;

  {$IFDEF WASP_REFRESH_TOKEN}
    Self.User.RefreshToken := {$MACRO WASP_REFRESH_TOKEN};
  {$ELSE}
    Self.User.RefreshToken := GetEnvVar('WASP_REFRESH_TOKEN');
  {$ENDIF}

  {$IFDEF SCRIPT_ID}
    Self.Script.ID := {$MACRO SCRIPT_ID};
  {$ELSE}
    Self.Script.ID := GetEnvVar('SCRIPT_ID');
  {$ENDIF}

  {$IFDEF SCRIPT_REVISION}
    Self.Script.Revision := StrToInt({$MACRO SCRIPT_REVISION}, 0);
  {$ELSE}
    Self.Script.Revision := StrToInt(GetEnvVar('SCRIPT_REVISION'), 0);
  {$ENDIF}

  if Self.User.RefreshToken = '' then Exit;

  Self.Lock := TLock.Create();
  Self.IsSetup := True;
  Async.ScheduleEvery('WaspClient-RefreshToken', @Self.RefreshSession, 5 * ONE_MINUTE);
  AddOnTerminate(@Self.Free);
  AddOnTerminate(@Self.Lock.Free);
end;

function TWaspClient.SetSchema(schema: String = 'public'): Boolean;
begin
  if not Self.Lock.TryEnter() then Exit;
  if schema = 'public' then
    Self.Client.RequestHeader['Accept-Profile'] := ''
  else
    Self.Client.RequestHeader['Accept-Profile'] := schema;
  Self.Lock.Leave();
  Result := True;
end;

function TWaspClient.CheckScriptAccess(): Boolean;
var
  url: String;
begin
  if not Self.Lock.TryEnter() then Exit;
  if not Self.Script.ID.IsUUID() then Exit;

  url := Self.Server + 'storage/v1/object/authenticated/scripts/' +
         Self.Script.ID + '/000000001/script.simba';

  Result := Self.Client.Head(url) = EHTTPStatus.OK;
  Self.Lock.Leave();
end;

var
  WaspClient: TWaspClient;
