(*
# WebGraph
Responsible for handling webwalking or any other graph logic you may need.

It's partially based on the original webgraph logic by [slacky](https://slacky.one/)
but it was written from scratch by Torwent.
*)

{$DEFINE WL_WEBGRAPH_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

{$R-}
type
(*
## EGraphNode
```pascal
EGraphNode = enum(NORMAL, NAMED, DOOR, LADDER, AGILITY, OBSTACLE, OTHER, BLOCKED);
```
Enum representing the type of nodes in a `TWebGraph`.
*)
  EGraphNode = enum(
    NORMAL, DOOR, LADDER, SHORTCUT, OBSTACLE, TRANSPORT, TELEPORT, OTHER,
    BLOCKED
  );

  TGraphNode = record
    Node: TPoint;
    Typ: EGraphNode;
  end;

function TGraphNode.Create(pt: TPoint; typ: EGraphNode = EGraphNode.NORMAL): TGraphNode; static;
begin
  Result.Node := pt;
  Result.Typ := typ;
end;

type
  TGraphNodeArray = array of TGraphNode;

function TGraphNodeArray.Offset(pt: TPoint): TGraphNodeArray;
var
  i: Integer;
begin
  for i := 0 to High(Self) do
    Result += [Self[i].Node.Offset(pt), Self[i].Typ];
end;

function TGraphNodeArray.IndexOf(value: TPoint): Integer; overload;
begin
  for Result := 0 to High(Self) do
    if Self[Result].Node = value then
      Exit;
  Result := -1;
end;

function TGraphNodeArray.NearestIndex(pt: TPoint): Integer;
var
  i: Integer;
  dist, best: Double;
begin
  best := $FFFFFF;
  for i := 1 to High(Self) do
  begin
    dist := Self[i].Node.DistanceTo(pt);

    if (dist < best) then
    begin
      best := dist;
      Result := i;
    end;
  end;
end;

function TGraphNodeArray.PolylineLength(): Double;
var
  i: Integer;
begin
  if Length(Self) < 2 then Exit;
  for i := 0 to High(Self)-1 do
    Result += Self[i].Node.DistanceTo(Self[i+1].Node);
end;

type
  TWebGraph = record
    Nodes: TGraphNodeArray;
    Paths: T2DIntegerArray;

    WalkableSpace: TPointArray;
    WalkableClusters, ObjectClusters: T2DPointArray;
    Tree: TKDPointTree;
  end;

procedure TWebGraph.Setup();
var
  tpa: TPointArray;
  node: TGraphNode;
begin
  for node in Self.Nodes do
    tpa += node.Node;
  Self.Tree := TKDPointTree.Create(tpa);
end;

function TWebGraph.Copy(): TWebGraph;
begin
  Result.Nodes := System.Copy(Self.Nodes);
  Result.WalkableSpace := System.Copy(Self.WalkableSpace);
  Result.WalkableClusters := System.Copy(Self.WalkableClusters);
  Result.ObjectClusters := System.Copy(Self.ObjectClusters);
  Result.Setup();
end;

procedure TWebGraph.BlockArea(area: TBox; inside: Boolean = True);
var
  i: Integer;
begin
  if inside then
  begin
    for i := 0 to High(Self.Nodes) do
      if area.Contains(Self.Nodes[i].Node) then
        Self.Nodes[i].Typ := EGraphNode.BLOCKED;
    Exit;
  end;

  for i := 0 to High(Self.Nodes) do
    if not area.Contains(Self.Nodes[i].Node) then
      Self.Nodes[i].Typ := EGraphNode.BLOCKED;
end;

function TWebGraph.NearestNodeIndex(pt: TPoint): Integer;
var
  q: TPoint;
begin
  q := Self.Tree.Nearest(pt, False);
  if not Self.WalkableClusters.InSameTPA(pt, q) then
    raise GetDebugLn('WebGraph', 'No near nodes were found around point ' + ToStr(pt));
  Result := Self.Nodes.IndexOf(q);
end;

function TWebGraph.NearNodesIndices(pt: TPoint; amount: Integer): TIntegerArray;
var
  i: Integer;
  weights: TDoubleArray;
  tpa: TPointArray;
begin
  tpa := Self.Tree.KNearest(pt, amount);

  for i := 0 to High(tpa) do
    if Self.WalkableClusters.InSameTPA(pt, tpa[i]) then
    begin
      Result += Self.Nodes.IndexOf(tpa[i]);
      weights += pt.DistanceTo(tpa[i]);
    end;

  if Result = [] then
    raise GetDebugLn('WebGraph', 'No near nodes were found around point ' + ToStr(pt));

  Result.Sort(weights, True);
end;

{$R+}

function TWebGraph.FindPath(start, finish: Integer; rnd: Double = 0): TIntegerArray;
type
  TNode = record
    Indices: TIntegerArray;
    Score: Double;
  end;
var
  queue: array of TNode;
  visited: TBooleanArray;
  cIdx, pathIdx, i: Integer;
  current, node: TNode;
  p, q: TPoint;
  hyp: Double;

  function GetNextShortest(): TNode;
  var i, node: Integer;
  begin
    Result := queue[0];
    for i := 1 to High(queue) do
      if queue[i].Score < Result.Score then
      begin
        node   := i;
        Result := queue[i];
      end;
    Delete(queue, node, 1);
  end;
begin
  queue   := [[[start],0]];
  SetLength(visited, Length(Self.Nodes));

  while Length(queue) <> 0 do
  begin
    current := GetNextShortest();
    cIdx := current.Indices[High(current.Indices)];
    if (Self.Nodes[cIdx].Typ = EGraphNode.BLOCKED) or visited[cIdx] then
      Continue;
    visited[cIdx] := True;

    if (cIdx = finish) then
      Exit(current.Indices);

    p := Self.Nodes[cIdx].Node;
    for pathIdx in Self.Paths[cIdx] do
    begin
      if not visited[pathIdx] then
      begin
        q := Self.Nodes[pathIdx].Node;
        node.Indices := current.Indices + pathIdx;

        hyp := Hypot(p.x-q.x, p.y-q.y);
        node.Score   := current.Score + hyp + (hyp*Random()*Rnd-Rnd/2);
        queue += node;
      end;
    end;
  end;
end;

function TWebGraph.NearestWalkablePoint(p: TPoint): TPoint;
var
  pArea, obj, tpa, bestObj: TPointArray;
  pAreaClusters: T2DPointArray;
  i, bestLength, tempLength: Integer;
begin
  pArea := TPointArray.CreateFromCircle(p, 20, True).Intersection(Self.WalkableSpace);
  pAreaClusters := pArea.Cluster(1);

  if Self.ObjectClusters = [] then Exit(pArea.NearestPoint(p));

  obj := [];
  for i := 0 to High(Self.ObjectClusters) do
  begin
    if not Self.ObjectClusters[i].Bounds().Contains(p) then Continue;
    if Self.ObjectClusters[i].Contains(p) then
    begin
      obj := Self.ObjectClusters[i];
      {$IFDEF DEBUG_WALKER}
      WriteLn('Point was inside object! with edges: ', obj);
      {$ENDIF}
      Break;
    end;
  end;

  if obj = [] then Exit(pArea.NearestPoint(p));

  for i := 0 to High(pAreaClusters) do
  begin
    tpa := pAreaClusters[i].Intersection(obj);
    tempLength := Length(tpa);
    if tempLength > bestLength then
    begin
      bestLength := tempLength;
      bestObj := tpa;
    end;
  end;
  Result := bestObj.NearestPoint(p);
end;

function TWebGraph.PathBetween(a, b: TPoint; rnd: Double = 0; attempts: Integer = 3): TGraphNodeArray;
var
  nodeA, nodeB: TGraphNode;
  i, j: Integer;
  nS, nG, indices: TIntegerArray;
begin
  if not Self.WalkableSpace.Contains(a) or not Self.WalkableSpace.Contains(b) then
  begin
    a := RSTranslator.NormalizeDoor(a);
    b := RSTranslator.NormalizeDoor(b);

    if not Self.WalkableSpace.Contains(a) then
      a := Self.NearestWalkablePoint(a);
    if not Self.WalkableSpace.Contains(b) then
      b := Self.NearestWalkablePoint(b);
  end;

  nS := Self.NearNodesIndices(a, attempts);
  nG := Self.NearNodesIndices(b, attempts);

  nodeA := [a, EGraphNode.NORMAL];
  nodeB := [b, EGraphNode.NORMAL];
  if nS[0] = nG[0] then
    Exit([nodeA, nodeB]);

  if (Length(nG) = 1) and (Self.Paths[nG[0]] = []) then
    raise GetDebugLn('WebGraph', 'Points ' + ToStr(a) + ' and ' + ToStr(b) + ' don''t connect!');

  for i := 0 to High(nS) do
    for j := 0 to High(nG) do
    begin
      if nS[i] = nG[j] then
        Continue;
      indices := Self.FindPath(nS[i],nG[j], rnd);
      if Length(indices) > 0 then
        Break(2);
    end;

  if Length(indices) = 0 then
    raise GetDebugLn('WebGraph', 'Points ' + ToStr(a) + ' and ' + ToStr(b) + ' don''t connect!');

  Result += nodeA;
  for i in indices do
    Result += Self.Nodes[i];
  Result += nodeB;
end;


function TWebGraph.PathToNear(a: TPoint; out b: TPoint; rnd: Double = 0): TGraphNodeArray;
var
  i: Integer;
  cluster: TPointArray;
  best: TIntegerArray;
begin
  for i := 0 to High(Self.WalkableClusters) do
    if Self.WalkableClusters[i].Contains(a) then
    begin
      cluster := Self.WalkableClusters[i];
      Break;
    end;

  if cluster = [] then
    raise GetDebugLn('WebGraph', 'Can''t find player cluster containg: ' + ToStr(a));

  best := Self.NearNodesIndices(b, 12);
  for i := 0 to High(best) do
  begin
    b := Self.Nodes[best[i]].Node;
    if cluster.Contains(b) then
      Exit(Self.PathBetween(a, b, rnd, 2));
  end;
end;


function TWebGraph.AddNode(pt: TPoint; typ: EGraphNode; fromNode: Integer): Boolean;
var
  i: Integer;
begin
  if fromNode < 0 then
    Exit(False);

  i := Length(Self.Nodes);
  SetLength(Self.Nodes, i+1);
  SetLength(Self.Paths, i+1);
  Self.Nodes[i] := [pt, typ];

  Self.Paths[fromNode] += i;
  Self.Paths[i] += fromNode;
  Result := True;
end;


function TWebGraph.ConnectNodes(a,b: Integer): Boolean;
begin
  if b in Self.Paths[a] then
  begin
    Self.Paths[a].Remove(b);
    Self.Paths[b].Remove(a);
  end else
  begin
    Self.Paths[a] += b;
    Self.Paths[b] += a;
  end;

  Result := True;
end;

function TWebGraph.ConnectNodes(a,b: TPoint): Boolean; overload;
var
  i, j: Integer;
begin
  i := Self.Nodes.IndexOf(a);
  if i = -1 then Exit;

  j := Self.Nodes.IndexOf(b);
  if j = -1 then Exit;

  Result := Self.ConnectNodes(i,j);
end;

function TWebGraph.ConnectNodes(a: Integer; b: TPoint): Boolean; overload;
var
  i: Integer;
begin
  i := Self.Nodes.IndexOf(b);
  if i = -1 then Exit;

  Result := Self.ConnectNodes(a, i);
end;

function TWebGraph.ConnectNodesEx(a,b: TPoint): Boolean;
var
  i, j: Integer;
begin
  i := Self.NearestNodeIndex(a);
  j := Self.NearestNodeIndex(b);
  Result := Self.ConnectNodes(i,j);
end;


function TWebGraph.DeleteNode(node: Integer): Integer;
var
  i,j,n,curr: Integer;
  marked: TIntegerArray;
begin
  marked += node;
  repeat
    curr := marked.Pop;

    for n in Self.Paths[curr] do
    begin
      Self.Paths[n].Remove(curr);
      if Self.Paths[n] = [] then
        marked += n;
    end;

    // offset remainding nodes
    for i:=0 to High(Self.Paths) do
      for j:=0 to High(Self.Paths[i]) do
        if Self.Paths[i][j] > curr then
          Dec(Self.Paths[i][j]);

    for i:=0 to High(marked) do
      if marked[i] > curr then Dec(marked[i]);

    // remove the node itself
    Delete(Self.Paths, curr, 1);
    Delete(Self.Nodes, curr, 1);
    Result += 1;
  until Length(marked) = 0;
end;

procedure TWebGraph.DeleteNodes(b: TBox);
var
  i: Integer;
begin
  for i := High(Self.Nodes) downto 0 do
    if b.Contains(Self.Nodes[i].Node) then
      Self.DeleteNode(i);
end;



procedure TWebGraph.Merge(graph: TWebGraph);
var
  i, j, len: Integer;
begin
  len := Length(Self.Nodes);

  for i := 0 to High(graph.Nodes) do
  begin
    Self.Nodes += graph.Nodes[i];
    Self.Paths += graph.Paths[i];

    for j := 0 to High(Self.Paths[len+i]) do
      Self.Paths[len+i,j] += len;
  end;

  Self.WalkableSpace    += graph.WalkableSpace;
  Self.WalkableClusters += graph.WalkableClusters;
  Self.ObjectClusters   += graph.ObjectClusters;
end;


procedure TWebGraph.Draw(img: TImage);
var
  i,j: Integer;
begin
  if Self.Nodes = [] then Exit;

  img.DrawColor := $00EAEA;
  for i := 0 to High(Self.Paths) do
    for j := 0 to High(Self.Paths[i]) do
      img.DrawLine(Self.Nodes[i].Node, Self.Nodes[Self.Paths[i, j]].Node);

  for i := 0 to High(Self.Nodes) do
  begin
    case Self.Nodes[i].Typ of
      EGraphNode.DOOR:  img.DrawColor := $FF105F;
      EGraphNode.TELEPORT: img.DrawColor := $FF7F00;
      EGraphNode.NORMAL: img.DrawColor := $0101CC;
      else img.DrawColor := $FF00FF;
    end;
    img.DrawBox(TBox.Create(Self.Nodes[i].Node, 1, 1));
  end;

end;

procedure TWebGraph.Draw(canvas: TImageBoxCanvas); overload;
var
  i,j: Integer;
  color: TColor;
begin
  if Self.Nodes = [] then Exit;

  for i := 0 to High(Self.Paths) do
    for j := 0 to High(Self.Paths[i]) do
    begin
      if Self.Paths[i, j] > High(Self.Nodes) then Continue;
      canvas.DrawLine(Self.Nodes[i].Node, Self.Nodes[Self.Paths[i, j]].Node, $00EAEA);
    end;

  for i := 0 to High(Self.Nodes) do
  begin
    case Self.Nodes[i].Typ of
      EGraphNode.DOOR: color := $FF105F;
      EGraphNode.TELEPORT: color := $FF7F00;
      EGraphNode.NORMAL: color := $0101CC;
      else color :=  $FF00FF;
    end;
    canvas.DrawBox(TBox.Create(Self.Nodes[i].Node, 1, 1), color);
  end;
end;


