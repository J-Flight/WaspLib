{$DEFINE SRLT_KEYBOARD_INCLUDED}
{$INCLUDE_ONCE SRLT/utils.simba}

type
  EKeycodeArray = array of EKeyCode;

  TKeyboard = record
    LastKey: EKeyCode;
    const ARROWS: EKeycodeArray = [EKeyCode.LEFT, EKeycode.UP, EKeycode.Right, EKeycode.DOWN];
    const FKEYS: EKeycodeArray = [EKeyCode.F1, EKeycode.F2, EKeycode.F3, EKeycode.F4, EKeycode.F5, EKeycode.F6, EKeycode.F7, EKeycode.F8, EKeycode.F9, EKeycode.F10, EKeycode.F11, EKeycode.F12];
    const MISC: EKeycodeArray = [EKeyCode.LCONTROL, EKeyCode.CONTROL, EKeyCode.SPACE, EKeyCode.SHIFT, EKeyCode.RSHIFT];
  end;

procedure TKeyboard.Setup();
begin
  Target.KeyOptions.MinPressTime := 30;
  Target.KeyOptions.MaxPressTime := 60;
end;

(*
### Keyboard.KeyDown
```pascal
procedure TKeyboard.KeyDown(key: EKeyCode);
```
Presses down the given virtual key.
*)
procedure TKeyboard.KeyDown(key: EKeyCode);
begin
  Target.KeyDown(key);
  Self.LastKey := key;
end;

(*
### Keyboard.KeyUp
```pascal
procedure TKeyboard.KeyUp(key: EKeyCode);
```
Releases the given virtual key.
*)
procedure TKeyboard.KeyUp(key: EKeyCode);
begin
  Target.KeyUp(key);
end;

(*
### Keyboard.PressKey
```pascal
procedure TKeyboard.PressKey(key: Integer);
```
Presses the given virtual key.
*)
procedure TKeyboard.PressKey(key: EKeyCode);
begin
  Target.KeyPress(key);
  Self.LastKey := key;
end;


(*
### Keyboard.IsKeyDown
```pascal
procedure TKeyboard.IsKeyDown(Key: EKeyCode);
```
Returns True if the given virtual key is currently down.
*)
function TKeyboard.IsKeyDown(key: EKeyCode): Boolean;
begin
  Result := Target.KeyPressed(key);
end;

(*
### Keyboard.Send
```pascal
procedure TKeyboard.Send(text: String);
procedure TKeyboard.Send(text: String; keyPressAfter: EKeyCode); overload;
```
Writes the given text to whatever window is currently in focus.

First overload allows you to pass KeyWait, KeyModWait, and KeyPressWait, all are timing modifiers.
Last overload allows you to pass a long a key to be pressed after the text has been typed, for example ``VK_ENTER``.
*)
procedure TKeyboard.Send(text: String);
var
  i: Integer;
begin
  for i := 1 to Length(text) do
  begin
    Target.KeySend(text[i]);
    Sleep(Random(Target.KeyOptions.MinPressTime+10, Target.KeyOptions.MaxPressTime+25));
  end;

  Self.LastKey := Target.KeyCodeFromChar(text[Length(text)]);
end;

procedure TKeyboard.Send(text: String; keyPressAfter: EKeyCode); overload;
begin
  Self.Send(text);
  Sleep(30, 120, ERandomDir.LEFT);
  Self.PressKey(keyPressAfter);
end;


procedure TKeyboard.RandomKey();
begin
  case Random(100) of
    0..74:
    case Random(3) of
      0: Self.PressKey(EKeyCode.LEFT);
      1: Self.PressKey(EKeyCode.RIGHT);
      2: Self.PressKey(EKeyCode.UP);
    end;
    75..89: Self.PressKey(Self.FKEYS.Random());
    90..99: Self.PressKey(Self.MISC.Random());
  end;
end;


var
  Keyboard: TKeyboard;

begin
  Keyboard.Setup();
end;
