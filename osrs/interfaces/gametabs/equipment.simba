(*
# Equipment
Methods to interact with the Equipment gametab.
```{figure} ../../images/equipment.png
```
*)
{$DEFINE WL_EQUIPMENT_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}


type
(*
## ERSEquipmentSlot
```pascal
ERSEquipment = (
  HEAD, SECONDARY_AMMO, CAPE, NECK, AMMO, WEAPON, BODY, SHIELD, LEGS, HANDS, FEET, RING
);
```
Enumerator of the equipment slots.
*)
  ERSEquipment = enum(
    HEAD, SECONDARY_AMMO, CAPE, NECK, AMMO, WEAPON, BODY, SHIELD, LEGS, HANDS, FEET, RING
  );

(*
## ERSEquipmentButton
```pascal
ERSEquipmentButton = (STATS, PRICES, DEATH, FOLLOWER);
```
Enumerator of the equipment buttons.
*)
  ERSEquipmentButton = enum(STATS, PRICES, DEATH, FOLLOWER);

(*
## TRSEquipment
Main record reponsible for handling the equipment gametab.
*)
  TRSEquipment = record
    Slots: TRSSlotInterface;
    Items: TRSItemInterface;
    SlotBoxes: TBoxArray;
    Buttons: array [ERSEquipmentButton] of TRSButton;
  end;

(*
## Equipment.SetupGameTab
```pascal
procedure TRSEquipment.SetupGameTab();
```
Internal method used to setup the {ref}`TRSEquipment` coordinates.

This is automatically called for you on the {ref}`Equipment variable`.
*)

function TRSEquipment.Open(): Boolean; forward;

procedure TRSEquipment.SetupGameTab();
var
  btns: TBoxArray;
  i: Integer;
begin
  Self.SlotBoxes := TBoxArray.Create(GameTab.TopLeft.Offset(33, 5), 3,2, 35, 35, [6, 4]) +
                    TBoxArray.Create(GameTab.TopLeft.Offset(18, 83), 3, 3, 35, 35, [21, 5]);

  Delete(Self.SlotBoxes, 0, 1);
  Delete(Self.SlotBoxes, 8, 1);
  Delete(Self.SlotBoxes, 9, 1);

  Self.Slots.Setup('Equipment.Slots', Self.SlotBoxes);
  Self.Items.Setup('Equipment.Items', @Self.Slots, [2, 11, 2, -2], @Self.Open);

  btns := TBoxArray.Create(GameTab.BottomLeft.Offset(4, -52), 4, 1, 39, 39, [6,0]);

  for i := 0 to 3 do
  begin
    Self.Buttons[i].EnabledColors := [];
    Self.Buttons[i].Bounds := btns[i];
  end;
end;

(*
## Equipment.IsOpen
```pascal
function TRSEquipment.IsOpen(): Boolean;
```
Returns True/False whether the equipment tab is open or not.

Example:
```pascal
WriteLn Equipment.IsOpen();
```
*)
function TRSEquipment.IsOpen(): Boolean;
begin
  Result := GameTabs.IsOpen(ERSGameTab.EQUIPMENT);
end;

(*
## Equipment.Open
```pascal
function TRSEquipment.Open(): Boolean;
```
Attempts to open the equipment tab.
Returns true if we succeed.

Example:
```pascal
WriteLn Equipment.Open();
```
*)
function TRSEquipment.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.EQUIPMENT);
end;

var
(*
## Equipment variable
Global {ref}`TRSEquipment` variable.
*)
  Equipment: TRSEquipment;
