(*
# Logout
Methods to interact with the logout tab.
```{figure} ../../images/inventory.png
```
*)
{$DEFINE WL_LOGOUT_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osrs.simba}
{$ENDIF}

type
(*
## ERSRating
```pascal
ERSRating = (ONE, TWO, THREE, FOUR, FIVE);
```
Enum representing the star buttons on the {ref}`Logout` gametab.
*)
  ERSRating = (ONE, TWO, THREE, FOUR, FIVE);
(*
## ERSLogoutButton
```pascal
ERSLogoutButton = (WORLD_SWITCHER, CLICK_HERE);
```
Enum representing the buttons available on the {ref}`Logout` gametab.
*)
  ERSLogoutButton = (WORLD_SWITCHER, CLICK_HERE);

(*
## TRSLogout
Main record that holds and the methods to interact with the logout gametab.
*)
  TRSLogout = record
    Ratings: array [ERSRating] of TRSButton;
    Buttons: array [ERSLoginButton] of TRSButton;
  end;

(*
## Logout.SetupGameTab
```pascal
procedure TRSLogout.SetupGameTab();
```
Internal method used to setup {ref}`TRSLogout` coordinates.
This is called automatically for you on the {ref}`Logout variable`.
*)
procedure TRSLogout.SetupGameTab();
var
  btns: TBoxArray;
  i: Integer;
begin
  btns := TBoxArray.Create(GameTab.TopLeft.Offset(2,61), 5, 1, 31, 35, [6, 0]);
  for i := 0 to High(btns) do
  begin
    Self.Ratings[i].EnabledColors := [[$0F1043, 0], [$23269F, 0.227]];
    Self.Ratings[i].Bounds := btns[i];
  end;

  Self.Buttons[ERSLogoutButton.WORLD_SWITCHER].EnabledColors := [[$0F1043, 0], [$23269F, 0.227]];
  Self.Buttons[ERSLogoutButton.CLICK_HERE].EnabledColors := [[$0F1043, 0], [$23269F, 0.227]];

  with GameTab.Bounds do
  begin
    Self.Buttons[ERSLogoutButton.WORLD_SWITCHER].Bounds := TBox.Create(X1+22, Y2-97, X2-22, Y2-66);
    Self.Buttons[ERSLogoutButton.CLICK_HERE].Bounds := TBox.Create(X1+22, Y2-49, X2-22, Y2-18);
  end;
end;

(*
## TRSLogout.Ratings
```pascal
Ratings: array [ERSRating] of TRSButton;
```
`TRSButton` for each of the {ref}`Logout` rating buttons.

Example:
```pascal
ShowOnClient(Logout.Ratings);
```
```{figure} ../../images/logout_ratings.png
```

You can also access each button with an index or, more conveniently, with a
{ref}`ERSRating`.

Example:
```pascal
ShowOnClient(Logout.Ratings[ERSRating.THREE]);
```
```{figure} ../../images/logout_rating.png
```
*)

(*
## TRSLogout.Buttons
```pascal
Buttons: array [ERSLogoutButton] of TRSButton;
```
`TRSButton` for each of the {ref}`Logout` buttons.

Example:
```pascal
ShowOnClient(Logout.Buttons);
```
```{figure} ../../images/logout_buttons.png
```

You can also access each button with an index or, more conveniently, with a
{ref}`ERSLogoutButton`.

Example:
```pascal
ShowOnClient(Logout.Buttons[ERSLogoutButton.WORLD_SWITCHER]);
```
```{figure} ../../images/logout_button.png
```
*)

(*
## Logout.IsOpen
```pascal
function TRSLogout.IsOpen(): Boolean;
```
Returns true if the logout gametab is open, false if it's not.

Example:
```pascal
WriteLn Logout.IsOpen();
```
*)
function TRSLogout.IsOpen(): Boolean;
begin
  Result := GameTabs.IsOpen(ERSGameTab.LOGOUT);
end;

(*
## Logout.Open
```pascal
function TRSLogout.Open(): Boolean;
```
Attempts to open the logout tab, returns true if it succeeds.

Example:
```pascal
WriteLn Logout.Open();
```
*)
function TRSLogout.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.LOGOUT);
  if Result and WorldSwitcher._IsOpen() then
    Result := WorldSwitcher.Close();
end;


(*
## Logout.IsRated
```pascal
function TRSLogout.IsRated(): Boolean;
```
Returns true if a star button was clicked.

Example:
```pascal
WriteLn Logout.IsRated();
```
*)
function TRSLogout.IsRated(): Boolean;
begin
  Result := Self.Ratings[ERSRating.ONE].Enabled();
end;

(*
## Logout.Rate
```pascal
function TRSLogout.Rate(star: ERSRating): Boolean;
```
Click a star button to rate the game.

Example:
```pascal
WriteLn Logout.Rate(ERSRating.FOUR);
```
*)
function TRSLogout.Rate(star: ERSRating): Boolean;
begin
   if Self.Ratings[star].Enabled() and
      ((star = ERSRating.FIVE) or not Self.Ratings[star+1].Enabled()) then
       Exit(True);

   Self.Ratings[star].Click(EMouseButton.LEFT);
   Result := Self.Ratings[star].WaitEnabled();
end;

(*
## Logout.Logout
```pascal
function TRSLogout.Logout(attempts: UInt32 = 5; time: UInt32 = 20000): Boolean;
```
Attempts to logout of the game. Returns true if we succesfully logout of the game.
If the {ref}`WorldSwitcher` is open, we will use {ref}`WorldSwitcher.Logout` instead.

Example:
```pascal
WriteLn Logout.Logout();
```
*)
function TRSLogout.Logout(attempts: UInt32 = 5; time: UInt32 = 20000): Boolean;
var
  i: Integer;
  interval: UInt32;
  stars: ERSRating;
begin
  if not Self.Open() then Exit;
  if WorldSwitcher.Logout() then Exit(True);

  interval := time div attempts;

  if Biometrics.RandomBoolean(EBiometric.RATING) then
  begin
    if Biometrics.RandomBoolean(EBiometric.LIKES) then
      i := RandomLeft(4, 0)
    else
      i := RandomLeft(0, 4);

    stars := ERSRating(i);
    WriteLn GetDebugLn('Logout', 'Rating the game with ' + ToStr(stars) + ' stars before logging out.');
    Self.Rate(stars);
  end;

  if Keyboard.LastKey = EKeyCode.RETURN then
    Keyboard.RandomKey();  //unlock enter if using remote input

  for i := 1 to attempts do
  begin
    Self.Buttons[ERSLogoutButton.CLICK_HERE].Click();
    if SleepUntil(not RSClient.IsLoggedIn(), 500, interval + Random(-2000, 2000)) then
      Exit(True);
  end;
end;

var
(*
## Logout variable
Global {ref}`TRSLogout` variable.
*)
  Logout: TRSLogout;
