(*
# Prayer
Methods to interact with the prayer gametab.
*)

{$DEFINE SRLT_PRAYER_INCLUDED}
{$IFNDEF SRLT_OSRS}
  {$I SRLT/osrs.simba}
{$ENDIF}

type
  TRSPrayerSet = set of ERSPrayer;

type
  TRSPrayer = record
    Slots: TBoxArray;
  end;

(*
## Prayer.SetupAlignment
```pascal
procedure TRSPrayer.SetupAlignment(); overload;
```
Setups up the interface aligment for the current client mode.

```{note}
This is automatically called on the **TRSClient.ClientModeChanged** function.
```
*)
procedure TRSPrayer.SetupInterface();
begin
  Self.Slots := TBoxArray.Create(GameTab.Bounds.TopLeft.Offset(1,10), 5, 6, 33, 33, [4, 4]);
end;

(*
## Prayer.IsOpen
```pascal
function TRSPrayer.IsOpen(): Boolean;
```
Returns true if the Prayer tab is open.

Example:
```pascal
WriteLn Prayer.IsOpen();
```
*)
function TRSPrayer.IsOpen(): Boolean;
begin
  Result := GameTabs.IsOpen(ERSGameTab.PRAYER);
end;

(*
## Prayer.Open
```pascal
function TRSPrayer.Open(): Boolean;
```
Attempts to open the Prayer tab and returns true if open.

Example:
```pascal
WriteLn Prayer.Open();
```
*)
function TRSPrayer.Open(): boolean;
begin
  Result := GameTabs.Open(ERSGameTab.PRAYER);
end;

(*
## Prayer.GetInfoBox
```pascal
function TRSPrayer.GetInfoBox(): Boolean;
```
Retuns a TBox of the yellow prayer info box.

Example:
```pascal
WriteLn Prayer.GetInfoBox();
```
*)
function TRSPrayer.GetInfoBox(): TBox;
var
  tpa: TPointArray;
begin
  if not Self.IsOpen then
    Exit;

  tpa := Target.FindColor($A0FFFF, 4, GameTab.Bounds);
  tpa := tpa.Cluster(1).Largest;
  if tpa.Length > 1000 then
    Result := tpa.Bounds;
end;

(*
## Prayer.InfoIsOpen
```pascal
function TRSPrayer.InfoIsOpen(): Boolean;
```
Returns true if the info yellow box about a prayer is open.

Example:
```pascal
WriteLn Prayer.InfoIsOpen();
```
*)
function TRSPrayer.InfoIsOpen(): Boolean;
begin
  Result := Self.GetInfoBox <> [];
end;

(*
## Prayer.InfoIsOpen
```pascal
function TRSPrayer.InfoIsOpen(): Boolean;
```
Moves the mouse to close the prayer info box if it is open. Retuns true if the info box is close.

Example:
```pascal
WriteLn Prayer.InfoIsOpen();
```
*)
function TRSPrayer.CloseInfo(): Boolean;
var
  destination: TPoint;
  timeout: UInt64;
begin
  if not Self.InfoIsOpen then
    Exit(True);

  destination := GameTab.Bounds.Expand(10).NearestEdge(Target.MouseXY);
  Async.MouseMove(destination);

  timeout := GetTickCount() + 3000;

  while Self.InfoIsOpen() or (GetTickCount() > timeout) do
    Sleep(20, 100, ERandomDir.LEFT);

  Async.MouseStop();

  Result := not Self.InfoIsOpen();
end;

(*
## Prayer.Find
```pascal
function TRSPrayer.Find(prayer: ERSPrayer; out b: TBox): Boolean;
```
Return true or false wether we found a prayer or not and return it's bounds if found.
*)
function TRSPrayer.Find(prayer: ERSPrayer; out b: TBox; attempts: Int32 = 2): Boolean;
var
  match: TImageMatch;
begin
  if not Self.IsOpen() then
    Exit;

  Result := PrayerFinder.Find([prayer], Self.Slots, match);

  if Result then
  begin
    b := match.Box.Expand(-1);
    Exit;
  end;

  if attempts = 0 then
    Exit;

  if Self.InfoIsOpen then
    Result := Self.CloseInfo and Self.Find(prayer, b, attempts-1);
end;

(*
## Prayer.Hover
```pascal
function TRSPrayer.Hover(prayer: ERSPrayer): Boolean;
```
Used to move the mouse to the specified spell.

Example:
```pascal
Magic.Hover(ERSSpell.HIGH_LEVEL_ALCHEMY);
```
*)
function TRSPrayer.Hover(prayer: ERSPrayer; checkUpText: Boolean = False): Boolean;
var
  b: TBox;
begin
  if not (Self.IsOpen and Self.Find(prayer, b)) then
    Exit;

  Mouse.Move(b);
  Result := True;

  if checkUpText then
    Result := MainScreen.GetUpText.After('ctivate').Trim
      = ToStr(prayer).ToLower.Replace('_', ' ').CapitalizeWords;
end;

function TRSPrayer.Click(prayer: ERSPrayer; button: EMouseButton = EMouseButton.LEFT): Boolean;
begin
  Result := Self.Hover(prayer);
  if Result then
    Target.MouseClick(button);
end;

function TRSPrayer.CanActivate(prayer: ERSPrayer): Boolean;
var
  b: TBox;
begin
  Result := Self.Find(prayer, b);
end;

function TRSPrayer.IsActive(prayers: TRSPrayerSet): Boolean; overload;
var
  prayer: ERSPrayer;
  b: TBox;
begin
  for prayer in prayers do
    if not (Self.Find(prayer, b) and Target.HasColor(7315120, 27, 10, b)) then
      Exit;

  Result := True;
end;

function TRSPrayer.GetActivePrayers(): TRSPrayerSet;
var
  prayer: ERSPrayer;
begin
  for prayer := Low(ERSPrayer) to High(ERSPrayer) do
    if Self.IsActive([prayer]) then
      Result += prayer;
end;

function TRSPrayer.Activate(prayers: TRSPrayerSet): Boolean;
var
  prayer: ERSPrayer;
begin
  Result := True;
  if not Self.IsOpen then
    Exit(False);

  for prayer in prayers do
  begin
    if Self.IsActive([prayer]) then
      Continue;
    Result := Result and Self.Click(prayer);
  end;

  Result := Result and SleepUntil(Self.IsActive(prayers), 100, 2000);
end;

function TRSPrayer.Deactivate(prayers: TRSPrayerSet): Boolean;
var
  prayer: ERSPrayer;
  emptyPrayerSet: TRSPrayerSet;
begin
  Result := True;
  if not Self.IsOpen() then
    Exit;

  for prayer in prayers do
  begin
    if not Self.IsActive([prayer]) then
      Continue;
    Result := Result and Self.Click(prayer);
  end;

  Result := Result and SleepUntil(((Self.GetActivePrayers() * prayers) = emptyPrayerSet), 100, 2000);
end;

{$H-}
var
  Prayer: TRSPrayer;
{$H+}


