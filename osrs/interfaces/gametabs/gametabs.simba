(*
# GameTabs
Methods to interact with the gametab buttons
*)
{$DEFINE SRLT_GAMETABS_INCLUDED}
{$include_once SRLT/osrs.simba}

type
(*
(ERSGameTab)=
## type ERSGameTab
```pascal
ERSGameTab = enum(NONE, COMBAT, STATS, ACHIEVEMENTS, INVENTORY, EQUIPMENT, PRAYER, MAGIC, CLAN, FRIENDS, ACCOUNT, LOGOUT, OPTIONS, EMOTES, MUSIC);
```
*)
  ERSGameTab = enum(
    NONE,
    COMBAT, STATS, ACHIEVEMENTS, INVENTORY, EQUIPMENT, PRAYER, MAGIC,
    CLAN, FRIENDS, ACCOUNT, LOGOUT, OPTIONS, EMOTES, MUSIC
  );

(*
(TRSGameTabs)=
## type TRSGameTabs
```pascal
TRSGameTabs = record
  Bounds: TBox;
  Tabs: TBoxArray; //Hidden, you are meant to use the Get() method.
  const ActiveColor: TColorTolerance = [$14194A, 1.235, EColorSpace.HSL, [1.631, 1.084, 0.286]];
end;
```
*)
  TRSGameTabs = record
    Bounds: TBox;
    {%codetools off}
    Tabs: TBoxArray;
    {%codetools on}
    const ActiveColor: TColorTolerance = [$14194A, 1.235, EColorSpace.HSL, [1.631, 1.084, 0.286]];
  end;

(*
## GameTabs.SetupInterface
```pascal
procedure TRSGameTabs.SetupInterface();
```
Internal method automatically called for you responsible for setting up coordinates.
*)
procedure TRSGameTabs.SetupInterface();
var
  b: TBox;
  tab: ERSGameTab;
begin
  Self.Bounds.Y2 := Target.Bounds.Y2;

  case RSClient.Mode of
    ERSMode.FIXED:
    begin
      Self.Bounds.X1 := Target.Bounds.X2 - 242;
      Self.Bounds.X2 := Target.Bounds.X2 - 2;
      Self.Bounds.Y1 := Target.Bounds.Y2 - 334;
    end;

    ERSMode.RESIZABLE_CLASSIC:
    begin
      Self.Bounds.X1 := Target.Bounds.X2 - 240;
      Self.Bounds.X2 := Target.Bounds.X2;
      Self.Bounds.Y1 := Target.Bounds.Y2 - 334;
    end;

    ERSMode.RESIZABLE_MODERN:
    begin
      Self.Bounds.X2 := Target.Bounds.X2;

      //Needed because depending on the client width the tabs stack into row(s)
      if Target.Width >= 948 then
      begin
        Self.Bounds.X1 := Target.Bounds.X2 - 428;
        Self.Bounds.Y1 := Target.Bounds.Y2 - 35;
      end
      else
      begin
        Self.Bounds.X1 := Target.Bounds.X2 - 230;
        Self.Bounds.Y1 := Target.Bounds.Y2 - 71;
      end;
    end;
  end;

  case RSClient.Mode of
    ERSMode.FIXED, ERSMode.RESIZABLE_CLASSIC:
      Self.Tabs := TBoxArray.Create(Self.Bounds.TopLeft.Offset(6,1), 7, 2, 30, 33, [3, 265]);

    ERSMode.RESIZABLE_MODERN:
      begin
        // One row
        if Target.Width >= 948 then
        begin
          Self.Tabs := TBoxArray.Create(Self.Bounds.BottomLeft.Offset(0,-35), 14, 1, 30, 33, [3, 0]);
          for tab := ERSGameTab.MUSIC downto ERSGameTab.FRIENDS - 1 do
            Self.Tabs[tab-1] := Self.Tabs[tab-2];
        end
        else // Two rows
          Self.Tabs := TBoxArray.Create(Self.Bounds.BottomLeft.Offset(0,-70), 7, 2, 30, 33, [3, 3]);

        // Logout button is moved to top right corner
        Self.Tabs[7] := Self.Tabs[10];
        Self.Tabs[10].X1 := Target.Bounds.X2 - 19;
        Self.Tabs[10].Y1 := Target.Bounds.Y1 + 9;
        Self.Tabs[10].X2 := Target.Bounds.X2 - 10;
        Self.Tabs[10].Y2 := Target.Bounds.Y1 + 18;
      end;
  end;
end;


(*
## GameTabs.Get
```pascal
function TRSGameTabs.Get(const tab: ERSGameTab): TBox;
```
Get a GameTab button bounds.

Example:
```pascal
ShowOnClient(GameTabs.Get(ERSGameTab.INVENTORY));
```
*)
function TRSGameTabs.Get(const tab: ERSGameTab): TBox;
begin
  Result := Self.Tabs[tab-1];
end;

(*
## GameTabs.GetCurrent
```pascal
function TRSGameTabs.GetCurrent(): ERSGametab;
```
Get the currently active ERSGameTab.

Example:
```pascal
WriteLn GameTabs.GetCurrent();
```
*)
function TRSGameTabs.GetCurrent(): ERSGametab;
var
  tab: ERSGameTab;
begin
  if (RSClient.Mode = ERSMode.RESIZABLE_MODERN) and (Target.CountColor(8639715, 0, Self.Tabs[ERSGameTab.LOGOUT-1]) > 0) then
    Exit(ERSGameTab.LOGOUT);

  for tab := ERSGameTab.COMBAT to High(ERSGameTab) do
    if (Target.CountColor(Self.ActiveColor, Self.Tabs[tab-1]) > 50) then
      Exit(tab);
end;


(*
## GameTabs.IsOpen
```pascal
function TRSGameTabs.IsOpen(const tab: ERSGameTab): Boolean;
```
Returns true/false if the specified `tab` is currently open.

Example:
```pascal
WriteLn GameTabs.IsOpen(ERSGameTab.INVENTORY);
```
*)
function TRSGameTabs.IsOpen(const tab: ERSGameTab): Boolean;
begin
  Result := Self.GetCurrent() = tab;
end;

(*
## GameTabs.WaitOpen
```pascal
function TRSGameTabs.WaitOpen(const tab: ERSGameTab; const time: Integer = 600; interval: Integer = -1): Boolean;
```
Returns true/false if the specified `tab` opens within the specified `time`.

Example:
```pascal
Mouse.Click(GameTabs.Get(ERSGameTab.INVENTORY), EMouseButton.LEFT);
WriteLn GameTabs.WaitOpen(ERSGameTab.INVENTORY);
```
*)
function TRSGameTabs.WaitOpen(const tab: ERSGameTab; const time: Integer = 600; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.IsOpen(tab), interval, time);
end;

(*
## GameTabs.Open
```pascal
function TRSGameTabs.Open(const tab: ERSGameTab): Boolean;
```
Attempts to open the specified `tab` gametab.

Example:
```pascal
WriteLn GameTabs.Open(ERSGameTab.INVENTORY);
```
*)
function TRSGameTabs.Open(const tab: ERSGameTab): Boolean;
begin
  if Self.IsOpen(tab) then Exit(True);

  for 1 to 3 do
  begin
    Mouse.Click(Self.Tabs[tab-1], EMouseButton.LEFT);
    if Self.WaitOpen(tab, 2000) then Exit(True);
  end;
end;


procedure TRSGameTabs.Draw(img: TImage);
begin
  img.DrawBox(Self.Bounds);
  img.DrawBoxArray(Self.Tabs, False);
end;

var
  GameTabs: TRSGameTabs;
