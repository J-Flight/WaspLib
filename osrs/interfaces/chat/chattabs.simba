(*
# ChatTabs
Chat tabs refer to the tabs/buttons that are below the {ref}`Chat` box.

You can use these to change the current chat being viewed.
```{figure} ../../images/chattabs.png
```
*)
{$DEFINE WL_CHAT_TABS_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
(ERSChatTab)=
## ERSChatTab enum
```pascal
ERSChatTab = enum(NONE, ALL, GAME, PUB, PRIV, CHANNEL, CLAN, TRADE);
```
Enum representing each of the chat tabs available.
*)
  ERSChatTab = enum(NONE, ALL, GAME, PUB, PRIV, CHANNEL, CLAN, TRADE);

(*
(ERSChatTabState)=
## ERSChatTabState enum
```pascal
ERSChatTabState = enum(NONE, ENABLED, FILTERED, DISABLED, HIDE, AUTOCHAT);
```
Enum representing each of the filters a chat tab can have.

You can see the filters when you right click the tab:
```{figure} ../../images/chattab_filter.png
```
*)
  ERSChatTabState = enum(NONE, ENABLED, FILTERED, DISABLED, HIDE, AUTOCHAT);

(*
## TRSChatTabs
Record responsible with interacting with the {ref}`ChatTabs`.

In {ref}`TRSChat` you have a `Tabs` variable of this type.
*)
  TRSChatTabs = record
    Bounds: TBox;
    Tabs: array [0..7] of TRSButton;
  end;

(*
## Chat.Tabs.SetupInterface
```pascal
procedure TRSChatTabs.SetupInterface();
```
Internal method used to setup the {ref}`TRSChatTabs` coordinates.

This is automatically called for you on the {ref}`Chat.Tabs variable`.
*)
procedure TRSChatTabs.SetupInterface();
var
  i: Int32;
begin
  with RSClient.Bounds do
  begin
    Self.Bounds.X1 := X1;
    Self.Bounds.X2 := X1 + 518;
    Self.Bounds.Y1 := Y2 - 22;
    Self.Bounds.Y2 := Y2;
  end;

  with Self.Bounds do
    for i := 0 to 6 do
      Self.Tabs[i+1] := new TRSButton([X1+3+(i*62), Y1, X1+58+(i*62), Y2-1], [[$0E202D, 0.888]]);

  MainScreen.Mask.DrawColor := $FFFFFF;
  MainScreen.Mask.DrawBoxFilled(Self.Bounds);
end;

(*
## Chat.Tabs.GetActive
```pascal
function TRSChatTabs.GetActive(): ERSChatTab;
```
Returns the {ref}`TRSChatTab` that is active right now.

Example:
```pascal
WriteLn Chat.Tabs.GetActive();
```
*)
function TRSChatTabs.GetActive(): ERSChatTab;
var
  tab: ERSChatTab;
begin
  for tab := ERSChatTab.ALL to High(ERSChatTab) do
    if Self.Tabs[tab].Enabled then
      Exit(tab);
end;

(*
## Chat.Tabs.IsActive
```pascal
function TRSChatTabs.IsActive(tab: ERSChatTab): Boolean;
```
Returns True/False if the specified {ref}`ERSChatTab` is active right now.

Example:
```pascal
WriteLn Chat.Tabs.IsActive(ERSChatTab.ALL);
```
*)
function TRSChatTabs.IsActive(tab: ERSChatTab): Boolean;
begin
  Result := Self.Tabs[tab].Enabled;
end;


(*
## Chat.Tabs.Open
```pascal
function TRSChatTabs.Open(tab: ERSChatTab): Boolean;
```
Attempts to open the specified {ref}`ERSChatTab`.

Example:
```pascal
WriteLn Chat.Tabs.Open(ERSChatTab.ALL);
```
*)
function TRSChatTabs.Open(tab: ERSChatTab): Boolean;
begin
  Result := Self.Tabs[tab].Enabled[True];
end;


function TRSChatTabs._GetState(tab: ERSChatTab): ERSChatTabState;
const
  COLORS: array [1..5] of Integer = [65280, 65535, 255, 16776960, 16756480];
var
  state: ERSChatTabState;
begin
  Result := ERSChatTabState.NONE;
  if tab = ERSChatTab.ALL then Exit;

  for state := ERSChatTabState.ENABLED to High(ERSChatTabState) do
    if Target.CountColor(COLORS[state], 0, Self.Tabs[tab].Bounds) > 0 then
      Exit(state);
end;

(*
## Chat.Tabs.GetState
```pascal
function TRSChatTabs.GetState(tab: ERSChatTab): ERSChatTabState;
```
Returns the {ref}`ERSChatTabState` of the specified {ref}`ERSChatTab`.

Example:
```pascal
WriteLn Chat.Tabs.GetState(ERSChatTab.PUBLIC_);
```
*)
function TRSChatTabs.GetState(tab: ERSChatTab): ERSChatTabState;
begin
  Result := Self._GetState(tab);
end;

(*
## Chat.Tabs.ChangeState
```pascal
function TRSChatTabs.ChangeState(tab: ERSChatTab; state: ERSChatTabState): Boolean;
```
Attempts to change the state of the specified {ref}`ERSChatTab` to the
specified {ref}`ERSChatTabState`.

Example:
```pascal
WriteLn Chat.Tabs.GetState(ERSChatTab.PUBLIC_, ERSChatTabState.ENABLED);
```
*)
function TRSChatTabs.ChangeState(tab: ERSChatTab; state: ERSChatTabState): Boolean;
begin
  if Self._GetState(tab) = state then
    Exit(True);

  Self.Tabs[tab].Click(EMouseButton.RIGHT);

  if not ChooseOption.WaitOpen(200) then
    Exit;

  case state of
    ERSChatTabState.ENABLED:  ChooseOption.Select(['Filter', 'standard', 'all']);
    ERSChatTabState.FILTERED: ChooseOption.Select(['Filter', 'friends']);
    ERSChatTabState.DISABLED: ChooseOption.Select(['none']);
    ERSChatTabState.HIDE:     ChooseOption.Select(['Hide']);
    ERSChatTabState.AUTOCHAT: ChooseOption.Select(['autochat']);
  end;

  Result := Self._GetState(tab) = state;
end;
