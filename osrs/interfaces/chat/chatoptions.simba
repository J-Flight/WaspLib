(*
# Chat Options
The {ref}`Chat` interface options menu that shows up when you are interacting
with certain objects or NPCs.

```{figure} ../../images/chat_options.png
A chat options menu.
```
*)
{$DEFINE WL_CHAT_OPTIONS_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TRSChatOption
Helper record to handler chat options.
*)
  TRSChatOption = record
    Text: String;
    Bounds: TBox;
    Key: EKeyCode;
  end;
  TRSChatOptionArray = array of TRSChatOption;

(*
## Chat.GetOptions
```pascal
function TRSChat.GetOptions(colors: TIntegerArray = []): TRSChatOptionArray;
function TRSChat.GetOptions(): String; overload;
```
Returns every chat option visible with the specified `colors`.

Example:
```pascal
img := Target.GetImage();
for option in Chat.GetOptions() do
  img.DrawBox(option.Bounds);
img.Show();
img.Free();
```

```{figure} ../../images/chat_getoptions.png
Highlighting Chat.GetOptions()
```
*)
function TRSChat.GetOptions(colors: TIntegerArray = []): TRSChatOptionArray;
var
  pt: TPoint;
  i: Integer;
begin
  if colors = [] then
    colors := Self.GetColors([ERSChatColor.BLACK, ERSChatColor.WHITE]);

  OCR.RecognizeLines(Self.Bounds, RSFonts.QUILL_8, colors, 0);
  if OCR.Engine.Matches = [] then Exit;

  SetLength(Result, Length(OCR.Engine.Matches));

  pt := Self.Bounds.TopLeft;

  for i := 0 to High(OCR.Engine.Matches) do
  begin
    Result[i].Bounds := OCR.Engine.Matches[i].Bounds.Offset(pt);
    Result[i].Text := OCR.Engine.Matches[i].Text;
    Result[i].Key := EKeyCode(Ord(EKeyCode.NUM_1) + i);
  end;

  if ('Click here to continue' in Result[High(Result)].Text) then
    Result[High(Result)].Key := EKeyCode.SPACE;
end;

(*
## Chat.GetOptionsString
```pascal
function TRSChat.GetOptionsString(): String;
```
Returns every chat option visible in black and white as a string.

Example:
```pascal
WriteLn Chat.GetOptionsString();
```
*)
function TRSChat.GetOptionsString(): String;
var
  option: TRSChatOption;
begin
  for option in Self.GetOptions() do
  begin
    if Result <> '' then Result += LINE_SEP;
    Result += option.Text;
  end;
end;

(*
## Chat.FindOption
```pascal
function TRSChat.FindOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []): TRSChatOption;
```
Returns the chat option with the specified `chatOption` text if it's visible with the specified `colors`.
The result is returned as a {ref}`TRSChatOption`.

`chatOption` doesn't have to be the entire string of the option, partially is fine.
If `colors` is empty like it is by default, `$0`, `$FFFFFF` and `$FF0000` (black, white and blue) are used.

Example:
```pascal
option := Chat.FindOption('access my bank');
ShowOnTarget(option.Bounds);
```
```{figure} ../../images/chat_findoption.png
Highlighting Chat.FindOption()
```
*)
function TRSChat.FindOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []): TRSChatOption;
var
  option: TRSChatOption;
begin
  if colors = [] then
    colors := Self.GetColors([ERSChatColor.BLACK, ERSChatColor.WHITE, ERSChatColor.BLUE]);
  for option in Self.GetOptions(colors) do
    if option.Text.Contains(chatOption, caseSensitive) then
      Exit(option);
end;

(*
## Chat.HasOption
```pascal
function TRSChat.HasOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []): Boolean;
```
Returns True/False if the chat option with the specified `chatOption` text is visible with the specified `colors`.

`chatOption` doesn't have to be the entire string of the option, partially is fine.
If `colors` is empty like it is by default, `$0`, `$FFFFFF` and `$FF0000` (black, white and blue) are used.

Example:
```pascal
WriteLn Chat.HasOption('access my bank');
```
*)
function TRSChat.HasOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []): Boolean;
begin
  if colors = [] then
    colors := Self.GetColors([ERSChatColor.BLACK, ERSChatColor.WHITE, ERSChatColor.BLUE]);
  Result := Self.FindOption(chatOption, caseSensitive, colors) <> [];
end;

(*
## Chat.WaitOption
```pascal
function TRSChat.WaitOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []; time: Integer = 600; interval: Integer = -1): Boolean;
```
Same as {ref}`Chat.HasOption` but waits up to `time` milliseconds for it to be true.

Example:
```pascal
WriteLn Chat.WaitOption('access my bank');
```
*)
function TRSChat.WaitOption(chatOption: String; caseSensitive: Boolean = True; colors: TIntegerArray = []; time: Integer = 600; interval: Integer = -1): Boolean;
begin
  if colors = [] then
    colors := Self.GetColors([ERSChatColor.BLACK, ERSChatColor.WHITE, ERSChatColor.BLUE]);
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.HasOption(chatOption, caseSensitive, colors), time, interval);
end;


(*
## Chat.Select
```pascal
function TRSChat.Select(chatOption: String; caseSensitive: Boolean = True; keyboardProbability: Single = -1; colors: TIntegerArray = []): Boolean;
```
Selects a chat option whose text matches `chatOption`.

Example:
```pascal
WriteLn Chat.Select('access my bank');
```
*)
function TRSChat.Select(chatOption: String; caseSensitive: Boolean = True; keyboardProbability: Single = -1; colors: TIntegerArray = []): Boolean;
var
  option: TRSChatOption;
begin
  if colors = [] then
    colors := Self.GetColors([ERSChatColor.BLACK, ERSChatColor.WHITE]);
  option := Self.FindOption(chatOption, caseSensitive, colors);
  if option = [] then Exit;

  if keyboardProbability < 0 then
    keyboardProbability := Biometrics.RandomDouble(0.5);

  if RandomBoolean(keyboardProbability) then
    Keyboard.PressKey(option.Key)
  else
    Mouse.Click(option.Bounds, EMouseButton.LEFT);

  Result := True;
end;


function TRSChat.HasContinue(): Boolean;
var
  colors: TIntegerArray;
begin
  colors := Self.GetColors([ERSChatColor.BLUE, ERSChatColor.WHITE]);
  Result := Self.HasOption('Click here to continue', True, colors) or
            Self.HasOption('Please wait', True, colors);
end;

(*
## Chat.ContinueChat
```pascal
function TRSChat.ContinueChat(keyboardProbability: Single = -1): Boolean;
```
Selects the "Click here to continue" chat option.

Example:
```pascal
WriteLn Chat.ContinueChat();
```
*)
function TRSChat.Continue(keyboardProbability: Single = -1): Boolean;
var
  colors: TIntegerArray;
begin
  if keyboardProbability < 0 then
    keyboardProbability := Biometrics.RandomDouble(0.5);
  colors := Self.GetColors([ERSChatColor.BLUE, ERSChatColor.WHITE]);
  if not Self.Select('Click here to continue', True, keyboardProbability, colors) then
    Exit;

  Result := Self.WaitOption('Please wait', True, colors, 3000) and
            SleepUntil(not Self.HasOption('Please wait', True, colors), RandomMode(100, 50, 1500), 3000);
end;

(*
## Chat.ContinueUntilOption
```pascal
function TRSChat.ContinueUntilOption(option: String; timeout: Integer = 10000): Boolean;
```
Selects the "Click here to continue" continuously until `option` is available
as a chat option or until we reach the `timeout` which is measured in milliseconds.

Example:
```pascal
WriteLn Chat.ContinueUntilOption('access my bank');
```
*)
function TRSChat.ContinueUntilOption(option: String; timeout: Integer = 10000): Boolean;
begin
  timeout += Time();
  while (Time() < timeout) do
  begin
    if Self.HasOption(option) then Break;
    Self.Continue();
    Sleep(0, 2500, ERandomDir.LEFT);
  end;

  Result := Self.Select(option);
end;


(*
## Chat.GetTitle
```pascal
function TRSChat.GetTitle(): String;
```
Returns the title above the chat options, this is a text you cannot select and
red, often with 2 swords pointing to it.

Example:
```pascal
WriteLn Chat.GetTitle();
```
*)
function TRSChat.GetTitle(): String;
var
  options: TRSChatOptionArray;
begin
  options := Self.GetOptions([$000080]);
  if Length(options) > 0 then
    Result := options[0].Text;
end;

(*
## Chat.IsTitle
```pascal
function TRSChat.IsTitle(text: String; similarity: Single = 0.8): Boolean;
```
Returns True/False if the current chat title matches `text`.

Example:
```pascal
WriteLn Chat.IsTitle('Select an option');
```
*)
function TRSChat.IsTitle(text: String; similarity: Single = 0.8): Boolean;
begin
  Result := Self.GetTitle().Similarity(text) >= similarity;
end;

(*
## Chat.IsTitle
```pascal
function TRSChat.IsTitle(text: String; similarity: Single = 0.8): Boolean;
```
Returns True/False if the current chat title matches `text`.

Example:
```pascal
WriteLn Chat.IsTitle('Select an option');
```
*)
function TRSChat.WaitTitle(text: String; time: Integer = 600; similarity: Single = 0.8; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.IsTitle(text, similarity), time, interval);
end;


(*
## Chat.LeveledUp
```pascal
function TRSChat.LeveledUp(): Boolean;
```
Returns true if we have the level up message on the chat box.
This function is overriden in {ref}`Chat.LevelUp override` to increment {ref}`Stats` `Levels` variables.

Example:
```pascal
if Chat.LeveledUp() then
  Chat.Continue();
```
*)
function TRSChat.LeveledUp(): Boolean;
begin
  Result := False;
end;


(*
## Chat.HandleLevelUp
```pascal
function TRSChat.HandleLevelUp(keyboardProbability: Single = -1): Boolean;
```
Attempts to handle the {ref}`Chat.LevelUp` notification.

Example:
```pascal
Chat.HandleLevelUp();
```
*)
function TRSChat.HandleLevelUp(keyboardProbability: Single = -1): Boolean;
var
  useKeyboard: Boolean;
  timeout: UInt64;
begin
  timeout := Time() + 8000;
  repeat
    Self.Continue(keyboardProbability);
    Sleep(60, 3000, ERandomDir.LEFT);
    Result := Chat.GetDisplayName() <> '';
  until Result or (Time() > timeout);
end;
