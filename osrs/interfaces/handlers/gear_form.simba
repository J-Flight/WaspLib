(*
# Gear Form
This page is about {ref}`Gear Layouts` dedicated `TLazForm` utilities.

This is meant to be used with {ref}`TScriptForm` with
{ref}`TScriptForm.CreateGearTab` and will setup a a `TLazTabSheet` on it with
several controls to configure the {ref}`Gear Layouts`:
```{figure} ../../images/gearform.png
```
*)

{$DEFINE WL_GEAR_FORM_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TGearFormHelper
The `TGearFormHelper` type is, as it's name implies a helper type for the
`Gear Form`.

It has a lot of methods and callbacks that are hidden by default and won't be mentioned on
this page because they are internal methods you shouldn't touch.

Do feel free to read the source code though if you so desire.
*)
  TGearFormHelper = record
    Sets: array of TRSGear;
    {%codetools on}
    Index: Integer;
    Selected: ERSEquipment;
    Gear: TRSGear;

    SlotBoxes, DPISlots: TBoxArray;
    FilteredLists, ItemLists: array [ERSEquipment] of TLazListBox;

    LayoutCombobox: TLazComboBox;
    LinkedComboboxes: array of TLazComboBox;
    LayoutName, Search: TLazEdit;
    ListLabel: TLazLabel;
    UnfilterButton, DuplicateButton, RemoveButton: TLazButton;
    ImageBox: TImageBox;

    TwoHandedIndex: Integer;

    Config: TConfigJSON;

    const SLOT_NAMES: TStringArray = ['Head', 'Cape', 'Necklace', 'Ammunition', 'Weapon', 'Body', 'Shield', 'Legs', 'Hands', 'Feet', 'Ring', 'Secondary Ammunition'];
    const SLOT_IMG: TImage = ImageFromString('IMG:AQAAACQAAAAkAAAAAAAAAAgAAAAAAAAAAQAAAAAAAAAoWFABAAAAAPDVPwEAAAAAwLpRAQAAAAAA9UkAAQAAAJDXPwEAAAAAAYA4AAEAAAABAAAAAAAAAGo/PQABAAAAMNY/AQAAAAAukjgAAQAAACQAAAAAAAAAopA1AAEAAAAaHyIAMzxF/9Bn7gYAiVBORw0KGgoAAAANSUhEUgAAACQAAAAkCAIAAABuYg/PAAAC3klEQVR4nMWXvWrkQBCE92EuOxxdahwcbGCDAi9sYIOTxU/g9w9c8B0fpdbgyHcHgxhJrf6pru4enc73v1h3P3/8paWJUxv7eH/+9rUwhgt59/R4fni437bH8/l3rpfLxv56fc4+mwj4/OX1yiYrAjzhQ/YY097JYHEkompEafZZ0XW7vXHL86y4lWteZUU1buEZtwbHOjV6Ecr3WWhkb5TtQZ7kGo0s8MASZkAiq/H8Y0wbLGEhSuACMYLDkuEipgD2uMXqzlj7i3TnLIpUB6RtPvuRZt+qbRrrZBAWTgEdtyYmm8TELfY6GpS0uztjWGo0xNMNudFZ3AdMI26PJfYCRnEw5/AKypkJuSdRdY6HUl97E0ayhRzx4alB6K8xYfIoI8jUxiIy60ago8WiIcrOIkntwlLGyvmKIEBhbjsTaAeoZgF4DBkBl1YzMgARaH3vMvBjC9bcDJlOOU8WbGyiy3IfSj/jW8p06FJmZ0wXTL7uWw+476tBim4ruYXDZmcShJSIoX1PrEzPkRQtI8JKThjtIHbSwfIRypEUY1B0IUZm0RvpQHY8EMP9EcoghZaape6nMbyWgXapbhYdyiCFaNvhKDgpszOWmAboUcfDJb8HKWx1bGwOeDyLur1DaZfBkd+DOHLKIWUKFnVGVsyt3cSeMkIR2zbZIY6RtuiNtig+6zkyQmmv7ZCXGq2dlFnUeZcMZWEpm/7mGIpvxQBjnrH0bFHUXdEdCopGAjqgvrXmOpeLnHU3Q/VWc7LB6aqSFNJHY2hjci6G50BMjnGEGqH0+WCro5gP+ZzZNnMGjLZEU9156lA6Kw6UMcFts4t51sQj/B4TI5Qu9m0/dTthEnuOGLmEJQ33QcHuZRNp2xRiMG+TxLczxuq+Lk36QDEKjs5kG+wUYoZs7Y7f/WOhaM9+wTQrtr4eYO49F80fi/HL5AfHQ4Cn1Sb3Zf8XY13CkfnL9B9+Bv/Nb+4n1nh/wtuIW5MAAAAASUVORK5CYII=');
    const BACKGROUND_IMG: TImage = ImageFromString('IMG:AQAAAMoAAAARAQAAAAAAAAgAAAAAAAAAAQAAAAAAAAB4X1sBAAAAAPDVPwEAAAAAEDjyBgAAAAAA9UkAAQAAAJDXPwEAAAAAAYA4AAEAAAABAAAAAAAAAGo/PQABAAAAMNY/AQAAAAAukjgAAQAAABEBAAAAAAAA6asYAAEAAAAaHyIAMzxF/1jf8QYAiVBORw0KGgoAAAANSUhEUgAAAMoAAAERCAIAAAC1ia+nAAAhqUlEQVR4nO1dUaslx3G+eQsYgRE4BMeG4EiJkDQXIoFwHGOEoUEeaVA88mAmQViyJYJkgcSC5LXAGPZFCBYjMOhBjwY/OLaxwfghD/lv6bt1z7ffVPf0mTOnu6dnT8PH2dm5M1N1qmuqq6u7v3P1p//5wuKPv/s144f/8eLPPviRxYc/He/dffuzTz96Y2zfe+s1iw/e+YF82j/Zg3fffMUey8U4j7vkwF5jD37x4U8++eV7cqU9CDy5Pj/n8xeK8J7HM63DKBf6zRf3La6sb9l/Pr//sUgSvPStp1596YWXX3zGflrZFvbAPkL+27/87/ZAgGN7sVwvn/YJ9oCvdGEf9V+vGX5IfX7+53PjhkXMnRdx7D+/+uQj63b//UZ/ZR3N+tZf//L7P//ht+La4lsQaT/nHspg/eS/uB3g/7pPrs/f5PnLRbjn+ZnWbcR/xJesL9qTN+4l4VH+bD/lahEMbayb20+5x4VcrG4B5C55gguct/fW5+d//kIRc+cZ4kLiSxP3QtyykgT2zza4Sc8okJNyIMd8MW4RuDfaT5x3L8DD6/PzP3+hCO959UzEMO1e4nFPP/GVf336H+wnDvhTDr753D+75+VYYC+wwH/lTy/+21M4431yff4mz18uInBeIH5mr3n/ndcn7iV9onVAe5H59vMVFStgg5SkaNbDbt1Lukz0r9W9KlbDjhaR6f/j3/3t1dXfXInHVfeqOB/Kl95767Ur8Tg5WzvHinOg3Oum7vWLD38ip2QgYBPAzbWs2Ck+eOcHUnp96F42gkm5TDzMJmWba1mxU1hfevVQ3xdcKY+rnWPFalhfQn3/1r2Uxz37L3+/uZYVOwV8SXrC2+jFHlfdq2I1rHv98DALeeteyuOqe1WsxhsPFl9YYIbqSnlcda+K1fjs048wrX6beymPq6l9xWrcu/s2lmzcRi/lcTV6VazGhz8dUeS6jV7K42r0qlgNnnOUdUFXyuOefuIrm2tZsVPMzjnC42r0qlgNz5xjXTFREQtqecRN9KruVRELanmEda26IKciGmweXxfkVKTCu2++gjHi7aSQ8rgavSpW497dt3X0Uh5XCxMVq6GKXDfupTyuRq+K1fj8/scvv/jMJHopj6vuVbEaNlTxrrP333n9Snlcda+K1ZA8HlWIG/dSHlfX2leshnWvB0Q9zz0sqyqPq+5VsRq88vnhlDZ7XO0cK1bDhithF5OF9TfupTyuulfFavzsgx9JHm996eFae/a46l4VqyHz15ONaMrj6mrVitVAT/hwp5DyuFq1r1iNd998hXf8T3KvulOo4kx4ttEqj6u5V8VqcJHr4T7HC49ezTNPJELJolPAk3spj7vA6GUb485b34+Ohe61legUUGxLN5NCF557ybtum6T9nvnOd77ddd8z5rv2s+87OR6G79tje2AvwPnxPwc5sLAXyBm5UY6ljcPNvKHoRKj8Xrp1ET9sC6EhpS3tsYVtwh//+HX5r5y3sN5gP+2fLGyLijeIQ8h/EUW8Lb2h6KSo/F6TNkZvYtvGNpuFNKQcI5xww9sz9tM2pEACjzSwtK6EHItAV7Wh6KSo/F66jdG0AvQ4Ek6kJ5LOSKKINDDiilyGC6SZ5b/S2AH32kR0UlR+L93GHBikkTgBsu0nrXj9zD8pSKKjcib4BJ4WcK+FosW3pO+Dw60Wndq9ONG6aH4vtDFnNhI/5O2XXsl1LIb0U/AJeQh7Sdi9wqLlv0iw7IGNW/Jf8bAVopOi8nvpNpYG5o4GXZUKWogKFnxeQgs7CkaC4c4xLFoOJMeyf4Uro4tEVDtJdFJ89ulH1oUqv9fDNpZIIC89UmYZiHGIkkCCHkqany9A5ECqZJv5aOc4J1qGh8ioWIqIFodDirZcdFLcu/s2/4LaLb8XjxwvLXpJ6iPNI4FE2gyuI3k04pb0YpInsQvKQ6T/kvLB0eg1Jxp1B4iGFIiGULjXQtFJoUr0t/xebxx+qu8CoxeKSchalNMgMcKgDImRuhjFhYWpvVe0m6SzCIwqVOBcLjop/Pxe7HGXsyCHeyjpbiQMoDkRRdSwX2XQaGxchqw8HL28olG7wrHyLRGtTi4XnRSV30u3saTJyFq45VTH5IXcyLEE+ZO0dMC9XNGc3onHcLeIqoTrWxgEHBWd070umt+Lh29cC0CQcHulOQ/jZIirUPb80ZEjixaHQxjzlj+UV4lojqxh0UlR+b10G+NdR06NcgP3R+xtaEs3GbKXyaAPHV/AvVzR4ltqWCqiJTJxMGO3lhuXiE6Kyu+l21g6JulZpHtS7oVyAP8JozPlXkj/xSPDnaMrWkaF7FsQrbzZrcAtFJ0Uld9LtzFK55gYRjyAe6E5uTfk1oVDoLKAJQ9LqvYQrfIt70mInsvxw6KTovJ7edrYNolMtnCXxwfiAYhVkiFxROHOkUeXR92LRcuTlW9xD6hE83mV+QVEJ0Xl99JtLJEAIzg4CgIST+epNuYOFHd1DxbVSEZ1dOTIotVwQc6oXN4VjRdgoeikqPxeuo1t8OBqJIcoNXBTcULlPdeHyirWaUlHGXAvJZrFudVUVTJ1x48LRSdF5ffSbYwxf3dYVeyOHDEdhMV9nKWp1B7RZUndi0WzM6nChBKN85j/5kpsWHRSVH4v3cYyzsIgrqP5Y5U4o9nUeZWxqSmd8JyjEi0HcqwczhXNawkxL35UdFJUfi/dxjylyJPZWPri5tpuSYy7VO7FwmVVV3R3WDqBNAsLCZVviVx4J645Kjq1e1V+r0kb2yaxOZCFNLA94FxH1sbgDNa9cPxw3QujhHBZ1RXNXiKiJT8Tf+JIxqKxX2iJ6KSo/F66jbmwyeGKux40qloP09EUskrA8afwpJASjS4Sot2+mGcVRTRff1R0UlR+L93GPFXnzvSxw2HUxok8hy44XP9ghYy4S7hzVKK76aw2xhP8fCRYqLHBvZaITorK76XbmLdCoOU4Wqjcy035VbVCWloWYIVzL69oeBi8WenDdXnUe5eLTu1eld9r0sbSQ0nAcKcXXe+Zc6/+sIoVpYGj673mRPeHbWcMrmLIf8WHRlpAu0R0UlR+L93GPFIbH+zu4pk+3ljBGQ9n0OiqOPvBSPBo3Suz6KSo/F66jeVFl/aTBkZ78xpl5EOoMyGWdIeV8naUxy0t/hFekJNfdFJUfi/dxoKR1ushy+bV66pvkkmY7jDHh5QfXZukPktIADKLTorK7zVpY+YRQQvxsmOkzEhxMK+HrIiLmdjxcecUCpOcopOi8nv5nQw1dG5UBBVsauXxP/dcaHWk2GjgMkUnQuX38jfznXkqttVY0sAbik6Byu/lb+NEKFl0ClR+r5V47LEvPf74l7/+ta/alrOfN9PJFyD6VFR+r5WwbSzNbBOdF55/NrN7bSX6VFR+r5WwDWwjh/188slv2ChyM/6/ANGnolx+r8tMgB6x3Ktcfq/mIodvG4pOgUL5veSFu3Nh7N8bik6E4vi9EMzltTNBCu65dQrD3ti/NxSdFMXxe3HvMMxTcHvXV/Gmhn2xf28oOimK4/dqFlBw81pNrIWSCRDvthnMqHQLFl1V4vGIKI7fq1lAwc0O1DoU3GqfBWbueCIv0MZh0W0a9u8NRed0r+35vWBoTi+GGfZvpBrjgYIb5/nd7Ytn/95QdFIUx++FoRM6Bby7vFyzo90TyD+uDxua8X5za7UJ2L8hGv1UNtHXeyAeL47fC+8x4jzyVjOl4J7L69EwY2L278EhHs8mmncutQUTjxfH79Uso+CGJ6mtO5zyw9BjGvZvBI+BiMfziMZXHsomHi+O36tZRsHN/SBGWB1RveHi6Ozfc6K5v04kGjmZCtgdbVU6VXRSFMfvxd1EO0PBza3Imawher6OVnIi+Q0P0c8X3dMG7uiihx0SjxfH79UsoOBmB+IG6Gj3Pd5apMPmbPZvHq56RfdUGYkrGs7K3SJEe7PPhaKTojh+Lx5D9T4Kbryy6DgQSFT/hS00iDHIS24NfZh7kc+waDRqWDS3d0B0d6hBiAMd/daDQ0cA0cqrTEnE48XxezXHKLi5mQ1RcMOgA9GHdAdqZC8F99e/9tUXnn+WV60ERENuWHRHc1Zh0fbzySe/YRUQHcLfut0n8Xhx/F5IclsfBbfKndvDfmUxN2/0c7vIwaHgbh6sxZNFefIZRbSS7hVt3cveYh1LPCwsut0z8XhZ/F6qfm18FNycWY+0+MQQBTc3cDvD/m3b+PHHv/zYY1/i6BUWfT1l/w6Ihit4RctieftpsU40B7ORdnK7eVi7HfF4cfxePPvG7N+jM+HTHdhgOE1BH8G+aGbYv8W3bDPbEILo5RWtMuiAaJV6z4mGbwVEX++feLw4fi+MocYp+3c75f3uaE6N1w4MB3pIzq/bGQpu2RMBBERjKMctp1Kfwfk90TNFs6PgTL8r4vHi+L2aGfZv7u/UwI2vRPPA3GYxBXdAtCFiIxUhXGB26EzR7KyuaPWt3fFjCcTjxfF78XuMrscQ5Z+8rMOUBAtX4hPu1VNNEleGi0+uaKQvKkrhgdzqmIQ+UzQ7k5KrvrXS5yTRSVEcvxdm34xDwY3243E7bAfH6g9L7RC9cAsQnvhzRaPhua6rRKvQ1Z/C/r1QtIpeyrPXiU6K4vi9ePZtmGf/HhwKbtQkDVFwqwEXOrujE38sunPYv70JFrzZnM7+vVx0uyvi8eL4vTCGOsr+LZlKR+zfaGwM2ZQfIFUPT/wtYf/m0Nj5iMeHU9i/TxLd7Yd4vDh+L0yPjMfYv+FPKD8ah4JbZcH4U3hN33g6+/f1oW4O0Xx9UtHutz5JdFIUx++FbgIm5qwW87tI6jF04lQD1SZOU/rY7N/u4I5Fo41TiOa4dabopCiO3wtJLr/BatgIl2LHQlLfEgU3t30Xm/07LJqvjC6ao9eZopOiOH4v7iZQfYBvccQCVAFsnFJw415M5IWHb0o0MuUVooeLJx4vjt+LK0AwIqdQ44kU3FxsxHDsaPEJoivx+Dkojt8LS1MwAsJPRI2rKLhVdt8Wyf69oeikKI7fq/FRcB+dhzmKvmz27w1Fp3avsvi9mimZB8x0jm8NxbN/byg6KQrl94KtOZPlFxfDJWQesCy6D5geeS6sXEXnQaH8XuZSOdw2FJ0CxfF7saEToYrOhuL4vfYCMOfeNl7B9Mwbojh+r70A7lU+PfOGKI7fay+QRfqyI8PipuC0tUoFIiu/114Si6pnLA2z8ns1OxkWVT1jaZiP30teizvFs2pXPSPqmYPfCyFXXg5TKqt21TN6iT8HvxfH8KFgVu2qZ1w9TR5+r2YnrNo71RN7lgJ6YslJWE9ZWhJxcVgOfq/GYdXmfRB9Mazarp59kezfSk+4zpyeauXInJ64IJaeJg+/F8yBJMBdy4BkEwnBmJ1V29WzL5L9W+nJ6+5dPd31I149+a+x9DR5+L0wwEHoZg/rUhJ6n68nq1emnuw6Sk8OWqPDbAg9+QlxeX5z8HvhbUM0HqdMVOkIvaPo2aYnHj9HzyXL3aDn0Ysj6mny8Hs186zabIIUhN6x9IQ+BeoJt5hbpqv09F6G83GjVw5+rybIqs2G6GmNJe9WwPkxJav2nJ5KwyV6qt4qqZ7QjU20UE+2Z4rUPge/Fwdz3s0yOEza/XQkr5JimAaXIYGNGBVYz0An4uoZuDipnhiGD7QdEsdccQjbU40PorhXDn6vZjGXOL4erzHH+4c3Esm1icqqrfR0cxdvTiMquV2SGpGl1lPZk9/AgcjYA/aMrqfJw+/FIx1+b8YFrNoc6rsDKxqHDWQ5d3xc4hi9DwdqroE23shlgNITzcZ6Hk2Nrw/UmNATX4r1XKeh/HeJnsOUIJ07hzl7evU8s+lz8Hs1C1i1lYe1M4TeeB3NMi5xxe8tkAWA8ic5wF3QkxOawWH/drtCjlWunvAA6LlaQ8w2wp4cSpU9MZ8IBQL25H68jUSnk4PfC6loO8OqjbpRP624tg6htwrpwzEuccXvjQN7Uhh7mwcEvnIX68k9DusJZ2p9xOMqLVNdJOu5TkMsv2Z7wlxee4qeeGkD9oSSrOeZTZ+D30tVmc2UVZsH82qkhosx44GRAYzYTlm1FZe4y+8tx3JSXcN6KkdxE53eRzzOUYr1ROtCz3Ua4i63ar/EntcOSznrCSXbeCzlOfi9eI7MS+g90rZjnpREzGenHIKE3opLXPF7C9B+fA3omblJlJ7K79E1j1OWKB5/qTlWfg1WaIh4xvbknFXl6UvSRHfwbuKxlOfg98JIh98tvDqc8qNmoSqu6Inghe0yQu+TwHqGm4T1RMxwJ1gUUujJMRX2XO5bsKecic5SnoPfq1lG6M0JgYrqGEzxeAfhvY3Eqq309DaJV08UAlRFg5FOT5U2ufZkNVhPtid367H0NHn4vfhtQ29ifITemL5oIxF6R9STPczVc3TIm3PqCdeZs6cKV+OBK6CbFpDj6mny8HthjswcI/RWLaEarJ+W8tXsR8S5vDk9oYZXz25ahcqpJ/e/Xnsq9+oPCyFVnI6rp8nD78VzZMMprNqqcnhNFQE1qI5SBjyqp8oFXT0NsdVl1pPdy7UnAir0dL0zup4mD78XRjoLWbU7H5c4900qmegilQGP6on896iefUr2b6+eauSh7Ik/QU81qsVfI+pp8vB7YRJjXMaq3c1wic8Nf+LmNAE92aHn9OQIx8PhDHpyzqfsybn/QEsnhhO52U9FDn4vBHMYAtUsnoVFsoyBGCcuc69aRFbto3qqkaDSUzk9mjmnnugiXXsi93LtmUhPk4ffC6koxwO8K8NiQm8Vt/rYrNpL9PRGUDegmpTs3wE9VZK+0J6J9DR5+L04mKtRPTJTHrP0TmFJegHYgkNIF3sd1RI95xxrzML+HdbTOwx07cmXJdLT5OH34joNvuoKVm0kEJyHtfFYtWPpid6HE5qcerJxvHqyb6XT0+Th98ICEoyn1rFqw2TCqs0lgIjNdr6eXWL27yV6qpjaOyUudOLp9DR5+L2anbBqP2J6hhPEPCzlOfi9minlBr7MQCueOQ/tN2LVfiT1dB0rm54mJ78Xr4NT4xo1qEEeg++PzggNiWwUtoiiZNUzrp75+L3MHujOqp5x9czB78XmSITozVb1jIIc/F4nAZS4slzTlErobfUUDW2O3BTMDL2tPXPwe61rtubBiuHC3ctCNl90pTJDb2vPHPxep5pDLGLjgW25kt1LFsXLxp6bIeTWKhVozxz8Xqeaw75k+FGCkjsd0VB2j5XsXhvaMwe/17pmu80xt4he5afMe7FnDn6vdebYMHo1xQ/492LPHPxep5pj2x9TaQ7lyrZgvvi92DMHv9ep5thqwI8eRIINWENQ726L4YvfhT1NHn6vFebYZMDPfeKwgC9erUoYM/LF78KeJg+/16nm2GrA3yzgtV+yWpVXu2P6L+4yvV3Y0+Th91phjk0G/M0xXnvXk3ibF4NXKmPKuY9HKrkLe5o8/F57MQfci5MqbOXoaYusd9E9r3lExOqnK1djLdPbhT1NHn6vFebYsHNs53nt1Zp6tYhqnBLHs4+2UXnt92JPk4ff61RzbJvaYzVzN+W1d2MVL4l20zIELYw6I/LF78KeJg+/1wpzbFWYuDPPa4+wpEaL0oGy27F7jQl47fdiT5OH3+tUc2xVBmwW8O/3041oKg9jV+uT8e/vxZ4mD7/XqebYahKDO8fW4d9H9GJvU91lTySG8CdsH9qwMLHVpFAOfq915sg/Bdsc499Hn8iDRA5jKsh1afj392JPk4ff61RzbLW6kkeOmEDsiC8eNa1hni8e1xzh378Ae5o8/F6nmmOr1ZXNMf599I9mni+ea2DDPP/+JdjT5OH3OtUcW62uRGrfzvDvo3Ps5/ni2eHQRQ4O//4l2NPk4fc61Rxbra5UVXvj8O9z7sWdY0f8+zztCE/l1RabdI5brVbNwe91qjm2Te27ef597viYH2t0GLaQ6ZsZ/v1LsKfJw++1zhxbRa8xyL/PA0NDP5POxYtuAf/+JdjT5OH3OtUc25ZVw/z7qhgBb/MuyEnEv78Xe5o8/F6nmmPbSSGmvhkd/n23muoCHGBM6IBVrxc1KZSD32uFOTac0j7Ka4/aPbvUSNTR14l57fdiT5OH3+tUc2y7IAchhz2M3WsI8sWju2yT8drvxZ4mD7/XCnNsu5xwOa89jyW7XLz2e7GnycnvVb45MCk0Lua1xxRQTl77vdjTZOb3Wm6ObTtHLqUODq89yqT99KegeM6xS8lrvxd7msz8XgvNsXlqz3GLJ3bUBDZX7TG6RDzDgpw+AV/8LuxpyuT32rYw0c7z2nNNVcUqNU2E6fAxDV/8LuxpyuT32rasOgZ57dHxqcX1vJawn+4OQlK/VdV+27Jqcfxe204KLeGL99ZX1Qx3Ur74XdjTlMnvte2U9p0gX7xyKXfZKiJZUr74XdjTlMnvteFqVaYw6Wb44uFGPfHa80muoybii9+FPU2Z/F7bcqvCw3h7T18YX/xe7Fkiv9fm3KrcSz4C9HEb2rNEfq/NuVUbH29lFOT/Ltvas0R+r825VU/Ss3zq3g3tWSi/11YD6VP13AXx+Ib2LJHfa1tu1eV67uVnE0rgVi2I32svP6ZS9TyKrPxee0mZq56xNMzK79XsZMBf9YylYT5+r+ZQriycL77qGVHPHPxeCLl3yuaLr3pGn7zKwe/FMVz44t3lBswXj20RmIrJwxfv6hnmta96HkUOfq9myhc/tz2Qp41lwk5m6+QVZC7dPg1fvNKz9fHa8/rVqudR5OD3aoi7wY1Y/F9stwclLjIGlV6MCfjiWU8vr73Eg6rncuTg94I5OHS5fPG8HxWkt7xhmt+wPgFfPOvJiyCgZ0c0llXPJcjB74UBjqJgQFRvHY74nnZSSEjHW8g2baPyxUNPdDFhPY9iQz3lQHKsrexp8vB74W3jbVsjkY7KcKb38eFyUyGlSMQXDz3Ra0DPk7xK9f459VT2RM4+EP8+u2NSe5o8/F7NYYOXWoqOryff34bucGshRTBp+OKhZ+/w2nOfLnLdPh07aflPmfVU9vS+saxnUnuaPPxezWHxJ7/T/WGhMCeV0n4Yv3TTzdDXhw1eifjioWfv8Npzr41cp59uRFNfCplATj1de869qHxxOv79HPxeqnNEFjkcOOLRhP10hMzJppuZcQIbMSp0y3jtR2KdUEkxvxL59WR7cgqozMivRCJ7mjz8Xs1h/yAaA3UaHjDjG7KZOJ7h9i4NXzz0HHy89txBQ09uLejJkSO/nrAnhyt3hM6xKpE9TR5+Lx7phBNhBAbEMHFKtlc6vnjWk+PQSLy9sjVI9YDotdFazMSUWU+EMeP8zJbqAboDf2Iie5o8/F7NlC8+7GFiHcmm8bXVazek4YtXenKabIK89nAj1M07Is/Jr6dUHOY8TOmZzp6ueyXh90Iq2s7wxXMYENOg65F3i/MYDhJDVL74o3pyoHKrFXj7+etsoufo0MC64C4ikT1NHn4vVWU2Dl+8O5i/djZAY45WBfk2Hl/8UT2Vl8OruME4dMEDMusZ7iL4fWgT8+/n4PfiOTIvXzzeJH7j3X5z9P1qNQZNcefy5vREw3AZD50mGpt141FwHj3ZgaAAkjPviCSFniYPvxdGOuM8X7w7cgaGA5epeu042sUdkc3p6bq1UhU9O+vZxea1D+upOuueFkfAvfi7INmKrqfJw+/VLOCLV2NGvlIFLYx3uHUjlivn9GTHUuEWOgxTphOThtd+iZ5he6p3IJGeJg+/F79tiNJmyhePmg0yAFw5ly6oOB83KrCeKpQqPd1wyyWYPgGvfdiec3riSnxCW7ZnRD1NHn4vzJGZGb545A2BPJTXvmEel+thEefyWE/VBXMVoPUxyEFPnOSSWB91ztFrT2jregwcix3RtWcsPU0efi+eI8NSELTWOOUpnQNbc5jyxcd1L9aTfQt6Dg6vPXxLlDTTX7PijiliWXXOntxTs606Wh/G3ahrz1h6mjz8XhjpzPHF985cGCp+XJ6QZhscvvi47sV6oqOBnsOD5S5qcQf0lNClkkLo2UUtq87ZEzoz/76yJw8CXHvG0tPk4ffCJMYY5IvndkKRkGfB5Hzr8MXHdS/W08353OGtV0/0O6xn3Nxrzp48BAnY05t1xNXT5OH3QjCHIYYgX/w1zRwjIUBhpnX44qN3jqxnIBd0czIOXa6eYyRe+4X2vD7UdTFgdHNcrz1j6Wny8HshFeX3DO+KWozADYYktD32a2QRcxqvnkocN5LSk/vxPg2v/VF7hm3FvpVUT5OH34uDuRotIzPlFw5+hlcqnPjHWp+0RE8eWwWGuhi4jQl47RfqGfYt8aGkepo8/F5cp8HbpvjiW9roIS8fkoB+upYQby36ylhV5oV6dofa49zsHnrJovRke3ZUPkynp8nD74UFJBj3uXzxrhtJy/EorEvMF79cz4EW7mGoj8FX1RPIwe/VLOCLx3dTYQDuhVVvYzK++JP0HKfzLViXV/Vk5OD3aqaUG/gyw5QvHu+fmzfwZT2VE7uU1CBL9BwOvPacllU9gXz8XrAIvhLHcx58IT/A90eQh4GQjcIWUZSsesbVMx+/l9kD3VnVM66eOfi92ByJEL3Zqp5RkIPfq+JikYPfq+JikYPfq+JikYPfq+JikYPfq+JikYPfq+JikYPfq+JikYPfq+Ji4eH3qrlXRSx4FuTU3KsiFjyTQjX3qogFz4KcmntVxIKn7hWdW7XiYuFZkBOdW7XiYnF8QU7mHzuueJTgWZATnVu14mLhWZATnfyy4mLhWZBT3asiFjwLcqJzq1ZcLDyFiejcqhUXC1WiT8KtWnGx8CzIic6tWnGxUCX625+sqtGrIgpUiT4Jt2rFxcLzg3vRuVUrLhaeH9yrywkrYsHzg3t1OWFFLHh+cK8uJ6yIBc8P7tXlhBWx4PnBvejcqhUXC88P7kXnVq24WHh+cO///vdPf/7DbyNyq1ZcLHgj2i9//v7NcsI//u7Xn3360W++uF9T+4ozgSKXdScp39+6l/U7OWVTMQv7XzsKsF3pvbtvy0SSnLf3u5/2MntgA6M9tjda4Pzn9z+Wh9i/2gOb+tnHyjVLnh94stxbn3/+8+VziVCIm9PBHluXso5kH6XdSzxMihQWku9LaV86VDkvJ+VTZpMQD3Gj3CsHfI2LuecffbL9fOlbT9Xnn/98uVKdlETce6+cnNNBfEu711//8nubgYmHvXooXcinK0POzOlNDvocHgLwf8PPDz8Zz6nPP/P53gvCTunVAXFLfOmhe/3qk5tIaF0YMczeLBCR1pHlQNJ/GRqwTgBuxL0YpvJzFNznL39yff75zz9VqFcHjlvWl+T2K3vWZvhYey+waT9WhMkZKZSpy3BSDnAsN+JenJSN4XKG/+o+/+iT3dvr89c9n68MCGVxrg7WYdwbrV9eiW8pv5aT0FJJxUlc5mopcwL8Ve2n5J74k/fehU+2F1jU55//fFwZFqrOKx3ssTea/j/9onmmosM/YgAAAABJRU5ErkJggg==');
    {%codetools on}
  end;

{%codetools on}
procedure TGearFormHelper.Setup();
var
  i: Integer;
  slot: ERSEquipment;
begin
  Self.Config.Setup('gearlayouts' + PATH_SEP + ToStr(PlayerIndex));
  SetLength(Self.Sets, Self.Config.Data.Count);

  try
    for i := 0 to Self.Config.Data.Count-1 do
    begin
      Self.Sets[i].Name := Self.Config.Data.Key[i];

      for slot := Low(ERSEquipment) to High(ERSEquipment) do
        Self.Sets[i].Items[slot] := Self.Config.Data.Item[i].Item[Ord(slot)].AsString;
    end;
  except
    FileDelete(Self.Config.Path); //corrupted config.
  end;
end;

procedure TGearFormHelper.UpdateConfig();
var
  i: Integer;
begin
  Self.Config.Data := new TJSONObject();
  for i := 0 to High(Self.Sets) do
    Self.Config.Data.Add(Self.Sets[i].Name, Self.Sets[i].ToJSON());
  Self.Config.SaveConfig();
end;

procedure TGearFormHelper.AddLayout(gear: TRSGear);
begin
  Self.Sets += gear;
  Self.Config.Data.Add(gear.Name, gear.ToJSON());
  Self.UpdateConfig();
end;

procedure TGearFormHelper.RemoveLayout(index: Integer);
begin
  Delete(Self.Sets, index, 1);
  Self.UpdateConfig();
end;


procedure TGearFormHelper.UpdateListBox();
var
  idx, i: Integer;
begin
  if Self.ItemLists[Self.Selected].Items.Count = 0 then
  begin
    Self.ItemLists[Self.Selected].Items.BeginUpdate();
    Self.ItemLists[Self.Selected].Items.Add('Nothing');
    Self.ItemLists[Self.Selected].Items.Add('Anything');

    idx := Ord(Self.Selected);
    if idx = 11 then idx := 3;
    for i := 0 to GearData.Data.Item[idx].Count-1 do
      Self.ItemLists[Self.Selected].Items.Add(
        GearData.Data.Item[idx].Item[i].AsString.Capitalize()
      );

    if Self.Selected = ERSEquipment.WEAPON then
    begin
      Self.TwoHandedIndex := Self.ItemLists[Self.Selected].Items.Count;

      for i := 0 to GearData.Data.Item[12].Count-1 do
        Self.ItemLists[Self.Selected].Items.Add(
          GearData.Data.Item[12].Item[i].AsString.Capitalize()
        );
    end;

    Self.ItemLists[Self.Selected].Items.EndUpdate();
  end;

  i := Self.ItemLists[Self.Selected].Items.IndexOf(Self.Gear.Items[Self.Selected]);
  Self.ItemLists[Self.Selected].ItemIndex := Max(i, 0);
end;

procedure TGearFormHelper.UpdateFilters(slot: ERSEquipment; items: TStringArray = []);
begin
  Self.FilteredLists[slot].Items.BeginUpdate();
  Self.FilteredLists[slot].Clear();
  Self.FilteredLists[slot].Items.Add('Nothing');
  Self.FilteredLists[slot].Items.Add('Anything');
  Self.FilteredLists[slot].Items.AddStrings(items);
  Self.FilteredLists[slot].Items.EndUpdate();
end;


procedure TGearFormHelper.DrawItem(slot: ERSEquipment; background: TImage);
var
  id: String;
  img, item: TImage;
begin
  id := ItemFinder.Database.Get(Self.Gear.Items[slot].ToLower(), 'item', 'id');
  img := Self.SLOT_IMG.Copy();
  item := ItemFinder.GetImage(id);

  img.DrawImage(item, [2,2]);
  background.DrawImage(img, Self.SlotBoxes[slot].TopLeft);
end;

procedure TGearFormHelper.UpdateBackground();
var
  slot: ERSEquipment;
  bg: TImage;
begin
  bg := Self.BACKGROUND_IMG.Copy();

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
    case Self.Gear.Items[slot] of
      '', 'Nothing': ;
      'Anything': ; //TODO....
      else Self.DrawItem(slot, bg);
    end;

  Self.ImageBox.SetImage(bg.Resize(EImageRotateAlgo.NEAREST_NEIGHBOUR, FormUtils.Scale));
end;


{$H-}
procedure TGearFormHelper.OnImgPaint(sender: TImageBox; canvas: TImageBoxCanvas; r: TLazRect);
var
  slot: ERSEquipment;
begin
  for slot := Low(ERSEquipment) to High(ERSEquipment) do
    if slot = Self.Selected then
      canvas.DrawBox(Self.DPISlots[slot], $00FF00);
end;

procedure TGearFormHelper.OnSelectionChange(sender: TLazObject; user: Boolean);
begin
  Self.Gear.Items[Self.Selected] := TLazListBox(sender).GetSelectedText();
  if (Self.Selected = ERSEquipment.WEAPON) and (TLazListBox(sender).ItemIndex >= Self.TwoHandedIndex) then
    Self.Gear.Items[ERSEquipment.SHIELD] := 'Nothing';
  Self.UpdateBackground();
end;

procedure TGearFormHelper.OnSaveClick(sender: TLazObject);
var
  i: Integer;
begin
  Self.Gear.Name := Self.LayoutName.Text;
  Self.LayoutCombobox.Items.Strings[Self.Index] := Self.LayoutName.Text;
  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Strings[Self.Index] := Self.LayoutName.Text;

  if Self.Index = (High(Self.Sets) + 1) then
  begin
    Self.AddLayout(Self.Gear);
    Self.LayoutCombobox.Items.Add('New Layout');
    Self.DuplicateButton.Enabled := True;
    Self.RemoveButton.Enabled := True;
  end
  else
    Self.Sets[Self.Index] := Self.Gear;
  Self.UpdateConfig();
end;

procedure TGearFormHelper.OnDuplicateClick(sender: TLazObject);
var
  gear: TRSGear;
  i: Integer;
begin
  if Self.Index = (High(Self.Sets) + 1) then Exit;
  gear := Self.Sets[Self.Index];
  gear.Name += ' duplicate';
  Self.AddLayout(gear);
  Self.LayoutCombobox.Items.Insert(High(Self.Sets), gear.Name);
  Self.LayoutCombobox.ItemIndex := High(Self.Sets);
  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Add(gear.Name);

  Self.OnLayoutChange(Self.LayoutCombobox);
end;

procedure TGearFormHelper.OnRemoveClick(sender: TLazObject);
var
  i: Integer;
begin
  if Self.Index = (High(Self.Sets) + 1) then Exit;

  Self.RemoveLayout(Self.Index);
  Self.LayoutCombobox.Items.Delete(Self.Index);

  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Delete(Self.Index);

  Self.Index := Max(Self.Index-1, 0);
  Self.LayoutCombobox.ItemIndex := Max(High(Self.Sets), 0);
  Self.OnLayoutChange(Self.LayoutCombobox);
end;

procedure TGearFormHelper.OnKeyPress(sender: TLazObject; var key: Char);
var
  str: String;
begin
  str := Self.Search.Text;

  if key = #08 then
  begin
    str.Pop;
    Self.Search.Text := str;
    Exit;
  end;

  Self.Search.Text := str + key;
end;
{$H+}

procedure TGearFormHelper.OnImgClick(sender: TImageBox; x, y: Integer);
var
  i: Integer;
begin
  i := Self.DPISlots.ContainsPoint([x,y]);
  if i >= 0 then
  begin
    Self.ItemLists[Self.Selected].Hide();
    Self.ItemLists[Self.Selected].Enabled := False;
    Self.FilteredLists[Self.Selected].Hide();
    Self.FilteredLists[Self.Selected].Enabled := False;

    Self.Selected := ERSEquipment(i);
    Self.ListLabel.Caption := 'Selected slot: ' + Self.SLOT_NAMES[Self.Selected];
    Self.UpdateListBox();

    if Self.UnfilterButton.Enabled then
    begin
      Self.FilteredLists[Self.Selected].Show();
      Self.FilteredLists[Self.Selected].Enabled := True;
    end
    else
    begin
      Self.ItemLists[Self.Selected].Show();
      Self.ItemLists[Self.Selected].Enabled := True;
    end;
  end;

  sender.Repaint();
end;


procedure TGearFormHelper.OnLayoutChange(sender: TLazObject);
var
  slot: ERSEquipment;
begin
  Self.Index := TLazComboBox(sender).ItemIndex;
  if Self.Index > High(Self.Sets) then
  begin
    Self.Gear.Name := TLazComboBox(sender).Text;
    for slot := Low(ERSEquipment) to High(ERSEquipment) do
      Self.Gear.Items[slot] := 'Nothing';

    Self.DuplicateButton.Enabled := False;
    Self.RemoveButton.Enabled := False;
  end
  else
  begin
    Self.Gear := Self.Sets[Self.Index];
    Self.DuplicateButton.Enabled := True;
    Self.RemoveButton.Enabled := True;
  end;

  Self.ItemLists[Self.Selected].ItemIndex := Self.ItemLists[Self.Selected].Items.IndexOf(Self.Gear.Items[Self.Selected]);
  Self.LayoutName.Caption := TLazComboBox(sender).Text;

  Self.OnUnfilterClick(Self.UnfilterButton);

  Self.UpdateBackground();
end;

procedure TGearFormHelper.OnReadClick(sender: TLazObject);
var
  btn: TLazButton;
  slot: ERSEquipment;
  items: TRSItemArray;
begin
  btn := TLazButton(sender);
  btn.Caption := 'Loading...';
  btn.Font.Style := btn.Font.Style - [ELazFontStyles.fsBold];
  btn.Enabled := False;
  btn.Update();

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
  begin
    items := Equipment.Items.Discover(Ord(slot));

    if items <> [] then
      Self.Gear.Items[slot] := items[0]
    else
      Self.Gear.Items[slot] := 'Nothing';

    Self.UpdateFilters(slot, items);
  end;

  Self.UpdateBackground();

  Self.ItemLists[Self.Selected].Hide();
  Self.ItemLists[Self.Selected].Enabled := False;
  Self.FilteredLists[Self.Selected].Show();
  Self.FilteredLists[Self.Selected].Enabled := True;

  btn.Caption := 'Read From Client';
  btn.Font.Style := [ELazFontStyles.fsBold];
  btn.Enabled := True;
  Self.UnfilterButton.Enabled := True;
end;

procedure TGearFormHelper.OnUnfilterClick(sender: TLazObject);
begin
  Self.FilteredLists[Self.Selected].Hide();
  Self.FilteredLists[Self.Selected].Enabled := False;

  Self.ItemLists[Self.Selected].Show();
  Self.ItemLists[Self.Selected].Enabled := True;

  TLazButton(sender).Enabled := False;
end;

procedure TGearFormHelper.OnSearchChange(sender: TLazObject);
var
  i: Integer;
begin
  if TLazEdit(sender).IsEmpty() then Exit;
  i := Self.ItemLists[Self.Selected].Search(TLazEdit(sender).Text);
  if i > 0 then
    Self.ItemLists[Self.Selected].ItemIndex := i;
end;
{%codetools on}




(*
## GearForm.CreateGearCombobox
```pascal
function TGearFormHelper.CreateGearCombobox(owner: Pointer; caption: TLazLabel = nil): TLazComboBox;
```
This is the only method from {ref}`Gear Form` you might need to use.
This will create a combobox for you whose contents auto-update with changes made
on the `Gear Form`.
*)
function TGearFormHelper.CreateGearCombobox(owner: Pointer; caption: String = 'Gear:'; left, top: Integer = 0): TLazComboBox;
var
  i: Integer;
begin
  Result := TLazComboBox.CreateEx(owner, caption, 'Pick a gear layout.', left, top, 170, 40);
  Self.LinkedComboboxes += Result;

  for i := 0 to High(Self.Sets) do
    Result.Items.Add(Self.Sets[i].Name);

  Result.ItemIndex := Low(Self.Sets);
end;

var
(*
## GearForm variable
Global {ref}`TGearFormHelper` variable.
*)
  GearForm: TGearFormHelper;

(*
## ScriptForm CreateGearTab
```pascal
function TScriptForm.CreateGearTab(): TLazTabSheet;
```
Sets up a `TLazTabSheet` on your `TScriptForm` to configure your `Gear Layouts`.

Example:
```pascal
{$I WaspLib/osrs.simba}
var
  form: TScriptForm;
begin
  form.Setup();
  form.CreateGearTab();
  form.Run();
end.
```
```{figure} ../../images/gearform.gif
```
For a more complete example check out the file `Simba/Includes/WaspLib/examples/gear_form.simba`.
*)
function TScriptForm.CreateGearTab(): TLazTabSheet;
var
  save, read: TLazButton;
  width: Integer;
  i: Integer;
  slot: ERSEquipment;
begin
  GearForm.Setup();
  GearData.Setup();

  Result := Self.CreateTab('Gear Layouts');
  width := 280;

  GearForm.ListLabel := TLazLabel.CreateEx(Result, 'Selected slot: ' + GearForm.SLOT_NAMES[GearForm.Selected], 'Click a slot on the right side to update the list below.', 40, 20);

  GearForm.Search := TLazEdit.CreateEx(Result, 'Search:', 'Search the list below.', 40, 80, width);
  GearForm.Search.OnChange := @GearForm.OnSearchChange;


  for slot := Low(ERSEquipment) to High(ERSEquipment) do
  begin
    GearForm.ItemLists[slot] := TLazListBox.CreateEx(Result, '', 'Click a slot on the right side to update the list below.', 0,0, width, 340);
    GearForm.ItemLists[slot].AnchorVertically(GearForm.Search, 10);
    GearForm.ItemLists[slot].OnSelectionChange := @GearForm.OnSelectionChange;

    if slot <> ERSEquipment.HEAD then
    begin
      GearForm.ItemLists[slot].Hide();
      GearForm.ItemLists[slot].Enabled := False;
    end;

    GearForm.FilteredLists[slot] := TLazListBox.CreateEx(Result, '', 'Click a slot on the right side to update the list below.', 0,0, width, 340);
    GearForm.FilteredLists[slot].AnchorVertically(GearForm.Search, 10);
    GearForm.FilteredLists[slot].OnSelectionChange := @GearForm.OnSelectionChange;
    GearForm.FilteredLists[slot].Hide();
    GearForm.FilteredLists[slot].Enabled := False;
  end;

  GearForm.UnfilterButton := TLazButton.CreateEx(Result, 'Remove list filter', 'Remove the current filter.', 0,0, width);
  GearForm.UnfilterButton.AnchorVertically(GearForm.Search, 360);
  GearForm.UnfilterButton.Enabled := False;
  GearForm.UnfilterButton.OnClick := @GearForm.OnUnfilterClick;

  GearForm.LayoutCombobox := TLazComboBox.CreateEx(Result, 'Layout:', 'Select a gear layout.', Round(Result.Width / FormUtils.Scale) - width - 80, 30, width);
  for i := 0 to High(GearForm.Sets) do
    GearForm.LayoutCombobox.Items.Add(GearForm.Sets[i].Name);
  GearForm.LayoutCombobox.Items.Add('New Layout');
  GearForm.LayoutCombobox.ItemIndex := 0;
  GearForm.LayoutCombobox.OnChange := @GearForm.OnLayoutChange;

  GearForm.LayoutName := TLazEdit.CreateEx(Result, 'Layout name:', 'Change the current layout name.', 0,0, width);
  GearForm.LayoutName.AnchorVertically(GearForm.LayoutCombobox, 30);

  save := TLazButton.CreateEx(Result, 'Save' , 'Save the current gear layout.');
  save.AnchorVertically(GearForm.LayoutName, 10);
  save.BorderSpacing.Left := 18;
  save.OnClick := @GearForm.OnSaveClick;

  GearForm.DuplicateButton := TLazButton.CreateEx(Result, 'Duplicate' , 'Duplicate the current gear layout.');
  GearForm.DuplicateButton.AnchorHorizontally(save, 10);
  GearForm.DuplicateButton.OnClick := @GearForm.OnDuplicateClick;

  GearForm.RemoveButton := TLazButton.CreateEx(Result, 'Remove' , 'Remove the current gear layout.');
  GearForm.RemoveButton.AnchorHorizontally(GearForm.DuplicateButton, 10);
  GearForm.RemoveButton.OnClick := @GearForm.OnRemoveClick;

  GearForm.ImageBox := TImageBox.CreateEx(Result, '', '', Round(Result.Width / FormUtils.Scale) - width - 36, 170, 202, 273);
  GearForm.ImageBox.AllowZoom := False;
  GearForm.ImageBox.AllowMoving := False;
  GearForm.ImageBox.ShowScrollBars := False;
  GearForm.ImageBox.ShowStatusBar := False;
  GearForm.ImageBox.OnImgPaint := @GearForm.OnImgPaint;
  GearForm.ImageBox.OnImgClick := @GearForm.OnImgClick;
  GearForm.UpdateBackground();

  read := TLazButton.CreateEx(Result, 'Read From Client' , 'Read the layout from the client.', 0, 0, 204);
  read.Font.Style := [ELazFontStyles.fsBold];
  read.AnchorVertically(GearForm.ImageBox, 10);
  read.OnClick := @GearForm.OnReadClick;

  GearForm.SlotBoxes := Equipment.GetSlots([9, 35]);
  for i := 0 to High(GearForm.SlotBoxes) do
    with GearForm.SlotBoxes[i] do
      GearForm.DPISlots += [Round(FormUtils.Scale * X1), Round(FormUtils.Scale * Y1), Round(FormUtils.Scale * X2), Round(FormUtils.Scale * Y2)];

  GearForm.UpdateListBox();
  GearForm.OnLayoutChange(GearForm.LayoutCombobox);
end;
