(*
# Gear Form
This page is about {ref}`Gear Layouts` dedicated `TLazForm` utilities.

This is meant to be used with {ref}`TScriptForm` with
{ref}`TScriptForm.CreateGearTab` and will setup a a `TLazTabSheet` on it with
several controls to configure the {ref}`Gear Layouts`:
```{figure} ../../images/gearform.png
```
*)

{$DEFINE WL_GEAR_FORM_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TGearFormHelper
The `TGearFormHelper` type is, as it's name implies a helper type for the
`Gear Form`.

It has a lot of methods and callbacks that are hidden by default and won't be mentioned on
this page because they are internal methods you shouldn't touch.

Do feel free to read the source code though if you so desire.
*)
  TGearFormHelper = record
    Sets: array of TRSGear;
    {%codetools off}
    Index: Integer;
    Selected: ERSEquipment;
    Gear: TRSGear;

    SlotBoxes, DPISlots: TBoxArray;
    FilteredLists, ItemLists: array [ERSEquipment] of TLazListBox;

    LayoutCombobox: TLazComboBox;
    LinkedComboboxes: array of TLazComboBox;
    LayoutName: TLazEdit;
    ListLabel: TLazLabel;
    UnfilterButton, DuplicateButton, RemoveButton: TLazButton;
    ImageBox: TImageBox;

    TwoHandedIndex: Integer;

    Config: TConfigJSON;

    const SLOT_NAMES: TStringArray = ['Head', 'Cape', 'Necklace', 'Ammunition', 'Weapon', 'Body', 'Shield', 'Legs', 'Hands', 'Feet', 'Ring', 'Secondary Ammunition'];
    const SLOT_IMG: TImage = ImageFromString('IMG:AQAAACQAAAAkAAAAAAAAALDjPwEAAAAAr6YOAAEAAACQAAAAAAAAAGDjPwEAAAAAAQAAACQAAAAkAAAAIAAAAAAAAAAAAAAAAAAAAAMAAAAgCBAICAgACBgAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAYNGjDwAAAABAFAAAAAAAAAAAAAAAiVBORw0KGgoAAAANSUhEUgAAACQAAAAkCAIAAABuYg/PAAAC4UlEQVR4nMWXPYrcQBCF5xxODMaZ8Q3MBIYJZkHBDkzgBSeDT+D7B37wmY+nUuNo1wuNaEml+nn1qqp1+vTxA+vrl89vtDRxamO/fz2/+loYw4W8e7pezudv23a9XL7nertt7O/35+yziYDPf7zc2WRFgCd8yB5j2jsZLI5EVI0ozT4ruh6Pn9zyPCtu5ZpXWVGNW3jGrcGxTo1ehPJ9FhrZG2V7kCe5RiMLPLCEGZDIajz/GtMGS1iIErhAjOCwZLiIKYA9brG6M9b+It05iyLVAWmbz36k2bdqm8Y6GYSFU0DHrYnJJjFxi72OBiXt7s4YlhoN8XRDbnQW9wHTiNtjib2AURzMObyCcmZC7klUneOh1NfehJFsIUd8eGoQ+mtMmDzKCDK1sYjMuhHoaLFoiLKzSFK7sJSxcv5FEKAwt50JtANUswA8hoyAS6sZGYAItL53GfixBWtuhkynnCcLNjbRZbkPpZ/xLWU6dCmzM6YLJl/3rQfc99UgRbeV3MJhszMJQkrE0L4nVqbnSIqWEWElJ4x2EDvpYPkI5UiKMSi6ECOz6I10IDseiOH+CGWQQkvNUvfTGF7LQLtUN4sOZZBCtO1wFJyU2RlLTAP0qOPhkt+DFLY6NjYHPJ5F3d6htMvgyO9BHDnlkDIFizojK+bWbmJPGaGIbZvsEMdIW/RGWxSf9RwZobTXdshbjdZOyizqvEuGsrCUTX9zDMW3YoAxz1h6tijqrugOBUUjAR1Q31pznctFzrqboXqrOdngdFVJCumjMbQxORfDcyAmxzhCjVD6fLDVUcyHfM5smzkDRluiqe48dSidFQfKmOC22cU8a+IRfo+JEUoX+7afup0wiT1HjFzCkob7oGD3som0bQoxmLdJ4tsZY3VflyZ9oBgFR2eyDXYKMUO2dsfv/rFQtGe/YJoVW18PMPeei+aPxfhl8oPjIcDTapP7tv+LsS7hyPxleoefwf/zm/sHjP10ojAcrQkAAAAASUVORK5CYII=');
    const BACKGROUND_IMG: TImage = ImageFromString('IMG:AQAAAMoAAAARAQAAAAAAALXZcwABAAAAMHdrAAEAAAAoAwAAAAAAAGDjPwEAAAAAAQAAAMoAAAARAQAAIAAAAAAAAAAAAAAAAAAAAAMAAAAgCBAICAgACBgAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAMCLYBgAAAACoXQMAAAAAAAAAAAAAiVBORw0KGgoAAAANSUhEUgAAAMoAAAERCAIAAAC1ia+nAAAhqklEQVR4nO1d0evtxnH+9a0QDMGQUtKEFtVujW39oA6YNi3BFA44soUbOSKoxcRJbIIdg80FO64hBPxiDCYYAn7wYyAPaRISCHnoQ/+37r3zO9/9NLvao6Ozu5J8Bj7O1dVP0syZHc3Ozu5+5+YP//O5w+9/80vG9/7juZ++832Hd38yfPj+659+/N6rQ/PWay87vPPGd+XT/ckdvPmDF92xXIzzuEsO3DXu4Gfv/uijn78lV7qDyJPt+SWfP1NE8Dye6RxGudCvPv/E4cb5lvvns08+EEmC57/5xEvPP/vCc0+5TyfbwR24R8h/uxf+1R0IcOwuluvl0z3BHfCVPtyj/uvlAz/Enl/++dy4cRFT50Uc+88vPnrPud2PX+1unKM53/rzn377x9/9WlxbfAsi3efUQxmsn/wXtwP8X//J9vxVnj9fhH+en+ncRvxHfMn5ojt5370kPMqf3adcLYKhjXNz9yn3+JCL1S2A3CVP8IHz7l57fvnnzxQxdZ4hLiS+NHIvxC0nSeD+7IKb9IwCOSkHcswX4xaBf6P7xHn/Ajzcnl/++TNFBM+rZyKGafcSj3vysa/805N/4z5xwJ9y8M/P/IN/Xo4F7gIH/Ff+9Ny/PIEzwSfb81d5/nwRkfMC8TN3zdtvvDJyL+kTnQO6i6q//arBsAAuSEmK5jzszr2ky0T/au5lWAw3WkSm/3d/9Zc3N39xIx5n7mW4HMqX3nrt5RvxODlrnaPhEij3ul/3+tm7P5JTMhBwCeDqWhp2infe+K6UXh+6l4tgUi4TD3NJ2epaGnYK50svHev7ghvlcdY5GhbD+RLq+3fupTzu6X/869W1NOwU8CXpCe+iF3ucuZdhMZx7fe84C3nnXsrjzL0Mi/Hqg8UXDpihulEeZ+5lWIxPP34P0+p3uZfyOEvtDYvx4fuvY8nGXfRSHmfRy7AY7/5kQJHrLnopj7PoZVgMnnOUdUE3yuOefOwrq2tp2Ckm5xzhcRa9DIsRmHO0FROGVFDLI+5HL3MvQyqo5RHOtWxBjiEZXB5vC3IMufDmD17EGPFuUkh5nEUvw2J8+P7rOnopj7PChGExVJHrvnspj7PoZViMzz754IXnnhpFL+Vx5l6GxXChinedvf3GKzfK48y9DIsheTyqEPfdS3mcrbU3LIZzrwdEPc88LKsqjzP3MiwGr3x+OKXNHmedo2ExXLgSdjFZWH/fvZTHmXsZFuOn73xf8njnSw/X2rPHmXsZFkPmr0cb0ZTH2WpVw2KgJ3y4U0h5nFXtDYvx5g9e5B3/o9zLdgoZLkRgG63yOMu9DIvBRa6H+xyvPHrVTz2WCVsWnQOB3Et53BVGL9cY9177TnLMdK+1ROeAYlu6Pyl05bmXvOuuSZpvH771rX9r228fDv/uPruuleO+/447dgfuApwf/rOXAwd3gZyRG+VY2jjezCuKzgTj99Kti/jhWggNKW3pjh1cE/7wh6/If+W8g/MG9+n+5OBaVLxBHEL+iygSbOkVRWeF8XuN2hi9iWsb12wO0pByjHDCDe/OuE/XkAIJPNLA0roSchwiXdWKorPC+L10G6NpBehxJJxITySdkUQRaWDEFbkMF0gzy3+lsSPutYrorDB+L93GHBikkTgBcu0nrXj71N8rSKKjcib4BJ4Wca+ZosW3pO+Dwy0Wndu9ONG6an4vtDFnNhI/5O2XXsl3LIb0U/AJeQh7Sdy94qLlv0iw3IGLW/Jf8bAForPC+L10G0sDc0eDrkoFLUQFBz4voYUdBSPBeOcYFy0HkmO5v8KV0UUiqp0lOis+/fg950LG7/WwjSUSyEuPlFkGYhyiJJCgh5Lm5wsQOZAquWY+2TlOiZbhITIqliKixeGQos0XnRUfvv86/4LaHb8XjxyvLXpJ6iPNI4FE2gyuI3k04pb0YpInsQvKQ6T/kvLByeg1JRp1B4iGFIiGULjXTNFZoUr0d/xerx5/qu8KoxeKSchalNMgMcKgDImRuhjFhZmpfVC0n6SzCIwqVOCcLzorwvxe7HHXsyCHeyjpbiQMoDkRRdSwX2XQaGxchqw8Hr2ColG7wrHyLRGtTs4XnRXG76XbWNJkZC3ccqpjCkJu5FiC/ElaOuJevmhO78RjuFtEVcL3LQwCToou6V5Xze/FwzeuBSBI+L3SlIdxMsRVKHf+5MiRRYvDIYwFyx/Kq0Q0R9a46Kwwfi/dxnjXkVOj3MD9EXsb2tJPhtxlMuhDxxdxL1+0+JYalopoiUwczNit5cY5orPC+L10G0vHJD2LdE/KvVAO4D9hdKbcC+m/eGS8c/RFy6iQfQuilTf7FbiZorPC+L10G6N0jolhxAO4F5qTe0NuXTgEKgtY8jCnag/RKt8KnoToqRw/LjorjN8r0MauSWSyhbs8PhAPQKySDIkjCneOPLo86V4sWp6sfIt7QCWaz6vMLyI6K4zfS7exRAKM4OAoCEg8nafamDtQ3NU+WFQjGdXJkSOLVsMFOaNyeV80XoCZorPC+L10G7vgwdVIDlFq4KbihMp7bo+VVazTko4y4l5KNIvzq6mqZOqPH2eKzgrj99JtjDF/e1xV7I8cMR2ExX2cpanUHtFlTt2LRbMzqcKEEo3zmP/mSmxcdFYYv5duYxlnYRDX0vyxSpzRbOq8ytjUlE58zlGJlgM5Vg7ni+a1hJgXPyk6K4zfS7cxTynyZDaWvvi5tl8S4y6Ve7F4WdUX3R6XTiDNwkJC5VsiF96Ja06Kzu1exu81amPXJC4HcpAGdgec68jaGJzBuheOH757YZQQL6v6otlLRLTkZ+JPHMlYNPYLzRGdFcbvpduYC5scrrjrQaOq9TAtTSGrBBx/ik8KKdHoIiHa74t5VlFE8/UnRWeF8XvpNuapOn+mjx0OozZO5Dl0weG6BytkxF3inaMS3Y5ntTGe4OcjwUKNDe41R3RWGL+XbmPeCoGW42ihci8/5VfVCmlpWYAVz72CouFh8GalD9flUe+dLzq3exm/16iNpYeSgOFPL/reM+Ve3XEVK0oDJ9d7TYnujtvOGFzFkP+KDw20gHaO6Kwwfi/dxjxSGx7s7uKZPt5YwRkPZ9Doqjj7wUjwZN2rsOisMH4v3cbyokv7SQOjvXmNMvIh1JkQS9rjSnk3yuOWFv+IL8gpLzorjN9Lt7FgoPV6yLJ59brqm2QSpj3O8SHlR9cmqc8cEoDCorPC+L1Gbcw8ImghXnaMlBkpDub1kBVxMRM7Pu6dQ2FSUnRWGL9X2MlQQ+dGRVDBplYe/3PPhVZHio0G3qboTDB+r3Az35umYluMOQ28ougcMH6vcBtnwpZF54Dxey3EI4986dFHv/z1r913CPfp0qBrEH0ujN9rIVwbSzO7ROfZbzxd2L3WEn0ujN9rIVwDu8jhPh9/vHJRxLX0NYg+F9vl97rOBOgLlnttl9+rvsrh24qic2Cj/F7ywt27MvbvFUVnwub4vRDM5bU7RCm4p9Yp9Htj/15RdFZsjt+Le4d+moI7uL6KNzXsi/17RdFZsTl+r3oGBTev1cRaKJkACW6bwYxKO2PRlRGPJ8Tm+L3qGRTc7ECNR8Gt9llg5o4n8iJtHBfd5GH/XlF0Sfdan98Lhub0op9g/0aqMRwpuHGe391u8+zfK4rOis3xe2HohE4B7y4v12xp9wTyj9vjhma839xaTQb2b4hGP1VM9O0eiMc3x++F9xhxHnnrYUzBPZXXo2GGzOzfvUc8Xkw071xqNkw8vjl+r3oeBTc8SW3d4ZQfhh7ysH8jePREPF5GNL5yv23i8c3xe9XzKLi5H8QIqyWqN1ycnP17SjT315lEIydTAbulrUrnis6KzfF7cTfRTFBwcytyJnsger6WVnIi+Y0P0S8X3dEG7uSi+x0Sj2+O36ueQcHNDsQN0NLue7y1SIcPF7N/83A1KLqjykha0XBW7hYhOph9zhSdFZvj9+IxVBei4MYri44DgUT1X9hCgxiDvEQMjbkX+YyLRqPGRXN7R0S3xxqEONDJb917dAQQrbzqsCXi8c3xe9WnKLi5mQ9EwQ2D9kQf0h6pkYMU3F//2lef/cbTvGolIhpy46JbmrOKi3afjz9eOQVEh/i3bvZJPL45fi8kuU2Iglvlzs1xv7KYmzf6+V1k71Fw1w/W4smiPPlMIlpJD4p27uVucY4lHhYX3eyZeHxb/F6qfn0IUXBzZj3Q4pMDUXBzAzcT7N+ujR999MuPPPIljl5x0bdj9u+IaLhCULQslnefDstEczAbaCe3n4c16xGPb47fi2ffmP178CZ82iMbDKcp6CPYFw8T7N/iW66ZXQhB9AqKVhl0RLRKvadEw7ciom/3Tzy+OX4vjKGGMft3M+b9bmlOjdcO9Ed6SM6vmwkKbtkTAUREYyjHLadSn977PdELRbOj4Ey3K+LxzfF71RPs39zfqYEbX4nmgbkPsym4I6IPRGykIoQPzA5dKJqd1RetvrU/ftwC8fjm+L34PUbXcyDKP3lZ+zEJFq7EJ9yro5okrowXn3zRSF9UlMIDudUxCX2haHYmJVd9a6XPWaKzYnP8Xph9O3gU3Gg/HrfDdnCs7rjUDtELtwDxiT9fNBqe67pKtApd3Tns3zNFq+ilPHuZ6KzYHL8Xz7710+zfvUfBjZrkgSi41YALnd3JiT8W3Xrs38EEC958OJ/9e77oZlfE45vj98IY6iT7t2QqLbF/o7ExZFN+gFQ9PvE3h/2bQ2MbIh7vz2H/Pkt0ux/i8c3xe2F6ZDjF/g1/Qvnx4FFwqywYf4qv6RvOZ/++PdbNIZqvzyra/9Znic6KzfF7oZuAiTmrxfwuknoMnTjVQLWJ05QuNfu3P7hj0WjjHKI5bl0oOis2x++FJJffYDVshEuxYyGpb4iCm9u+Tc3+HRfNVyYXzdHrQtFZsTl+L+4mUH2Ab3HEAlQBbBhTcONeTOTFh29KNDLlBaL7qyce3xy/F1eAYEROoYYzKbi52Ijh2MniE0Qb8fgl2By/F5amYASEn4gaFlFwq+y+2ST794qis2Jz/F51iIL75DzMSXTbZv9eUXRu99oWv1c9JvOAmS7xrX7z7N8ris6KjfJ7wdacyfKLi+ESMg9YFt0HTI88F1Y20WWwUX6v6lo53FYUnQOb4/diQ2eCiS6GzfF77QVgzhUcNkzPvCI2x++1F8C9tk/PvCI2x++1F8gifdmR4eAS6tVV2iCK8nvtJbEwPVNpWJTfq97JsMj0TKVhOX4veS3ubZ5V2/RMqGcJfi+EXHk5Dltl1TY9k5f4S/B7cQzvN8yqbXqm1bMqw+9V74RVe6d6Ys9SRE8sOYnrKUtLEi4OK8HvVXus2rwPotsMq7avZ7dJ9m+lJ1xnSk+1cmRKT1yQSs+qDL8XzIEkwF/LgGQTCcFQnFXb17PbJPu30pPX3ft6+utHgnryX1PpWZXh98IAB6GbPazNSeh9uZ6s3jb1ZNdRenLQGjxmQ+jJT0jL81uC3wtvG6LxMGaiykfonUTPJj/x+CV6zlnuBj1PXpxQz6oMv1c9zarNJshB6J1KT+izQT3hFlPLdJWewctwPm30KsHvVUdZtdkQHa2x5N0KOD/kZNWe0lNpOEdP1Vtl1RO6sYlm6sn2zJHal+D34mDOu1l6j0m7G4/kVVIM0+AyJLAJowLrGelEfD0jF2fVE8PwnrZD4pgrDnF7qvFBEvcqwe9Vz+YSx9fjNeZ4//BGIrk+JGXVVnr6uUswpxGV/C5Jjchy66nsyW9gT2TsEXsm17Mqw+/FIx1+b4YZrNoc6tsjKxqHDWQ590Jc4hi990dqrp423shlgNITzcZ6nkyNb4/UmNATX4r1XKah/HeOnv2YIJ07hyl7BvW8sOlL8HvVM1i1lYc1E4TeeB0P87jEFb+3QBYAyp/kAHdBT05oeo/92+8KOVb5esIDoOdiDTHbCHtyKFX2xHwiFIjYk/vxJhGdTgl+L6SizQSrNupG3bji2niE3iqk96e4xBW/Nw7cSWHsrR8Q+MpdrCf3OKwnnKkJEY+rtEx1kaznMg2x/JrtCXMF7Sl64qWN2BNKsp4XNn0Jfi9VZT6MWbV5MK9GargYMx4YGcCIzZhVW3GJ+/zeciwn1TWsp3IUP9HpQsTjHKVYT7Qu9FymIe7yq/Zz7HnrsZSznlCyScdSXoLfi+fIgoTeA2075klJxHx2yj5K6K24xBW/twDtx9eAnpmbROmp/B5d8zBmieLxl5pj5ddggYaIZ2xPzllVnj4nTfQH74d0LOUl+L0w0uF3C68Op/yoWaiKK3oieGEzj9D7LLCe8SZhPREz/AkWhRx6ckyFPef7FuwpZ5KzlJfg96rnEXpzQqCiOgZTPN5BeG8SsWorPYNNEtQThQBV0WDk01OlTb49WQ3Wk+3J3XoqPasy/F78tqE3OYQIvTF90SQi9E6oJ3uYr+fgkTeX1BOuM2VPFa6GI1dAOy4gp9WzKsPvhTmywylCb9USqsG6cSlfzX4knMub0hNqBPVsx1Woknpy/xu0p3Kv7rgQUsXptHpWZfi9eI6sP4dVW1UOb6kioAbVScqAJ/VUuaCv54HY6grrye7l2xMBFXr63plcz6oMvxdGOjNZtdsQlzj3TSqZaBOVAU/qifz3pJ5dTvbvoJ5q5KHsiT9BTzWqxV8T6lmV4ffCJMYwj1W7neASnxr+pM1pInqyQ0/pyRGOh8MF9OScT9mTc/+elk70Z3Kzn4sS/F4I5jAEqlk8C4tkGQMxTlymXrWErNon9VQjQaWncno0c0k90UX69kTu5dszk55VGX4vpKIcD/Cu9LMJvVXc6lKzas/RMxhB/YB6yMn+HdFTJekz7ZlJz6oMvxcHczWqR2bKY5bOKyxJLwBbcAhpU6+jmqPnlGMNRdi/43oGh4G+PfmyTHpWZfi9uE6Dr7qAVRsJBOdhTTpW7VR6ovfhhKaknmycoJ7sW/n0rMrwe2EBCcZTy1i1YTJh1eYSQMJmu1zPNjP79xw9VUztvBIXOvF8elZl+L3qnbBqf8H0jCeIZVjKS/B71WPKDXyZnlY8cx7arcSq/YXU03esYnpWJfm9eB2cGteoQQ3yGHx/dEZoSGSjsEUSJU3PtHqW4/eq9kB3Znqm1bMEvxebIxOSN5vpmQQl+L3OAihxZbnmYauE3k5P0dDlyPWGmaHXtWcJfq9lzVY/WDG8cfdykM0X7VaZode1Zwl+r3PNIRZx8cC13JbdSxbFy8YelxSvrtIG7VmC3+tcc7iXDD9KsOVORzSU3WNbdq8V7VmC32tZswlWiV7bT5n3Ys8S/F7LzLFi9Ko3P+Dfiz1L8Huda451f0ylPpYrmw3zxe/FniX4vc41x1oDfvQgEmwOG+aL34U9qzL8XgvMscqAn/vEfgZfvFqVMBTki9+FPasy/F7nmmOtAX89g9d+zmpVXu2O6b+0y/R2Yc+qDL/XAnOsMuCvT/Ha+57E27wYvFIZU85dOlLJXdizKsPvtRdzwL04qcJWjo62yAYX3fOaR0SsbrxyNdUyvV3YsyrD77XAHCt2js00r71aU68WUQ1j4nj20SYpr/1e7FmV4fc61xzrpvZYzdyOee39WMVLov20DEELo86EfPG7sGdVht9rgTnWKkzcm+a1R1hSo0XpQNnt2L2GDLz2e7FnVYbf61xzrFUGrGfw73fjjWgqD2NX67Lx7+/FnlUZfq9zzbHWJAZ3jo3Hv4/oxd6musuOSAzhT9g+tGJhYq1JoRL8XsvMUX4Ktj7Fv48+kQeJHMZUkGvz8O/vxZ5VGX6vc82x1upKHjliArElvnjUtPppvnhcE+ffvwZ7VmX4vc41x1qrK+tT/PvoHw/TfPFcA+un+fevwZ5VGX6vc82x1upKpPbNBP8+Osdumi+eHQ5dZO/x71+DPasy/F7nmmOt1ZWqan/w+Pc59+LOsSX+fZ52hKfyaotVOse1VquW4Pc61xzrpvbtNP8+d3zMjzV4DFvI9A8T/PvXYM+qDL/XMnOsFb2GKP8+DwwP9DPpXLxoZ/DvX4M9qzL8XueaY92yapx/XxUj4G3BBTmZ+Pf3Ys+qDL/XueZYd1KIqW8Gj3/fr6b6AAcYEzpg1etVTQqV4PdaYI4Vp7RP8tqjds8uNRB19G1mXvu92LMqw+91rjnWXZCDkMMexu7VR/ni0V022Xjt92LPqgy/1wJzrLuccD6vPY8l21K89nuxZ1WS32v75sCk0DCb1x5TQCV57fdiz6owv9d8c6zbOXIptfd47VEm7cY/BcVzjm1OXvu92LMqzO810xyrp/Yct3hiR01gc9Ueo0vEMyzI6TLwxe/CntU2+b3WLUw007z2XFNVsUpNE2E6fMjDF78Le1bb5Pdat6w6RHnt0fGpxfW8lrAb7w5CUr9W1X7dsurm+L3WnRSawxcfrK+qGe6sfPG7sGe1TX6vdae070X54pVL+ctWEcmy8sXvwp7VNvm9VlytyhQm7QRfPNyoI157Psl11Ex88buwZ7VNfq91uVXhYby9p9sYX/xe7LlFfq/VuVW5l/wC0MetaM8t8nutzq1ah3grk6D8d1nXnlvk91qdW/UsPbdP3buiPTfK77XWQPpcPXdBPL6iPbfI77Uut+p8Pffyswlb4FbdEL/XXn5MxfQ8iaL8XntJmU3PVBoW5feqdzLgNz1TaViO36s+lis3zhdveibUswS/F0LuvW3zxZueySevSvB7cQwXvnh/uQHzxWNbBKZiyvDF+3rGee1Nz5Mowe9Vj/nip7YH8rSxTNjJbJ28gsyl2+Xhi1d6NiFee16/anqeRAl+r5q4G/yIxf/FdntQ4iJjUOnFkIEvnvUM8tpLPDA956MEvxfMwaHL54vn/aggveUN0/yGdRn44llPXgQBPVuisTQ956AEvxcGOIqCAVG98TjiO9pJISEdbyHbtEnKFw892wle+0jPHsSKesqB5Fhr2bMqw++Ft423bQ1EOirDmS7Eh8tNhZQiE1889Gw9XvuzvEr1/iX1VPZEzt4T/z67Y1Z7VmX4verjBi+1FB1fT76/C93x1kKKcMjDFw89O4/Xnvt0kev36dhJy38qrKeyZ/CNZT2z2rMqw+9VHxd/8jvdHRcKc1Ip7YfxSzveDH173OCViS8eenYerz332sh1uvFGNPWlkAmU1NO359SLyhfn498vwe+lOkdkkf2RIx5N2I1HyJxs+pkZJ7AJo0I7j9d+INYJlRTzK1FeT7Ynp4DKjPxKZLJnVYbfqz7uH0RjoE7DA2Z8QzYTxzPc3ubhi4eefYjXnjto6MmtBT05cpTXE/bkcOWP0DlWZbJnVYbfi0c68UQYgQExTJyS7ZWPL5715Dg0EG+vbA1SPSB6bbQWMzEV1hNh7OD9zJbqAdojf2Ime1Zl+L3qMV983MPEOpJN42ur167Pwxev9OQ0+RDltYcboW7eEnlOeT2l4jDlYUrPfPb03SsLvxdS0WaCL57DgJgGXY+8W5zHcJDok/LFn9STA5VfrcDbz19nFT0HjwbWB3cRmexZleH3UlXmg8cX7w/mb70N0JijVUG+SccXf1JP5eXwKm4wDl3wgMJ6xrsIfh+azPz7Jfi9eI4syBePN4nfeL/fHEK/Wo1BU9q5vCk90TBcxkOnicZm3XgUXEZPdiAogOQsOCLJoWdVht8LI51hmi/eHzkD/ZHLVL12HO3Sjsim9PTdWqmKnp31bFPz2sf1VJ11R4sj4F78XZBsJdezKsPvVc/gi1djRr5SBS2Md7h1E5Yrp/Rkx1LhFjr0Y6aTQx5e+zl6xu2p3oFMelZl+L34bUOUPoz54lGzQQaAK6fSBRXn00YF1lOFUqWnH265BNNl4LWP23NKT1yJT2jL9kyoZ1WG3wtzZIcJvnjkDZE8lNe+YR6X62EJ5/JYT9UFcxWgCTHIQU+c5JJYl3TOMWhPaOt7DByLHdG3Zyo9qzL8XjxHhqUgaK1hzFM6BbZmP+aLT+terCf7FvTsPV57+JYoeRj/mhV3TAnLqlP25J6abdXS+jDuRn17ptKzKsPvhZHOFF98582FoeLH5Qlptt7ji0/rXqwnOhro2T9Y7qIWd0BPCV0qKYSebdKy6pQ9oTPz7yt78iDAt2cqPasy/F6YxBiifPHcTigS8iyYnG88vvi07sV6+jmfP7wN6ol+h/VMm3tN2ZOHIBF7BrOOtHpWZfi9EMxhiD7KF39LM8dICFCYaTy++OSdI+sZyQX9nIxDl6/nkIjXfqY9b491XQwY/Rw3aM9UelZl+L2QivJ7hndFLUbgBkMS2pz6NbKEOU1QTyWOG0npyf14l4fX/qQ947Zi38qqZ1WG34uDuRotIzPlFw5+hlcqnvinWp80R08eW0WGuhi4DRl47WfqGfct8aGselZl+L24ToO3TfHFN7TRQ14+JAHdeC0h3lr0lamqzDP1bI+1x6nZPfSSm9KT7dlS+TCfnlUZfi8sIMG4z+eL991IWo5HYW1mvvj5eva0cA9DfQy+TE+gBL9XPYMvHt9NhQG4F1a9Ddn44s/ScxjPt2BdnunJKMHvVY8pN/Bl+jFfPN4/P2/gyzoqJ7Y5qUHm6Nkfee05LTM9gXL8XrAIvhLHcx58IT/A90eQh4GQjcIWSZQ0PdPqWY7fq9oD3ZnpmVbPEvxebI5MSN5spmcSlOD3MlwtSvB7Ga4WJfi9DFeLEvxehqtFCX4vw9WiBL+X4WpRgt/LcLUowe9luFoE+L0s9zKkQmBBjuVehlQITApZ7mVIhcCCHMu9DKkQqHsl51Y1XC0CC3KSc6sarhanF+QU/rFjwxcJgQU5yblVDVeLwIKc5OSXhqtFYEGOuZchFQILcpJzqxquFoHCRHJuVcPVQpXos3CrGq4WgQU5yblVDVcLVaK/+8kqi16GJFAl+izcqoarReAH95JzqxquFoEf3LPlhIZUCPzgni0nNKRC4Af3bDmhIRUCP7hnywkNqRD4wb3k3KqGq0XgB/eSc6sarhaBH9z7v//9wx9/9+uE3KqGqwVvRPv5f799fznh73/zy08/fu9Xn39iqb3hQqDI5dxJyvd37uX8Tk65VMzB/deNAlxX+uH7r8tEkpx39/uf7jJ34AKjO3Y3OuD8Z598IA9xf3UHLvVzj5Vr5jw/8mS5155/+fPlc45QiJvSwR07l3KO5B6l3Us8TIoUDpLvS2lfOlQ5LyflU2aTEA9xo9wrB3yNj6nnn3yy+3z+m0/Y8y9/vlypTkoiHrxXTk7pIL6l3evPf/qty8DEw146li7k05chZ6b0Jgd9Bg8B+L/x58efjOfY8y98fvCCuFMGdUDcEl966F6/+Oh+JHQujBjmbhaISOfIciDpvwwNWCcAN+JeDFP5OQr+8+c/2Z5/+fPPFRrUgeOW8yW5/caddRk+1t4LXNqPFWFyRgpl6jKclAMcy424FydlY7ic4b/6zz/5ZP92e/6y5/OVEaEsztfBOYx/o/PLG/Et5ddyEloqqTiJy3wtZU6Av6r7lNwTfwreO/PJ7gIHe/7lz8eVcaHqvNLBHQej6f8DWLH7QPy3DN8AAAAASUVORK5CYII=');

    {%codetools on}
  end;

{%codetools off}
procedure TGearFormHelper.Setup();
var
  i: Integer;
  slot: ERSEquipment;
begin
  Self.Config.Setup('gearlayouts' + PATH_SEP + ToStr(PlayerIndex));
  SetLength(Self.Sets, Self.Config.Data.Count);

  try
    for i := 0 to Self.Config.Data.Count-1 do
    begin
      Self.Sets[i].Name := Self.Config.Data.Key[i];

      for slot := Low(ERSEquipment) to High(ERSEquipment) do
        Self.Sets[i].Items[slot] := Self.Config.Data.Item[i].Item[Ord(slot)].AsString;
    end;
  except
    FileDelete(Self.Config.Path); //corrupted config.
  end;
end;

procedure TGearFormHelper.UpdateConfig();
var
  i: Integer;
begin
  Self.Config.Data := new TJSONObject();
  for i := 0 to High(Self.Sets) do
    Self.Config.Data.Add(Self.Sets[i].Name, Self.Sets[i].ToJSON());
  Self.Config.SaveConfig();
end;

procedure TGearFormHelper.AddLayout(gear: TRSGear);
begin
  Self.Sets += gear;
  Self.Config.Data.Add(gear.Name, gear.ToJSON());
  Self.UpdateConfig();
end;

procedure TGearFormHelper.RemoveLayout(index: Integer);
begin
  Delete(Self.Sets, index, 1);
  Self.UpdateConfig();
end;


procedure TGearFormHelper.UpdateListBox();
var
  idx, i: Integer;
begin
  if Self.ItemLists[Self.Selected].Items.Count = 0 then
  begin
    Self.ItemLists[Self.Selected].Items.BeginUpdate();
    Self.ItemLists[Self.Selected].Items.Add('Nothing');
    Self.ItemLists[Self.Selected].Items.Add('Anything');

    idx := Ord(Self.Selected);
    if idx = 11 then idx := 3;
    for i := 0 to GearData.Data.Item[idx].Count-1 do
      Self.ItemLists[Self.Selected].Items.Add(
        GearData.Data.Item[idx].Item[i].AsString.Capitalize()
      );

    if Self.Selected = ERSEquipment.WEAPON then
    begin
      Self.TwoHandedIndex := Self.ItemLists[Self.Selected].Items.Count;

      for i := 0 to GearData.Data.Item[12].Count-1 do
        Self.ItemLists[Self.Selected].Items.Add(
          GearData.Data.Item[12].Item[i].AsString.Capitalize()
        );
    end;

    Self.ItemLists[Self.Selected].Items.EndUpdate();
  end;

  i := Self.ItemLists[Self.Selected].Items.IndexOf(Self.Gear.Items[Self.Selected]);
  if i > 0 then
    Self.ItemLists[Self.Selected].ItemIndex := i;
end;

procedure TGearFormHelper.UpdateFilters(slot: ERSEquipment; items: TStringArray = []);
begin
  Self.FilteredLists[slot].Items.BeginUpdate();
  Self.FilteredLists[slot].Clear();
  Self.FilteredLists[slot].Items.Add('Nothing');
  Self.FilteredLists[slot].Items.Add('Anything');
  Self.FilteredLists[slot].Items.AddStrings(items);
  Self.FilteredLists[slot].Items.EndUpdate();
end;


procedure TGearFormHelper.DrawItem(slot: ERSEquipment; background: TImage);
var
  id: String;
  img, item: TImage;
begin
  id := ItemFinder.Database.Get(Self.Gear.Items[slot].ToLower(), 'item', 'id');
  img := Self.SLOT_IMG.Copy();
  item := ItemFinder.GetImage(id);

  img.DrawImage(item, [2,2]);
  background.DrawImage(img, Self.SlotBoxes[slot].TopLeft);
end;

procedure TGearFormHelper.UpdateBackground();
var
  slot: ERSEquipment;
  bg: TImage;
begin
  bg := Self.BACKGROUND_IMG.Copy();

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
    case Self.Gear.Items[slot] of
      '', 'Nothing': ;
      'Anything': ; //TODO....
      else Self.DrawItem(slot, bg);
    end;

  Self.ImageBox.SetImage(bg.Resize(EImageRotateAlgo.NEAREST_NEIGHBOUR, FormUtils.Scale));
end;


{$H-}
procedure TGearFormHelper.OnImgPaint(sender: TImageBox; canvas: TImageBoxCanvas; r: TLazRect);
var
  slot: ERSEquipment;
begin
  for slot := Low(ERSEquipment) to High(ERSEquipment) do
    if slot = Self.Selected then
      canvas.DrawBox(Self.DPISlots[slot], $00FF00);
end;

procedure TGearFormHelper.OnSelectionChange(sender: TLazObject; user: Boolean);
begin
  Self.Gear.Items[Self.Selected] := TLazListBox(sender).GetSelectedText();
  if (Self.Selected = ERSEquipment.WEAPON) and (TLazListBox(sender).ItemIndex >= Self.TwoHandedIndex) then
    Self.Gear.Items[ERSEquipment.SHIELD] := 'Nothing';
  Self.UpdateBackground();
end;

procedure TGearFormHelper.OnSaveClick(sender: TLazObject);
var
  i: Integer;
begin
  Self.Gear.Name := Self.LayoutName.Text;
  Self.LayoutCombobox.Items.Strings[Self.Index] := Self.LayoutName.Text;
  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Strings[Self.Index] := Self.LayoutName.Text;

  if Self.Index = (High(Self.Sets) + 1) then
  begin
    Self.AddLayout(Self.Gear);
    Self.LayoutCombobox.Items.Add('New Layout');
    Self.DuplicateButton.Enabled := True;
    Self.RemoveButton.Enabled := True;
  end
  else
    Self.Sets[Self.Index] := Self.Gear;
  Self.UpdateConfig();
end;

procedure TGearFormHelper.OnDuplicateClick(sender: TLazObject);
var
  gear: TRSGear;
  i: Integer;
begin
  if Self.Index = (High(Self.Sets) + 1) then Exit;
  gear := Self.Sets[Self.Index];
  gear.Name += ' duplicate';
  Self.AddLayout(gear);
  Self.LayoutCombobox.Items.Insert(High(Self.Sets), gear.Name);
  Self.LayoutCombobox.ItemIndex := High(Self.Sets);
  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Add(gear.Name);

  Self.OnLayoutChange(Self.LayoutCombobox);
end;

procedure TGearFormHelper.OnRemoveClick(sender: TLazObject);
var
  i: Integer;
begin
  if Self.Index = (High(Self.Sets) + 1) then Exit;

  Self.RemoveLayout(Self.Index);
  Self.LayoutCombobox.Items.Delete(Self.Index);

  for i := 0 to High(Self.LinkedComboboxes) do
    Self.LinkedComboboxes[i].Items.Delete(Self.Index);

  Self.Index := Max(Self.Index-1, 0);
  Self.LayoutCombobox.ItemIndex := Max(High(Self.Sets), 0);
  Self.OnLayoutChange(Self.LayoutCombobox);
end;
{$H+}

procedure TGearFormHelper.OnImgClick(sender: TImageBox; x, y: Integer);
var
  i: Integer;
begin
  i := Self.DPISlots.ContainsPoint([x,y]);
  if i >= 0 then
  begin
    Self.ItemLists[Self.Selected].Hide();
    Self.ItemLists[Self.Selected].Enabled := False;
    Self.FilteredLists[Self.Selected].Hide();
    Self.FilteredLists[Self.Selected].Enabled := False;

    Self.Selected := ERSEquipment(i);
    Self.ListLabel.Caption := 'Selected slot: ' + Self.SLOT_NAMES[Self.Selected];
    Self.UpdateListBox();

    if Self.UnfilterButton.Enabled then
    begin
      Self.FilteredLists[Self.Selected].Show();
      Self.FilteredLists[Self.Selected].Enabled := True;
    end
    else
    begin
      Self.ItemLists[Self.Selected].Show();
      Self.ItemLists[Self.Selected].Enabled := True;
    end;
  end;

  sender.Repaint();
end;


procedure TGearFormHelper.OnLayoutChange(sender: TLazObject);
var
  slot: ERSEquipment;
begin
  Self.Index := TLazComboBox(sender).ItemIndex;
  if Self.Index > High(Self.Sets) then
  begin
    Self.Gear.Name := TLazComboBox(sender).Text;
    for slot := Low(ERSEquipment) to High(ERSEquipment) do
      Self.Gear.Items[slot] := 'Nothing';

    Self.DuplicateButton.Enabled := False;
    Self.RemoveButton.Enabled := False;
  end
  else
  begin
    Self.Gear := Self.Sets[Self.Index];
    Self.DuplicateButton.Enabled := True;
    Self.RemoveButton.Enabled := True;
  end;

  Self.ItemLists[Self.Selected].ItemIndex := Self.ItemLists[Self.Selected].Items.IndexOf(Self.Gear.Items[Self.Selected]);
  Self.LayoutName.Caption := TLazComboBox(sender).Text;

  Self.OnUnfilterClick(Self.UnfilterButton);

  Self.UpdateBackground();
end;

procedure TGearFormHelper.OnReadClick(sender: TLazObject);
var
  btn: TLazButton;
  slot: ERSEquipment;
  items: TRSItemArray;
begin
  btn := TLazButton(sender);
  btn.Caption := 'Loading...';
  btn.Font.Style := btn.Font.Style - [ELazFontStyles.fsBold];
  btn.Enabled := False;
  btn.Update();

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
  begin
    items := Equipment.Items.Discover(Ord(slot));

    if items <> [] then
      Self.Gear.Items[slot] := items[0]
    else
      Self.Gear.Items[slot] := 'Nothing';

    Self.UpdateFilters(slot, items);
  end;

  Self.UpdateBackground();

  Self.ItemLists[Self.Selected].Hide();
  Self.ItemLists[Self.Selected].Enabled := False;
  Self.FilteredLists[Self.Selected].Show();
  Self.FilteredLists[Self.Selected].Enabled := True;

  btn.Caption := 'Read From Client';
  btn.Font.Style := [ELazFontStyles.fsBold];
  btn.Enabled := True;
  Self.UnfilterButton.Enabled := True;
end;

procedure TGearFormHelper.OnUnfilterClick(sender: TLazObject);
begin
  Self.FilteredLists[Self.Selected].Hide();
  Self.FilteredLists[Self.Selected].Enabled := False;

  Self.ItemLists[Self.Selected].Show();
  Self.ItemLists[Self.Selected].Enabled := True;

  TLazButton(sender).Enabled := False;
end;

procedure TGearFormHelper.OnSearchChange(sender: TLazObject);
var
  i: Integer;
begin
  if TLazEdit(sender).IsEmpty() then Exit;
  i := Self.ItemLists[Self.Selected].Search(TLazEdit(sender).Text);
  if i > 0 then
    Self.ItemLists[Self.Selected].ItemIndex := i;
end;
{%codetools on}

(*
## GearForm.CreateGearCombobox
```pascal
function TGearFormHelper.CreateGearCombobox(owner: Pointer; caption: TLazLabel = nil): TLazComboBox;
```
This is the only method from {ref}`Gear Form` you might need to use.
This will create a combobox for you whose contents auto-update with changes made
on the `Gear Form`.
*)
function TGearFormHelper.CreateGearCombobox(owner: Pointer; caption: String = 'Gear:'; left, top: Integer = 0): TLazComboBox;
var
  i: Integer;
begin
  Result := TLazComboBox.CreateEx(owner, caption, 'Pick a gear layout.', left, top, 170, 40);
  Self.LinkedComboboxes += Result;

  for i := 0 to High(Self.Sets) do
    Result.Items.Add(Self.Sets[i].Name);

  Result.ItemIndex := Low(Self.Sets);
end;

var
(*
## GearForm variable
Global {ref}`TGearFormHelper` variable.
*)
  GearForm: TGearFormHelper;

(*
## ScriptForm CreateGearTab
```pascal
function TScriptForm.CreateGearTab(): TLazTabSheet;
```
Sets up a `TLazTabSheet` on your `TScriptForm` to configure your `Gear Layouts`.

Example:
```pascal
{$I WaspLib/osrs.simba}
var
  form: TScriptForm;
begin
  form.Setup();
  form.CreateGearTab();
  form.Run();
end.
```
```{figure} ../../images/gearform.gif
```
For a more complete example check out the file `Simba/Includes/WaspLib/examples/gear_form.simba`.
*)
function TScriptForm.CreateGearTab(): TLazTabSheet;
var
  save, read: TLazButton;
  width: Integer;
  i: Integer;
  slot: ERSEquipment;
  search: TLazEdit;
begin
  GearForm.Setup();
  GearData.Setup();

  Result := Self.CreateTab('Gear Layouts');
  width := 280;

  GearForm.ListLabel := TLazLabel.CreateEx(Result, 'Selected slot: ' + GearForm.SLOT_NAMES[GearForm.Selected], 'Click a slot on the right side to update the list below.', 40, 20);

  search := TLazEdit.CreateEx(Result, 'Search:', 'Search the list below.', 40, 80, width);
  search.OnChange := @GearForm.OnSearchChange;

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
  begin
    GearForm.ItemLists[slot] := TLazListBox.CreateEx(Result, '', 'Click a slot on the right side to update the list below.', 0,0, width, 340);
    GearForm.ItemLists[slot].AnchorVertically(search, 10);
    GearForm.ItemLists[slot].OnSelectionChange := @GearForm.OnSelectionChange;
    if slot <> ERSEquipment.HEAD then
    begin
      GearForm.ItemLists[slot].Hide();
      GearForm.ItemLists[slot].Enabled := False;
    end;

    GearForm.FilteredLists[slot] := TLazListBox.CreateEx(Result, '', 'Click a slot on the right side to update the list below.', 0,0, width, 340);
    GearForm.FilteredLists[slot].AnchorVertically(search, 10);
    GearForm.FilteredLists[slot].OnSelectionChange := @GearForm.OnSelectionChange;
    GearForm.FilteredLists[slot].Hide();
    GearForm.FilteredLists[slot].Enabled := False;
  end;

  GearForm.UnfilterButton := TLazButton.CreateEx(Result, 'Remove list filter', 'Remove the current filter.', 0,0, width);
  GearForm.UnfilterButton.AnchorVertically(search, 360);
  GearForm.UnfilterButton.Enabled := False;
  GearForm.UnfilterButton.OnClick := @GearForm.OnUnfilterClick;

  GearForm.LayoutCombobox := TLazComboBox.CreateEx(Result, 'Layout:', 'Select a gear layout.', Round(Result.Width / FormUtils.Scale) - width - 80, 30, width);
  for i := 0 to High(GearForm.Sets) do
    GearForm.LayoutCombobox.Items.Add(GearForm.Sets[i].Name);
  GearForm.LayoutCombobox.Items.Add('New Layout');
  GearForm.LayoutCombobox.ItemIndex := 0;
  GearForm.LayoutCombobox.OnChange := @GearForm.OnLayoutChange;

  GearForm.LayoutName := TLazEdit.CreateEx(Result, 'Layout name:', 'Change the current layout name.', 0,0, width);
  GearForm.LayoutName.AnchorVertically(GearForm.LayoutCombobox, 30);

  save := TLazButton.CreateEx(Result, 'Save' , 'Save the current gear layout.');
  save.AnchorVertically(GearForm.LayoutName, 10);
  save.BorderSpacing.Left := 18;
  save.OnClick := @GearForm.OnSaveClick;

  GearForm.DuplicateButton := TLazButton.CreateEx(Result, 'Duplicate' , 'Duplicate the current gear layout.');
  GearForm.DuplicateButton.AnchorHorizontally(save, 10);
  GearForm.DuplicateButton.OnClick := @GearForm.OnDuplicateClick;

  GearForm.RemoveButton := TLazButton.CreateEx(Result, 'Remove' , 'Remove the current gear layout.');
  GearForm.RemoveButton.AnchorHorizontally(GearForm.DuplicateButton, 10);
  GearForm.RemoveButton.OnClick := @GearForm.OnRemoveClick;

  GearForm.ImageBox := TImageBox.CreateEx(Result, '', '', Round(Result.Width / FormUtils.Scale) - width - 36, 170, 202, 273);
  GearForm.ImageBox.AllowZoom := False;
  GearForm.ImageBox.AllowMoving := False;
  GearForm.ImageBox.ShowScrollBars := False;
  GearForm.ImageBox.ShowStatusBar := False;
  GearForm.ImageBox.OnImgPaint := @GearForm.OnImgPaint;
  GearForm.ImageBox.OnImgClick := @GearForm.OnImgClick;
  GearForm.UpdateBackground();

  read := TLazButton.CreateEx(Result, 'Read From Client' , 'Read the layout from the client.', 0, 0, 204);
  read.Font.Style := [ELazFontStyles.fsBold];
  read.AnchorVertically(GearForm.ImageBox, 10);
  read.OnClick := @GearForm.OnReadClick;

  GearForm.SlotBoxes := Equipment.GetSlots([9, 35]);
  for i := 0 to High(GearForm.SlotBoxes) do
    with GearForm.SlotBoxes[i] do
      GearForm.DPISlots += [Round(FormUtils.Scale * X1), Round(FormUtils.Scale * Y1), Round(FormUtils.Scale * X2), Round(FormUtils.Scale * Y2)];

  GearForm.UpdateListBox();
  GearForm.OnLayoutChange(GearForm.LayoutCombobox);
end;
