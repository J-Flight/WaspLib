(*
# BankPin
Methods to interact with the bank pin interface.

```{figure} ../../images/bankpin_interface.png
The bankpin interface.
```
*)

{$DEFINE SRLT_BANKPIN_INCLUDED}
{$INCLUDE_ONCE SRLT/osrs.simba}

type
(*
## TRSBankPin
Main record to interact with the {ref}`BankPin` interface.
*)
  TRSBankPin = record
    Bounds: TBox;
    Buttons: TBoxArray;
  end;

(*
## BankPin.SetupInterface
```pascal
procedure TRSBankPin.SetupInterface();
```
Internal method used to setup the {ref}`TRSBankPin` coordinates.
This is automatically called for you on the {ref}`BankPin variable`.
*)
procedure TRSBankPin.SetupInterface();
begin
  with InterfaceArea do
  begin
    Self.Bounds.X1 := Max(Floor(Center.X-(Width-1)/2), Floor(Center.X-487/2));
    Self.Bounds.Y1 := Max(Floor(Center.Y-(Height-3)/2), Center.Y-148);
    Self.Bounds.X2 := Min(Floor(Center.X+(Width-1)/2), Floor(Center.X +487/2));
    Self.Bounds.Y2 := Min(Floor(Center.Y+(Height-3)/2), Center.Y+157);
  end;

  Self.Buttons := TBoxArray.Create(Self.Bounds.TopLeft.Offset(21,84), 3, 3, 63, 63, [31, 9]);
  Self.Buttons.Insert(Self.Buttons[2].Offset([84, 0]), 3);
end;

(*
## BankPin.IsOpen
```pascal
function TRSBankPin.IsOpen(): Boolean;
```
Returns true if the bank pin is open.

Example:
```pascal
WriteLn BankPin.IsOpen();
```
*)
function TRSBankPin.IsOpen(): Boolean;
begin
  Result := Target.HasColor($060D56, 10, 3400, Self.Buttons[0]);
end;

(*
## BankPin.WaitOpen
```pascal
function TRSBankPin.WaitOpen(time: Integer = 600; interval: Integer = -1): Boolean;
```
Returns true if the bank pin opens within `time` milliseconds..

Example:
```pascal
WriteLn BankPin.WaitOpen();
```
*)
function TRSBankPin.WaitOpen(time: Integer = 600; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.IsOpen(), interval, time);
end;


(*
## BankPin.IsLoading
```pascal
function TRSBankPin.IsLoading(): Boolean;
```
Returns true if the bank pin is loading.
The bank pin is loading when the interface is open but the pin numbers are not
available yet.

Example:
```pascal
WriteLn BankPin.IsLoading();
```
*)
function TRSBankPin.IsLoading(): Boolean;
begin
  Result := not Target.HasColor($007FFF, 0, 1, Self.Buttons[0]);
end;

(*
## BankPin.WaitLoading
```pascal
function TRSBankPin.WaitLoading(time: Integer = 600; interval: Integer = -1): Boolean;
```
Returns true if the bank pin finishes loading within `time` milliseconds..
For more information on bank pin loading read {ref}`BankPin.IsLoading`.

Example:
```pascal
WriteLn BankPin.WaitLoading();
```
*)
function TRSBankPin.WaitLoading(time: Integer = 600; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(not Self.IsLoading(), interval, time);
end;


(*
## BankPin.FindDigit
```pascal
function TRSBankPin.FindDigit(digit: Char): Integer;
```
Finds a bank pin digit.

Example:
```pascal
WriteLn BankPin.FindDigit('5');
```
*)
function TRSBankPin.FindDigit(digit: Char): Integer;
var
  i: Integer;
  hovering: Integer = -1;
begin
  for i := 0 to High(Self.Buttons) do
  begin
    if Self.Buttons[i].Contains(Mouse.Position()) then
    begin
      hovering := i;
      Continue;
    end;

    if OCR.Locate(Self.Buttons[i], digit, [$007FFF], 0, RSFonts.BOLD) > 0.95 then
      Exit(i);
  end;

  if hovering > -1 then Exit(hovering);
  Result := -1;
end;

(*
## BankPin.ClickDigit
```pascal
function TRSBankPin.ClickDigit(digit: Char): Boolean;
```
Finds a digit with {ref}`BankPin.FindDigit` and clicks it if it was found.

Example:
```pascal
WriteLn BankPin.ClickDigit('5');
```
*)
function TRSBankPin.ClickDigit(digit: Char): Boolean;
var
  i: Integer;
begin
  i := Self.FindDigit(digit);
  if i < 0 then Exit;
  Mouse.Click(Self.Buttons[i], EMouseButton.LEFT);
  Result := True;
end;


(*
## BankPin.GetDigitIndex
```pascal
function TRSBankPin.GetDigitIndex(): Integer;
```
Returns the index of the next digit to enter.

Example:
```pascal
WriteLn BankPin.GetDigitIndex();
```
*)
function TRSBankPin.GetDigitIndex(): Integer;
const
  DIGITS: TStringArray = ['FIRST', 'SECOND', 'THIRD', 'FOURTH'];
var
  i: Integer;
begin
  for i := 0 to High(DIGITS) do
    if OCR.Locate(Self.Bounds, DIGITS[i], [$FFFFFF], 0, RSFonts.BOLD) > 0.95 then
      Exit(i+1);
end;

(*
## BankPin.Enter
```pascal
function TRSBankPin.Enter(pin: String): Boolean;
```
Handles entering the bank pin. Will do 4 attempts before terminating with a fatal error.

Example:
```pascal
BankPin.Enter('0000');
```
*)
function TRSBankPin.Enter(pin: String; attempts: Integer = 3): Boolean;

  function _IsUnlocked(): Boolean;
  begin
    if Self.IsOpen() then
      Exit;
    Result := not Chat.GetOptionsString().ContainsAny(['try again', 'forgotten your PIN', 'PIN incorrectly several']);
  end;

var
  i, idx: Integer;
  digit: Char;
  chatCache: String;
begin
  if attempts <= 0 then Exit;

  for i := 0 to 3 do
  begin
    idx := Self.GetDigitIndex();
    if idx = 0 then Break;

    digit := pin[idx];
    if not Self.ClickDigit(digit) then Break;
    Biometrics.Sleep(200, 1500, ERandomDir.LEFT);
    SleepUntil((idx <> Self.GetDigitIndex()) and not Self.IsLoading(), 100, 5000);
  end;

  chatCache := Chat.GetOptionsString();
  if 'try again' in chatCache then
  begin
    Chat.ContinueChat();
    Exit(Self.Enter(pin, Dec(attempts)));
  end;

  if 'forgotten your PIN' in chatCache then
  begin
    SleepUntil(not ('forgotten your PIN' in Chat.GetOptionsString()), 300, 20000);
    Exit(Self.Enter(pin, Dec(attempts)));
  end;

  if 'PIN incorrectly several' in chatCache then
    raise GetDebugLn('BankPin', 'Failed to enter the bank pin several times. Check that the pin is correct.');

  Result := SleepUntil(_IsUnlocked(), 300, 3000) or Self.Enter(pin, Dec(attempts));
end;

{$IFDEF SRLT_DEBUG_INTERFACES}
procedure TRSBankPin.Draw(img: TImage);
var
  i: Integer;
begin
  if not Self.IsOpen() then Exit;


  img.DrawColor := $00FFFF;
  img.DrawBox(Self.Bounds);

  img.DrawColor := $FFFFFF;
  img.DrawBoxArray(Self.Buttons, False);
end;

procedure TRSBankPin.ShowOnClient();
var
  img: TImage;
begin
  img := TImage.CreateFromTarget();
  Self.Draw(img);
  img.Show();
  img.Free();
end;
{$ENDIF}

var
(*
## BankPin variable
Global {ref}`TRSBankPin` variable.
*)
  BankPin: TRSBankPin;
