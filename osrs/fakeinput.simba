(*
# RemoteInput
Remote Input is an external plugin which provides, well, "remote" input.

In other words, it's the tool WaspLib uses so Simba doesn't use your real mouse and
keyboard while botting.

Remote input also gives you a full reflection environment to bot with if you
desire but in the case of WaspLib, it's used exclusively for input and you need to
either include a reflection library, implement one yourself or use the plugin
functions directly.

If you wish to completely disable remote input you can do so by adding the
following compiler directive before you include WaspLib in your script:
```pascal
{$DEFINE WL_DISABLE_FAKE_INPUT}
```

You can find remote input source code here: https://github.com/Brandon-T/RemoteInput
*)
{$DEFINE WL_FAKE_INPUT_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

{.$DEFINE WL_DISABLE_FAKE_INPUT}

{$IFNDEF WL_DISABLE_FAKE_INPUT}

{$loadlib Plugins/wasp-plugins/libremoteinput/libremoteinput}

type
(*
## TRemoteInput
Main record used to interact with the remote input plugin.
*)
  TRemoteInput = record
    Target: Pointer;
  end;

(*
## TRemoteInput.Setup
```pascal
procedure TRemoteInput.Setup();
```
Internal method used to setup {ref}`TRemoteInput`.
This is automatically called for you on the {ref}`RemoteInput variable`.
*)
procedure TRemoteInput.Setup();
var
  timer: TCountDown;
  window: TWindowHandle;
  pid: TProcessID;
begin
  {$IFDEF SIMBAHEADLESS}
  window := StrToInt(Target.ToString().Between('Handle=', ','));
  {$ELSE}
  window := GetSimbaTargetWindow();
  {$ENDIF}

  if window.GetClassName() <> 'SunAwtCanvas' then
    Exit;

  pid := window.GetPID();

  try
    RIInject(pid);
  except
    if GetExceptionMessage() = 'Access violation' then
      ErrorDialog(
        'RemoteInput',
        'A different RemoteInput has already been paired with this client.' +
        LINE_SEP + LINE_SEP + 'Please open a new one.'
      );
    raise GetExceptionMessage();
  end;

  timer.Start(2000);
  repeat
    try
      Self.Target := EIOS_PairClient(pid);
    except
      if GetExceptionMessage() = 'Access violation' then
        ErrorDialog(
          'RemoteInput',
          'A different RemoteInput has already been paired with this client.' +
          LINE_SEP + LINE_SEP + 'Please open a new one.'
        );
      raise GetExceptionMessage();
    end;

    if Self.Target <> nil then
      Break;
    Sleep(100);
  until timer.IsFinished;

  if Self.Target = nil then
  begin
    WriteLn GetDebugLn('RemoteInput', 'Invalid target for RemoteInput, RemoteInput will not be used.', ELogLevel.WARN);
    Exit;
  end;

  try
    Target.SetPlugin({$MACRO LOADEDLIB(libremoteinput)}, ToStr(pid), RSClient.Canvas);
  except
    if GetExceptionMessage() = 'Access violation' then
      ErrorDialog(
        'RemoteInput',
        'A different RemoteInput has already been paired with this client.' +
        LINE_SEP + LINE_SEP + 'Please open a new one.'
      );

    raise GetExceptionMessage();
  end;
end;

var
(*
## RemoteInput variable
Global {ref}`TRemoteInput` variable.
*)
  RemoteInput: TRemoteInput;

{$IFDEF WINDOWS}
{$loadlib Plugins/wasp-plugins/waspinput/waspinput}

type
(*
## TWaspInput
Main record used to interact with the wasp input plugin.
*)
  TWaspInput = record
    Target: Pointer;
  end;

(*
## TWaspInput.Setup
```pascal
procedure TWaspInput.Setup();
```
Internal method used to setup {ref}`TWaspInput`.
This is automatically called for you on the {ref}`WaspInput variable`.
*)
procedure TWaspInput.Setup();
var
  window: TWindowHandle;
  pid: TProcessID;
begin
  {$IFDEF SIMBAHEADLESS}
  window := StrToInt(Target.ToString().Between('Handle=', ','));
  {$ELSE}
  window := GetSimbaTargetWindow();
  {$ENDIF}

  if window.GetClassName() <> 'JagRenderView' then
    Exit;

  pid := window.GetPID();
  Inject({$MACRO LOADEDLIB(waspinput)}, pid);
  Target.SetPlugin({$MACRO LOADEDLIB(waspinput)}, ToStr(pid), RSClient.Canvas);
  Self.Target := @pid;
end;

var
  WaspInput: TWaspInput;
{$ENDIF}
{$ENDIF}


(*
## TRSClient.Clear
```pascal
procedure TRSClient.Clear();
```
Clears the client canvas.

Example:
```pascal
RSClient.Clear();
```
*)
procedure TRSClient.Clear();
begin
  if Self.Canvas <> nil then
    Self.Canvas.Clear();
end;


(*
## RSClient.LoseFocus()
```pascal
procedure TRSClient.LoseFocus();
procedure TRSClient.LoseFocus(delay: Int32);
```
Loses focus if available. Maybe useful for antiban.
You can optionally delay losing focus by passing in a `delay`.
*)
procedure TRSClient.LoseFocus();
{$IFNDEF WL_DISABLE_FAKE_INPUT}
var
  space: TBoxArray;
{$ENDIF}
begin
  {$IFNDEF WL_DISABLE_FAKE_INPUT}
  case RSClient.Client of
    {$IFDEF WINDOWS}
    ERSClient.OFFICIAL:
      if WaspInput.Target <> nil then
      begin
        space := Target.Bounds.Expand(50).Invert(Target.Bounds.Expand(500));
        if Length(space) = 0 then Exit;

        WriteLn(GetDebugLn('RSClient', 'LoseFocus method is not implemented for WaspInput yet.', ELogLevel.WARN));
        Exit;
      end;
    {$ENDIF}
    ERSClient.LEGACY, ERSClient.RUNELITE:
      if RemoteInput.Target <> nil then
      begin
        space := Target.Bounds.Expand(50).Invert(Target.Bounds.Expand(500));
        if Length(space) = 0 then Exit;

        Target.MouseMove(Space.Random());
        Target.MouseClick(EMouseButton.LEFT);
        Exit;
      end;
  end;
  {$ENDIF}
  WriteLn(GetDebugLn('RSClient', 'LoseFocus method is not implemented without a fake input tool.', ELogLevel.WARN));
end;

procedure TRSClient.LoseFocus(delay: UInt32); overload;
begin
  Sleep(delay);
  Self.LoseFocus();
end;


(*
## TRSClient.IsInputEnabled
```pascal
function TRSClient.IsInputEnabled(): Boolean;
```
Returns True/False if the user **REAL** input is enabled/disabled,
fully or partially, on the client.

Example:
```pascal
WriteLm RSClient.IsInputEnabled();
```
*)
function TRSClient.IsInputEnabled(): Boolean;
begin
  {$IFNDEF WL_DISABLE_FAKE_INPUT}
  case RSClient.Client of
    {$IFDEF WINDOWS}
    ERSClient.OFFICIAL: if WaspInput.Target <> nil then Exit(GetInputState());
    {$ENDIF}
    ERSClient.LEGACY, ERSClient.RUNELITE:
      if RemoteInput.Target <> nil then
        Exit(EIOS_IsMouseInputEnabled(RemoteInput.Target) and EIOS_IsKeyboardInputEnabled(RemoteInput.Target));
  end;
  {$ENDIF}
  WriteLn(GetDebugLn('RSClient', 'IsInputEnabled method is not implemented without a fake input tool.', ELogLevel.WARN));
  Result := False;
end;


(*
## TRSClient.DisableRealInput
```pascal
procedure TRSClient.DisableRealInput();
```
Disables the user real input on the client for both the mouse and keyboard.

Example:
```pascal
RSClient.DisableRealInput();
```
*)
procedure TRSClient.DisableRealInput();
begin
  {$IFNDEF WL_DISABLE_FAKE_INPUT}
  case RSClient.Client of
    {$IFDEF WINDOWS}
    ERSClient.OFFICIAL: if WaspInput.Target <> nil then SetInputState(False);
    {$ENDIF}
    ERSClient.LEGACY, ERSClient.RUNELITE:
      if RemoteInput.Target <> nil then
      begin
        EIOS_SetMouseInputEnabled(RemoteInput.Target, False);
        EIOS_SetKeyboardInputEnabled(RemoteInput.Target, False);
      end;
  end;
  {$ELSE}
  WriteLn(GetDebugLn('RSClient', 'DisableRealInput method is not implemented without a fake input tool.', ELogLevel.WARN));
  {$ENDIF}
end;

(*
## TRSClient.EnableRealInput
```pascal
procedure TRSClient.EnableRealInput();
```
Enables the user real input on the client for both the mouse and keyboard.

Example:
```pascal
RSClient.EnableRealInput();
```
*)
procedure TRSClient.EnableRealInput();
begin
  {$IFNDEF WL_DISABLE_FAKE_INPUT}
  case RSClient.Client of
    {$IFDEF WINDOWS}
    ERSClient.OFFICIAL: if WaspInput.Target <> nil then SetInputState(True);
    {$ENDIF}
    ERSClient.LEGACY, ERSClient.RUNELITE:
      if RemoteInput.Target <> nil then
      begin
        EIOS_SetMouseInputEnabled(RemoteInput.Target, True);
        EIOS_SetKeyboardInputEnabled(RemoteInput.Target, True);
      end;
  end;
  {$ELSE}
  WriteLn(GetDebugLn('RSClient', 'EnableRealInput method is not implemented without a fake input tool.', ELogLevel.WARN));
  {$ENDIF}
end;

(*
## TRSClient.InjectInput
```pascal
procedure TRSClient.InjectInput();
```
Injects the client with one of the fake input tools:
- {ref}`RemoteInput`
- {ref}`WaspInput`

Example:
```pascal
RSClient.InjectInput();
```
*)
procedure TRSClient.InjectInput();
begin
  {$IFNDEF WL_DISABLE_FAKE_INPUT}
  case Self.Client of
    {$IFDEF WINDOWS}
    ERSClient.OFFICIAL: WaspInput.Setup();
    {$ENDIF}
    ERSClient.LEGACY, ERSClient.RUNELITE: RemoteInput.Setup();
  end;

  AddOnTerminate(@Self.Clear);
  AddOnTerminate(@Self.EnableRealInput);
  {$ELSE}
  WriteLn(GetDebugLn('RSClient', 'InjectInput method is not implemented without a fake input tool.', ELogLevel.WARN));
  {$ENDIF}
end;
