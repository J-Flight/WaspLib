(*
# Map Debugger
Tool to help debug {ref}`TRSMap` and {ref}`TRSMapLoader`.

Example:
```pascal
{$I WaspLib/osrs.simba}

begin
  Map.Setup([ERSChunk.VARROCK]);
  MapDebugger.Setup(@Map.Loader);
  MapDebugger.Show();
end.
```

```{figure} ../../images/map_debugger.gif
```
*)

{$DEFINE WL_MAPDEBUGGER_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TMapDebugger
Main record used to hold core values of the {ref}`Map Debugger`.
*)
  TMapDebugger = record
    ImageBox: TImageBox;
    Images: TImageArray;

    Graph: TWebGraph;

    Selected: Integer;
    Path: TGraphNodeArray;

    GetLocal: function (pt: TPoint; offset: TPoint = [0,0]): TPoint of object;
    GetGlobal: function (pt: TPoint; offset: TPoint = [0,0]): TPoint of object;

    Coordinate, SelectedInfo: TLazLabel;
    ShowGraph, ShowWalkableSpace, ShowWalkableClusters, ShowObjectClusters: Boolean;
    ColorArray: TColorArray;
  end;

procedure TMapDebugger.OnChange(sender: TLazObject);
begin
  Self.ImageBox.SetImage(Self.Images[TLazComboBox(sender).ItemIndex]);
end;

{$R-}
{$H-}
procedure TMapDebugger.ImgBoxPaint(sender: TImageBox; canvas: TImageBoxCanvas; r: TLazRect);
var
  p: TPoint;
  b: TBox;
  tpa: TPointArray;
  i: Integer;
begin
  if Self.ShowWalkableSpace then
    canvas.DrawPoints(Self.Graph.WalkableSpace, Self.ColorArray.Last);

  if Self.ShowWalkableClusters then
    for i := 0 to High(Self.Graph.WalkableClusters) do
      canvas.DrawPoints(Self.Graph.WalkableClusters[i], Self.ColorArray[i]);

  if Self.ShowObjectClusters then
    for i := 0 to High(Self.Graph.ObjectClusters) do
      canvas.DrawPoints(Self.Graph.ObjectClusters[i], Self.ColorArray[i]);

  if Self.ShowGraph then
  begin
    Self.Graph.Draw(canvas);
    if Self.Selected > -1 then
    begin
      canvas.DrawBoxFilled(TBox.Create(Self.Graph.Nodes[Self.Selected].Node, 1, 1), $00FF00);
      if Self.Path <> [] then
      begin
        for i := 0 to High(Self.Path)-1 do
          tpa += TPointArray.CreateFromLine(Self.Path[i].Node, Self.Path[i+1].Node);
        canvas.DrawPoints(tpa, $FF0000);
      end;
    end;
  end;

  with sender.MouseXY do
    p := [X div 4 * 4, Y div 4 * 4];

  tpa := TBox.Create(p.X-1, p.Y-1, p.X + 4, p.Y + 4).Corners();
  tpa := tpa.Connect().Difference(tpa);
  canvas.DrawPoints(tpa, $F7AA4E);
end;
{$H+}

procedure TMapDebugger.ImgBoxMouseMove(sender: TImageBox; shift: ELazShiftStates; x, y: Integer);
var
  pt, g: TPoint;
  i: Integer;
begin
  pt := TPoint.Create(x,y);
  g := Self.GetGlobal(pt);
  Self.Coordinate.Caption := Format('[%d, %d]', [g.X, g.Y]);

  Self.Path := [];
  if (shift = [ELazShiftStates.Shift]) and (Self.Selected > -1) then
  begin
    i := Self.Graph.Nodes.NearestIndex(pt);

    if (Self.Selected <> i) and pt.InRange(Self.Graph.Nodes[i].Node, 12) then
    try
      Self.Path := Self.Graph.PathBetween(Self.Graph.Nodes[Self.Selected].Node, Self.Graph.Nodes[i].Node, 0.33);
    except
      WriteLn GetExceptionMessage();
    end;
  end;

  sender.Repaint();
end;

procedure TMapDebugger.ImgBoxMouseDown(sender: TImageBox; button: ELazMouseButton; shift: ELazShiftStates; x, y: Integer);
var
  pt, g: TPoint;
begin
  if button <> ELazMouseButton.Left then Exit;

  pt := TPoint.Create(x, y);

  Self.Selected := Self.Graph.Nodes.NearestIndex(pt);

  if pt.InRange(Self.Graph.Nodes[Self.Selected].Node, 4) then
  begin
    g := Self.GetGlobal(Self.Graph.Nodes[Self.Selected].Node);

    Self.SelectedInfo.Caption :=
      'Selected Index: ' + ToStr(Self.Selected).PadRight(4, ' ') +
      ' Coordinate: [' +
      String(ToStr(g.X) + ',' + ToStr(g.Y) + ']').PadRight(12, ' ') +
      ' Type: ' +
      ToStr(Self.Graph.Nodes[Self.Selected].Typ).After('EGraphNode.').PadRight(11,' ') +
      ' Path Count: ' +
      ToStr(Self.Graph.Paths[Self.Selected].Length).PadRight(2, ' ') +
      ' Paths: ' + ToStr(Self.Graph.Paths[Self.Selected]);
  end
  else
  begin
    Self.Selected := -1;
    Self.SelectedInfo.Caption := 'Selected Node: None';
  end;

  Self.ImgBoxMouseMove(sender, shift, x, y);
  WriteLn('Clicked: ' + Self.Coordinate.Caption);
end;
{$R+}

procedure TMapDebugger.GraphOnChange(sender: TLazObject);
begin
  Self.ShowGraph := TLazCheckBox(sender).IsChecked();
  Self.ImageBox.Repaint();
end;

procedure TMapDebugger.WalkableSpaceOnChange(sender: TLazObject);
begin
  Self.ShowWalkableSpace := TLazCheckBox(sender).IsChecked();
  Self.ImageBox.Repaint();
end;

procedure TMapDebugger.WalkableClustersOnChange(sender: TLazObject);
begin
  Self.ShowWalkableClusters := TLazCheckBox(sender).IsChecked();
  Self.ImageBox.Repaint();
end;

procedure TMapDebugger.ObjectClustersOnChange(sender: TLazObject);
begin
  Self.ShowObjectClusters := TLazCheckBox(sender).IsChecked();
  Self.ImageBox.Repaint();
end;

(*
## MapDebugger.Show
```pascal
procedure TMapDebugger.Show();
```
Shows the `MapDebugger` form. You must first call {ref}`TMapDebugger.Setup`
before calling this.

Example:
```pascal
MapDebugger.Show();
```
*)
procedure TMapDebugger.Show();
var
  form: TLazForm;
  panel: TLazPanel;
  lbl: TLazLabel;
  combobox: TLazComboBox;
  checkbox: TLazCheckBox;
begin
  form := TLazForm.Create();
  form.Caption := 'TRSMap Debugger';
  form.Width := 1280;
  form.Height := 800;
  form.Position := ELazFormPosition.ScreenCenter;
  form.Color := $242322;
  form.BorderStyle := ELazFormBorderStyle.Sizeable;

  panel := TLazPanel.CreateEx(form);
  panel.SetBounds(0, 0, form.Width, 30);
  panel.BevelOuter := ELazPanelBevel.None;
  panel.Align := ELazAlign.Top;

  Self.SelectedInfo := TLazLabel.CreateEx(panel);
  Self.SelectedInfo.Font.Color := $FFFFFF;
  Self.SelectedInfo.Align := ELazAlign.Left;
  Self.SelectedInfo.Font.Name := 'Courier New';
  Self.SelectedInfo.BorderSpacing.Around := 5;

  lbl := TLazLabel.CreateEx(panel, 'Total nodes: ' + ToStr(Length(Self.Graph.Nodes)) + ' ');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.Font.Name := 'Courier New';
  lbl.BorderSpacing.Around := 5;

  panel := TLazPanel.CreateEx(form);
  panel.SetBounds(0, 0, form.Width, 30);
  panel.BevelOuter := ELazPanelBevel.None;
  panel.Align := ELazAlign.Top;

  lbl := TLazLabel.CreateEx(panel, 'Show ObjectClusters');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.BorderSpacing.Around := 5;

  checkbox := TLazCheckBox.CreateEx(panel);
  checkbox.Align := ELazAlign.Left;
  checkbox.BorderSpacing.Left  := 10;
  checkbox.BorderSpacing.Right := 4;
  checkbox.OnChange := @Self.ObjectClustersOnChange;
  lbl.AnchorSideTop.Control := checkbox;
  lbl.OnClick := @FormUtils.OnCheckboxCaptionClick;

  lbl := TLazLabel.CreateEx(panel, 'Show WalkableClusters');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.BorderSpacing.Around := 5;

  checkbox := TLazCheckBox.CreateEx(panel);
  checkbox.Align := ELazAlign.Left;
  checkbox.BorderSpacing.Left  := 10;
  checkbox.BorderSpacing.Right := 4;
  checkbox.OnChange := @Self.WalkableClustersOnChange;
  lbl.AnchorSideTop.Control := checkbox;
  lbl.OnClick := @FormUtils.OnCheckboxCaptionClick;


  lbl := TLazLabel.CreateEx(panel, 'Show WalkableSpace');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.BorderSpacing.Around := 5;

  checkbox := TLazCheckBox.CreateEx(panel);
  checkbox.Align := ELazAlign.Left;
  checkbox.BorderSpacing.Left  := 10;
  checkbox.BorderSpacing.Right := 4;
  checkbox.OnChange := @Self.WalkableSpaceOnChange;
  lbl.AnchorSideTop.Control := checkbox;
  lbl.OnClick := @FormUtils.OnCheckboxCaptionClick;

  lbl := TLazLabel.CreateEx(panel, 'Show webgraph');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.BorderSpacing.Around := 5;

  checkbox := TLazCheckBox.CreateEx(panel);
  checkbox.Align := ELazAlign.Left;
  checkbox.State := ELazCheckBoxState.Checked;
  checkbox.BorderSpacing.Left  := 10;
  checkbox.BorderSpacing.Right := 4;
  checkbox.OnChange := @Self.GraphOnChange;
  lbl.AnchorSideTop.Control := checkbox;
  lbl.OnClick := @FormUtils.OnCheckboxCaptionClick;


  combobox := TLazComboBox.CreateEx(panel);
  combobox.Items.Add('Map');
  combobox.Items.Add('Height');
  combobox.Items.Add('Collision');
  combobox.ItemIndex := 0;
  combobox.Align := ELazAlign.Left;
  combobox.BorderSpacing.Left  := 10;
  combobox.BorderSpacing.Right := 10;
  combobox.OnChange := @Self.OnChange;

  lbl := TLazLabel.CreateEx(panel, 'Map:');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Left;
  lbl.BorderSpacing.Around := 5;

  lbl := TLazLabel.CreateEx(panel, 'Coordinate: ');
  lbl.Font.Color := $FFFFFF;
  lbl.Align := ELazAlign.Right;
  lbl.BorderSpacing.Around := 5;

  Self.Coordinate := TLazLabel.CreateEx(panel);
  Self.Coordinate.Align := ELazAlign.Right;
  Self.Coordinate.BorderSpacing.Around := 5;
  Self.Coordinate.Font.Color := $FFFFFF;

  Self.ImageBox := TImageBox.CreateEx(form);
  Self.ImageBox.Align := ELazAlign.Client;
  Self.ImageBox.SetImage(Self.Images[0]);
  Self.ImageBox.OnImgPaint := @Self.ImgBoxPaint;
  Self.ImageBox.OnImgMouseMove := @Self.ImgBoxMouseMove; 
  Self.ImageBox.OnImgMouseDown := @Self.ImgBoxMouseDown;

  Form.ShowModal();
end;

(*
## MapDebugger.Setup
```pascal
procedure TMapDebugger.Setup(loader: ^TRSMapLoader);
```
Setup method for the `MapDebugger` form.
A pointer to a {ref}`TRSMapLoader` must be passed into this as `loader`.

Example:
```pascal
MapDebugger.Setup(@Map.Loader);
```
*)
procedure TMapDebugger.Setup(loader: ^TRSMapLoader);
var
  map, heightmap, collision: TImage;
  p: TPoint;
  x, y, i: Integer;
  node: TGraphNode;
begin
  map := loader^.Map.Copy();
  heightmap := loader^.Heightmap.Copy();
  collision := loader^.Collision.Copy();

  for p in collision.FindColor($FFFFFF, 0) do
  begin
    x := (p.X div 4) mod 2;
    y := (p.Y div 4) mod 2;
    if x = y then Continue;
    collision.Pixel[p.X, p.Y] := $F5F5F5;
  end;

  Self.Images := [map, heightmap, collision];

  Self.GetGlobal := @loader^.GetGlobal;
  Self.GetLocal  := @loader^.GetLocal;

  Self.Graph.Nodes := [];
  for node in loader^.Graph.Nodes do
    Self.Graph.Nodes += TGraphNode.Create(Self.GetLocal(node.Node), node.Typ);

  Self.Graph.Paths := Copy(loader^.Graph.Paths);

  Self.Graph.WalkableSpace := [];
  for p in loader^.Graph.WalkableSpace do
    Self.Graph.WalkableSpace += Self.GetLocal(p);

  Self.Graph.WalkableClusters := [];
  SetLength(Self.Graph.WalkableClusters, loader^.Graph.WalkableClusters.Length);
  for i := 0 to High(loader^.Graph.WalkableClusters) do
    for p in loader^.Graph.WalkableClusters[i] do
      Self.Graph.WalkableClusters[i] += Self.GetLocal(p);

  Self.Graph.ObjectClusters := [];
  SetLength(Self.Graph.ObjectClusters, loader^.Graph.ObjectClusters.Length);
  for i := 0 to High(loader^.Graph.ObjectClusters) do
    for p in loader^.Graph.ObjectClusters[i] do
      Self.Graph.ObjectClusters[i] += Self.GetLocal(p);

  for i := 0 to Max(Length(loader^.Graph.WalkableClusters), Length(loader^.Graph.ObjectClusters)) do
    Self.ColorArray += TColor.Random();

  Self.Graph.Setup();
  Self.ShowGraph := True;

  Self.Show();
end;

var
(*
## MapDebugger variable
Global {ref}`TMapDebugger` variable.
*)
  MapDebugger: TMapDebugger;

procedure TRSMap.Debug();
begin
  MapDebugger.Setup(@Self.Loader);
end;
