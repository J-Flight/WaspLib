(*
# House Form
This file is about {ref}`TRSHouse` dedicated `TLazForm` utilities.

```{figure} ../../images/house_form.png
```
*)

{$DEFINE WL_HOUSE_FORM_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
  THouseFormHelper = record
    MatrixIdx, SelectedIdx: TPoint;
    Grid: TBoxArray;
    GridTPAs: T2DPointArray;

    RoomCombobox: TLazComboBox;
    ImageBox: TImageBox;
  end;


procedure THouseFormHelper.DrawGrid(img: TImage);
var
  i: Integer;
  selected: Boolean;
begin
  img.ReplaceColor($0, $1A1A1A);
  img.DrawAlpha := $7D;

  for i := 0 to High(Self.Grid) do
  begin
    selected := Self.Grid[i].Contains(Self.SelectedIdx);
    if selected then
    begin
      img.DrawColor := $00FFFF;
      img.DrawBoxFilled(Self.Grid[i]);
      Continue;
    end;
    img.DrawColor := $FFFFFF;
    img.DrawBox(Self.Grid[i]);
  end;
end;

procedure THouseFormHelper.DrawIcons(img: TImage);
var
  pt, idx: TPoint;
  room: THouseRoom;
  icon: TImage;
begin
  for idx.Y := Max(House.Loader.HouseBounds.Y1, 0) to Min(House.Loader.HouseBounds.Y2, House.Loader.AMOUNT-1) do
    for idx.X := Max(House.Loader.HouseBounds.X1, 0) to Min(House.Loader.HouseBounds.X2, House.Loader.AMOUNT-1) do
    begin
      room := House.Loader.ReadRoom(idx);
      if room.Room = EHouseRoom.UNKNOWN then Continue;

      pt := [idx.X * House.Loader.SIZE, idx.Y * House.Loader.SIZE];
      icon := House.Loader.GetIconImage(room.Room);
      icon := icon.RotateClockWise(room.Rotation);
      img.DrawImage(icon, pt);
    end;
end;


procedure THouseFormHelper.OnTeleportChange(sender: TObject);
begin
  House.TeleportMode := EHouseTeleportMode(TLazCombobox(sender).ItemIndex);
  House.Loader.Config.Item['TeleportMode'].AsInt := Ord(House.TeleportMode);
  House.Loader.Config.SaveConfig();
end;

procedure THouseFormHelper.LocationOnChange(sender: TObject);
begin
  House.Loader.Location := EHouseLocation(TLazComboBox(sender).ItemIndex);
  House.Loader.Config.Item['Location'].AsInt := Ord(House.Loader.Location);
  House.Loader.Config.SaveConfig();
end;

procedure THouseFormHelper.DecorationOnChange(sender: TObject);
begin
  House.Loader.Decoration := EHouseDecoration(TLazComboBox(sender).ItemIndex);
  House.Loader.Config.Item['Decoration'].AsInt := Ord(House.Loader.Decoration);
  House.Loader.Config.SaveConfig();
end;

procedure THouseFormHelper.RoomOnChange(sender: TObject);
var
  old, new: THouseRoom;
  room: EHouseRoom;
begin
  room := EHouseRoom(TLazComboBox(sender).ItemIndex);
  old := House.Loader.ReadRoom(Self.MatrixIdx);

  if old.Room = room then new := old
  else new := [room, 0, []];
  House.Loader.DrawMap(new, Self.MatrixIdx);

  Self.Redraw();
  //Self.RefreshObjectsPanel(parent.GetChild('objects_panel'), new);
end;

procedure THouseFormHelper.Redraw();
var
  img: TImage;
begin
  img := House.Loader.Map.Copy();
  Self.DrawGrid(img);
  Self.DrawIcons(img);
  Self.ImageBox.SetImage(img);
  Self.ImageBox.Repaint();
end;

{$H-}
procedure THouseFormHelper.OnRotateLeft(sender: TObject);
var
  current: THouseRoom;
begin
  current := House.Loader.ReadRoom(Self.MatrixIdx);
  current.Rotation := (current.Rotation - 1) and 3;
  House.Loader.DrawMap(current, Self.MatrixIdx);
  Self.Redraw();
end;

procedure THouseFormHelper.OnRotateRight(sender: TObject);
var
  current: THouseRoom;
begin
  current := House.Loader.ReadRoom(Self.MatrixIdx);
  current.Rotation := (current.Rotation + 1) and 3;
  House.Loader.DrawMap(current, Self.MatrixIdx);
  Self.Redraw();
end;

procedure THouseFormHelper.OnReadClick(sender: TObject);
begin
  HouseViewer.LoadHouse();
  Self.Redraw();
end;
{$H+}

procedure THouseFormHelper.OnImgPaint({$H-}sender: TImageBox;{$H+} canvas: TImageBoxCanvas; r: TLazRect);
var
  limit, b: TBox;
  pt: TPoint;
begin
  limit := [r.Left, r.Top, r.Right, r.Bottom];
  pt := [Self.MatrixIdx.X*House.Loader.SIZE, Self.MatrixIdx.Y*House.Loader.SIZE];
  b := Self.Grid[Self.MatrixIdx.Y * House.Loader.AMOUNT + Self.MatrixIdx.X];
  if not limit.ContainsBox(b) and not limit.Overlap(b) then Exit;
  canvas.DrawBox(b, $00FFFF);
end;

procedure THouseFormHelper.OnImgMouseDown(sender: TImageBox; btn: ELazMouseButton; shift: ELazShiftStates; x,y: Integer);
var
  pt: TPoint;
  img: TImage;
begin
  if btn <> ELazMouseButton.Left then Exit;
  if shift <> [ELazShiftStates.Left] then Exit;

  pt := [x div House.Loader.SIZE, y div House.Loader.SIZE];
  if pt = Self.MatrixIdx then Exit;

  Self.SelectedIdx := [x,y];
  Self.MatrixIdx := pt;

  Self.RoomCombobox.ItemIndex := Ord(House.Loader.ReadRoom(Self.MatrixIdx).Room);

  img := House.Loader.Map.Copy();
  Self.DrawGrid(img);
  Self.DrawIcons(img);
  sender.SetImage(img);
  sender.Repaint();

  //Self.RefreshObjectsPanel(parent.GetChild('objects_panel'), House.Loader.ReadRoom(House.Loader.Selected.Matrix));
end;





function THouseFormHelper.CreateTeleportCombobox(owner: Pointer): TLazComboBox;
var
  i: Integer;
begin
  Result := TLazComboBox.CreateEx(owner, '', 'Choose your method of house teleportation.', 170, 40);
  Result.Items.AddStrings(['Spell', 'Tablet', 'Contruction/Max cape']);

  House.Loader.SetupConfig();
  i := House.Loader.Config.Item['TeleportMode'].AsInt;

  House.TeleportMode := EHouseTeleportMode(i);
  Result.ItemIndex := i;
  Result.OnChange := @Self.OnTeleportChange;
end;

var
  HouseForm: THouseFormHelper;


function TScriptForm.CreateHouseTab(): TLazTabSheet;
var
  panel: TLazPanel;
  location, decoration: TLazComboBox;
  rotateLeft, rotateRight, loader: TLazButton;
  lbl: TLazLabel;
  b: TBox;
  img: TImage;
begin
  HouseForm.Grid := TBoxArray.Create([0,0], House.Loader.AMOUNT, House.Loader.AMOUNT, House.Loader.SIZE-1, House.Loader.SIZE-1, [1,1]);
  for b in HouseForm.Grid do
    HouseForm.GridTPAs += b.Corners().Connect();

  Result := Self.CreateTab('House Settings');

  panel := TLazPanel.CreateEx(Result);
  panel.Align := ELazAlign.Left;

  HouseForm.ImageBox := TImageBox.CreateEx(Result);
  HouseForm.ImageBox.Align := ELazAlign.Client;
  HouseForm.ImageBox.OnImgPaint := @HouseForm.OnImgPaint;
  HouseForm.ImageBox.OnImgMouseDown := @HouseForm.OnImgMouseDown;


  lbl := TLazLabel.CreateEx(panel, 'Location:', 'Change your house location.');
  lbl.Align := ELazAlign.Top;
  lbl.BorderSpacing.Top := 20;

  location := TLazComboBox.CreateEx(panel, '', 'Change your house location.');
  location.Items.AddStrings([
    'Rimmington', 'Taverley', 'Pollnivneach', 'Hosidius', 'Rellekka', 'Aldarin',
    'Brimhaven', 'Yanille', 'Prifddinas'
  ]);
  location.Width := 165;
  location.AnchorLabel(lbl);
  location.OnChange := @HouseForm.LocationOnChange;

  lbl := TLazLabel.CreateEx(panel, 'Decoration:', 'Change your house decoration.');
  lbl.AnchorSideTop.Control := location;
  lbl.AnchorSideTop.Side := ELazAnchorSideReference.asrBottom;
  lbl.BorderSpacing.Top := 20;

  decoration := TLazComboBox.CreateEx(panel, '', 'Change your house decoration.');
  decoration.Items.AddStrings([
    'Basic wood', 'Basic stone', 'Whitewashed stone', 'Fremennik-style wood',
    'Tropical wood', 'Fancy stone', 'Deathly mansion', 'Twisted theme',
    'Hosidius house', 'Cosy cabin'
  ]);
  decoration.Width := 165;
  decoration.AnchorLabel(lbl);
  decoration.OnChange := @HouseForm.DecorationOnChange;

  lbl := TLazLabel.CreateEx(panel, 'Room:', 'Change the selected room.');
  lbl.AnchorSideTop.Control := decoration;
  lbl.AnchorSideTop.Side := ELazAnchorSideReference.asrBottom;
  lbl.BorderSpacing.Top := 20;

  HouseForm.RoomCombobox := TLazComboBox.CreateEx(panel, '', 'Change the selected room.');
  HouseForm.RoomCombobox.Items.AddStrings([
    'None', 'Parlour', 'Garden', 'Kitchen', 'Dining room', 'Workshop', 'Bedroom',
    'Skill hall', 'League hall', 'Games room', 'Combat room', 'Quest hall',
    'Menagerie (outdoors)', 'Menagerie (indoors)', 'Study', 'Costume room',
    'Chapel', 'Portal chamber', 'Formal garden', 'Throne room',
    'Superior garden', 'Portal nexus', 'Achievement gallery'
  ]);
  HouseForm.RoomCombobox.Width := 165;
  HouseForm.RoomCombobox.AnchorLabel(lbl);
  HouseForm.RoomCombobox.OnChange := @HouseForm.RoomOnChange;

  rotateLeft := TLazButton.CreateEx(panel, 'Rotate Left', 'Rotate the current room counter clockwise.');
  rotateLeft.Width := 80;
  rotateLeft.AnchorSideTop.Control := HouseForm.RoomCombobox;
  rotateLeft.AnchorSideTop.Side := ELazAnchorSideReference.asrBottom;
  rotateLeft.BorderSpacing.Top := 20;
  rotateLeft.OnClick := @HouseForm.OnRotateLeft;

  rotateRight := TLazButton.CreateEx(panel, 'Rotate Right', 'Rotate the current room clockwise');
  rotateRight.Width := 80;
  rotateRight.AnchorSideLeft.Control := rotateLeft;
  rotateRight.AnchorSideLeft.Side := ELazAnchorSideReference.asrBottom;
  rotateRight.AnchorSideTop.Control := rotateLeft;
  rotateRight.AnchorSideTop.Side := ELazAnchorSideReference.asrTop;
  rotateRight.BorderSpacing.Left := 6;
  rotateRight.OnClick := @HouseForm.OnRotateRight;

  loader := TLazButton.CreateEx(panel, 'Read Layout', 'Reads the layout of the house automatically.');
  loader.Width := 165;
  loader.AnchorSideTop.Control := rotateLeft;
  loader.AnchorSideTop.Side := ELazAnchorSideReference.asrBottom;
  loader.BorderSpacing.Top := 20;

  loader.AnchorSideLeft.Control := HouseForm.RoomCombobox;
  loader.AnchorSideLeft.Side := ELazAnchorSideReference.asrCenter;
  loader.OnClick := @HouseForm.OnReadClick;

  panel := TLazPanel.CreateEx(panel);
  panel.Name := 'house_objects_panel';
  panel.Height := 210;
  panel.AnchorSideTop.Control := loader;
  panel.AnchorSideTop.Side := ELazAnchorSideReference.asrBottom;
  panel.BorderSpacing.Top := 20;

  if House.Loader.Map = nil then
    House.Loader.Init();

  location.ItemIndex   := Ord(House.Loader.Location);
  decoration.ItemIndex := Ord(House.Loader.Decoration);

  HouseForm.SelectedIdx := [House.Loader.AMOUNT div 2 * House.Loader.SIZE, House.Loader.AMOUNT div 2 * House.Loader.SIZE];
  HouseForm.MatrixIdx   := [House.Loader.AMOUNT div 2, House.Loader.AMOUNT div 2];

  img := House.Loader.Map.Copy();
  HouseForm.DrawGrid(img);
  HouseForm.DrawIcons(img);
  HouseForm.ImageBox.SetImage(img);

  HouseForm.RoomCombobox.ItemIndex := Ord(House.Loader.ReadRoom(HouseForm.MatrixIdx).Room);
end;
