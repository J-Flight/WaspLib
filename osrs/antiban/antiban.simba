(*
# Antiban
Methods to handle antiban.
*)

{$DEFINE WL_ANTIBAN_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osrs.simba}
{$ENDIF}

type
  PAntibanTask = ^TAntibanTask;
  PBreakTask = ^TBreakTask;
  PSleepTask = ^TSleepTask;
  PAntibanTaskArray = array of PAntibanTask;
  PAntibanBreakArray = array of PBreakTask;

  TAntibanMethod = procedure of object;

  TAntibanTask = record
    Method: TAntibanMethod;
    Interval: Double;
    StdVar: Double;
    Countdown: TCountDown;
  end;

  TBreakTask = record
    Interval: Double;
    Length: Double;
    LogoutChance: Double;
    StdVar: Double;
    Next, Previous: Double;
  end;

  TSleepTask = record
    Time: String;
    Length: Double;
    Randomness: String;
    LogoutChance: Double;
    StdVar: Double;
    Next: Double;
  end;

(*
## TAntiban
Main record to handle the built-in WaspLib antiban.
*)
  TAntiban = record
    DoingAntiban: Boolean;
    Debugging: Boolean;
    DebugRandoms: Boolean;

    Skills: array of ERSSkill;

    Zoom: record
      Min, Max: Integer;
    end;

    Tasks:  array of TAntibanTask;
    Breaks: array of TBreakTask;
    Sleeps: array of TSleepTask;

    OnStartTask,  OnFinishTask:  procedure(task: PAntibanTask) of object;
    OnStartBreak, OnFinishBreak: procedure(task: PBreakTask)   of object;
    OnStartSleep, OnFinishSleep: procedure(task: PSleepTask)   of object;

    OnBreaking: procedure(task: PBreakTask; var countdown: TCountdown) of object;
    OnSleeping: procedure(task: PSleepTask; var countdown: TCountdown) of object;
    TimeRunning: TStopwatch;
  end;

function TAntiban.TimeRunningAtClock(time: String; randomness: Double = 0.10): Int64;
var
  current, res: TDateTime;
begin
  current := TDateTime.CreateFromSystem();
  res := TDateTime.CreateFromISO(current.Date.ToString('YYYY-MM-DD') + 'T' + time);

  res -= current;
  if res < 0 then res += 1;

  Result := GetTimeRunning() + UInt64(Trunc(res * ONE_DAY));
  Result += Trunc(Abs(GaussRand(0, randomness * ONE_DAY / 2)));
end;


function TAntiban.GetActiveTasks(): PAntibanTaskArray;
var
  i: Integer;
begin
  for i := 0 to High(Self.Tasks) do
    if not Self.Tasks[i].Countdown.IsPaused then
      Result += @Self.Tasks[i];
end;



procedure TAntiban._UpdateTask(idx: Integer; method: TAntibanMethod; interval, randomness: Double);
begin
  Self.Tasks[idx].Interval := interval;
  Self.Tasks[idx].Method := @method;
  Self.Tasks[idx].StdVar := randomness;
  Self.Tasks[idx].Countdown.Start(Random(Trunc(Abs(GaussRand(Self.Tasks[idx].Interval, Self.Tasks[idx].Interval * Self.Tasks[idx].StdVar)))));
end;

procedure TAntiban._UpdateBreak(idx: Integer; interval, length, randomness, logoutChance: Double);
begin
  Self.Breaks[idx].Interval := interval;
  Self.Breaks[idx].Length := length;
  Self.Breaks[idx].StdVar := randomness;
  Self.Breaks[idx].LogoutChance := logoutChance;
  Self.Breaks[idx].Next := GetTimeRunning() + Abs(GaussRand(Self.Breaks[idx].Interval, Self.Breaks[idx].Interval * Self.Breaks[idx].StdVar));
end;

procedure TAntiban._UpdateSleep(idx: Integer; time: String; length, randomness, logoutChance: Double);
begin
  Self.Sleeps[idx].Time := time;
  Self.Sleeps[idx].Length := length;
  Self.Sleeps[idx].StdVar := randomness;
  Self.Sleeps[idx].LogoutChance := logoutChance;
  Self.Sleeps[idx].Next := Self.TimeRunningAtClock(time, randomness);
end;


(*
## Antiban.AddTask
```pascal
procedure TAntiban.AddTask(method: TAntibanMethod; interval: Double; randomness: Double = 0.2);
```
Schedule a antiban task.
An antiban task can be any procedure but they should be short actions that won't
break your main script.

`method` is a pointer to the task you want to perform. WaspLib includes a couple of them but you may make your own if you want.

`interval` is the aproximate interval of time that has to pass for the antiban
task to occur and it will be repeated everytime that the interval has passed
and the antiban is checked with {ref}`Antiban.DoAntiban`.

This task will only occur when **TAntiban.DoAntiban** is called.

Example:
```pascal
Antiban.AddTask(@Antiban.HoverSkills, 15 * ONE_MINUTE); //Every 15 minutes the script will run Antiban.HoverSkills().
```
*)
procedure TAntiban.AddTask(method: TAntibanMethod; interval: Double; randomness: Double = 0.2);
var
  len: Integer;
begin
  len := Length(Self.Tasks);
  SetLength(Self.Tasks, len+1);
  Self._UpdateTask(len, @method, interval, randomness);
end;

(*
## Antiban.AddBreak
```pascal
procedure TAntiban.AddBreak(interval, length: Double; randomness: Double = 0.2; logoutChance: Double = 0.33);
```
Schedule a break. Breaks can be of short or medium length and should be shorter than any sleep breaks.

`interval` is the aproximate interval of time that has to pass for the break to
occur and it will be repeated everytime that interval passes and the antiban is
checked either with {ref}`Antiban.DoBreak` or {ref}`Antiban.DoAntiban`.

`length`, `randomness` and `logoutChance` are the same as {ref}`Antiban.AddSleep`.

This break will only occur when {ref}`Antiban.DoAntiban` is called.

Example:
```pascal
Antiban.AddBreak(30 * ONE_MINUTE, 5 * ONE_MINUTE); //Every 30 minutes the script will take a 5 minute break, subject to variance from the randomness variable.
```
*)
procedure TAntiban.AddBreak(length, interval: Double; randomness: Double = 0.2; logoutChance: Double = 0.33);
var
  len: Integer;
begin
  len := System.Length(Self.Breaks);
  SetLength(Self.Breaks, len+1);
  Self._UpdateBreak(len, interval, length, randomness, logoutChance);
end;

(*
## Antiban.AddSleep
```pascal
procedure TAntiban.AddSleep(time: String; length: Double; randomness: Double = 0.1; logoutChance: Double = 0.5);
```
Schedule a sleep break. A sleep break is a large break, it can be any length but
it's usually the several hours and also the largest one/ones.

`time` is the aproximate time you want the break to occur and should be written
in a "bare" time format (00:00:00).

`length` is how long we will sleep for in milliseconds.

`randomness` is self explanatory, gives variance to the time our script will
sleep at and it's length too.
One thing to keep in mind is that randomness only affects time past `time`.
In other words, you will never sleep before `time`.

`logoutChance` is the probability of logging out for the sleep break or to
simply afk and logout from inactivity.

This sleep break will only occur when {ref}`Antiban.DoAntiban` is called and our sleep break is due.

Example:
```pascal
Antiban.AddSleep('01:30:45', 8 * ONE_HOUR, 0.1, 0.8); //At 01:30:45 on our computer time the script will take a break for 8 hours, subject to variance from the randomness variable.
```
*)
procedure TAntiban.AddSleep(time: String; length: Double; randomness: Double = 0.1; logoutChance: Double = 0.5);
var
  len: Integer;
begin
  len := System.Length(Self.Sleeps);
  SetLength(Self.Sleeps, len+1);
  Self._UpdateSleep(len, time, length, randomness, logoutChance);
end;


(*
## Antiban.TakeBreak
```pascal
procedure TAntiban.TakeBreak(var task: TBreakTask);
```
Internal function used by {ref}`Antiban.DoAntiban` and is responsible for
performing the specified break `task`.
In other words, this is what makes the script take the break.
*)
procedure TAntiban.TakeBreak(var task: TBreakTask);
var
  countdown: TCountDown;
  i: Integer;
  activeTasks: PAntibanTaskArray;
begin
  activeTasks := Self.GetActiveTasks();
  for i := 0 to High(activeTasks) do
    activeTasks[i]^.countdown.Pause();

  countdown.Start(Abs(Round(GaussRand(task.Length, task.Length * task.StdVar))));

  WriteLn GetDebugLn('Antiban', 'Taking a break for ' + FormatMilliseconds(countdown.Remaining, MINUTES_FORMAL));

  if (@Self.OnStartBreak <> nil) then
    Self.OnStartBreak(@task);

  if RandomBoolean(task.LogoutChance) then
  begin
    WriteLn GetDebugLn('Antiban', 'Logging out.');
    Logout.Logout();
  end;

  if RandomBoolean(0.50) then
    RSClient.LoseFocus();

  repeat
    if (Inc(i) mod 12 = 0) then
      WriteLn GetDebugLn('Antiban', 'Break time remaining: ' + FormatMilliseconds(countdown.Remaining, MINUTES_FORMAL));

    if (@Self.OnBreaking <> nil) then //callback runs every 5 seconds
      Self.OnBreaking(@task, countdown);

    Sleep(Round(Min(countdown.Remaining, 5 * ONE_SECOND)));
  until countdown.IsFinished;

  WriteLn GetDebugLn('Antiban', 'Break finished');

  if (@Self.OnFinishBreak <> nil) then
    Self.OnFinishBreak(@task);

  // Offset all tasks & breaks
  for i := 0 to High(Self.Breaks) do
    Self.Breaks[i].Next += Time() - (countdown.FFinishTime - countdown.FLength );

  for I := 0 to High(activeTasks) do
    activeTasks[i]^.countdown.Resume();

  // Setup next time this break happens
  task.Next := GetTimeRunning() + Abs(GaussRand(task.Interval, task.Interval * task.StdVar));
end;

(*
## Antiban.TakeSleep
```pascal
procedure TAntiban.TakeSleep(var task: TSleepTask);
```
Internal function used by {ref}`Antiban.DoAntiban` and is responsible for
performing the specified sleep `task`.
In other words, this is what makes the script take the sleep break.
*)
procedure TAntiban.TakeSleep(var task: TSleepTask);
var
  countdown: TCountDown;
  i: Integer;
  activeTasks: PAntibanTaskArray;
  marker: UInt64;
begin
  activeTasks := Self.GetActiveTasks();
  for i := 0 to High(activeTasks) do
    activeTasks[i]^.countdown.Pause();

  countdown.Start(Abs(Round(GaussRand(task.Length, task.Length * task.StdVar))));
  WriteLn GetDebugLn('Antiban', 'Sleeping for ' + FormatMilliseconds(countdown.Remaining, TIME_FORMAL));

  if @Self.OnStartSleep <> nil then Self.OnStartSleep(@task);

  if RandomBoolean(task.LogoutChance) then
  begin
    WriteLn GetDebugLn('Antiban', 'Logging out.');
    Logout.Logout();
  end;

  if RandomBoolean(0.7) then RSClient.LoseFocus();

  repeat
    if Time() > marker  then
    begin
      WriteLn GetDebugLn('Antiban', 'Sleep time remaining: ' + FormatMilliseconds(countdown.Remaining, TIME_FORMAL));
      //OnSleeping callback only runs every 5 mins.
      if (@Self.OnSleeping <> nil) then
        Self.OnSleeping(@task, countdown);
      marker := Time() + 5 * ONE_MINUTE;
    end;

    Sleep(Round(Min(countdown.Remaining, 5 * ONE_SECOND)));
  until countdown.IsFinished;

  WriteLn GetDebugLn('Antiban', 'Sleep finished.');

  if (@Self.OnFinishSleep <> nil) then
    Self.OnFinishSleep(@task);

  //Offset all tasks & breaks
  for i := 0 to High(Self.Breaks) do
    Self.Breaks[i].Next += Time() - (countdown.FFinishTime - countdown.FLength);

  for i := 0 to High(activeTasks) do
    activeTasks[i]^.countdown.Resume();

  // Schedule next time this sleep will happen
  task.Next := Self.TimeRunningAtClock(task.Time, task.StdVar);
end;


(*
## Antiban.DoTask
```pascal
function TAntiban.DoTask(): Boolean;
```
Checks for scheduled antiban tasks, if any is due it will do it.

You should only call this when taking doing an antiban task won't break your script.

Returns true if a task was performed.

Example:
```pascal
Antiban.DoTask();
```
*)
function TAntiban.DoTask(): Boolean;
var
  i: Int32;
  task: PAntibanTask;
  activeTasks: PAntibanTaskArray;
begin
  activeTasks := Self.GetActiveTasks();
  for i := 0 to High(activeTasks) do
    activeTasks[i]^.Countdown.Pause();

  for i := 0 to High(activeTasks) do
  begin
    task := activeTasks[i];

    if task^.Countdown.Remaining = 0 then
    begin
      if (@Self.OnStartTask <> nil) then
        Self.OnStartTask(Task);

      task^.Method();
      task^.Countdown.Start(Trunc(Abs(GaussRand(task^.Interval, task^.Interval * task^.StdVar))));
      task^.Countdown.Pause();

      if (@Self.OnFinishTask <> nil) then
        Self.OnFinishTask(Task);

      Result := True;
    end;
  end;

  for i := 0 to High(activeTasks) do
    activeTasks[i]^.Countdown.Resume();
end;

(*
## Antiban.DoBreak
```pascal
function TAntiban.DoBreak(): Boolean;
```
Checks for scheduled breaks, if any is due it will take it.

You should only call this when taking a break won't break your script.

Returns true if a break was taken.

Example:
```pascal
Antiban.DoBreak();
```
*)
function TAntiban.DoBreak(): Boolean;
var
  i: Integer;
  t: UInt64;
begin
  t := GetTimeRunning();
  for i := 0 to High(Self.Breaks) do
    if t > Self.Breaks[i].Next then
    begin
      Self.TakeBreak(Self.Breaks[i]);
      Result := True;
      // Restart loop
      i := -1;
      t := GetTimeRunning();
    end;
end;

(*
## Antiban.DoSleep
```pascal
function TAntiban.DoSleep(): Boolean;
```
Checks for scheduled sleep breaks, if any is due it will take it.

You should only call this when taking a sleep break won't break your script.

Returns true if a sleep break was taken.

Example:
```pascal
Antiban.DoSleep();
```
*)
function TAntiban.DoSleep(): Boolean;
var
  i: Integer;
  t: UInt64;
begin
  t := GetTimeRunning();
  for i := 0 to High(Self.Sleeps) do
    if t > Self.Sleeps[i].Next then
    begin
      Self.TakeSleep(Self.Sleeps[i]);
      Result := True;
      // Restart loop
      i := -1;
      t := GetTimeRunning();
    end;
end;


(*
## Antiban.DoAntiban
```pascal
function TAntiban.DoAntiban(checkTasks, checkBreaks, checkSleeps: Boolean = True): Boolean;
```
This should be called in your script when antiban sleeps, breaks or tasks won't
break your script.

When this is called, the setup sleep breaks, breaks and tasks will be checked,
if enough time has passed to perform any of them
(subject to the parameters you pass in too), they will be performed, otherwise,
nothing will happen.

Example:
```pascal
Antiban.AddTask(15 * ONE_MINUTE, @Antiban.HoverSkills);

while True do        //Infinite loop
  Antiban.DoAntiban(); //Antiban.HoverSkills will be called every time 15 minutes passed when this is called.
```
*)
function TAntiban.DoAntiban(checkTasks, checkBreaks, checkSleeps: Boolean = True): Boolean;
begin
  if Self.DoingAntiban then
    Exit;

  Self.DoingAntiban := True;

  WaspClient.Timer.Pause();
  WaspClient.TimeRunning.Pause();
  Logger.TimeRunning.Pause();
  Activity.Pause();
  Self.TimeRunning.Resume();

  if checkTasks  and Self.DoTask()  then Result := True;
  if checkBreaks and Self.DoBreak() then Result := True;
  if checkSleeps and Self.DoSleep() then Result := True;

  if not RSClient.IsLoggedIn() then
    Login.DoLogin();

  Self.DoingAntiban := False;

  WaspClient.Timer.Resume();
  WaspClient.TimeRunning.Resume();
  Logger.TimeRunning.Resume();
  Activity.Resume();
  Self.TimeRunning.Pause();
end;


(*
## Antiban.TimeUntilBreak
```pascal
function TAntiban.TimeUntilBreak(constref task: TBreakTask; fmt: String = TIME_FORMAL): String;
```
Check how much time is left until the specified break `task` should be taken.
*)
function TAntiban.TimeUntilBreak(constref task: TBreakTask; fmt: String = TIME_FORMAL): String;
begin
  Result := FormatMilliseconds(Max(0, Round(task.Next - GetTimeRunning())), fmt);
end;

(*
## Antiban.TimeUntilSleep
```pascal
function TAntiban.TimeUntilSleep(constref task: TSleepTask; fmt: String = TIME_FORMAL): String;
```
Check how much time is left until the specified sleep break `task` should be taken.
*)
function TAntiban.TimeUntilSleep(constref task: TSleepTask; fmt: String = TIME_FORMAL): String;
begin
  Result := FormatMilliseconds(Max(0, Round(task.Next - GetTimeRunning())), fmt);
end;

(*
## Antiban.SimulateBreaks
```pascal
procedure TAntiban.SimulateBreaks(bottingDays: UInt64 = 1000);
```
Performs a simulation of `bottingDays` amount of days with the currently setup
breaks and prints the results.

Example
```pascal
Antiban.AddBreak(30 * ONE_MINUTE, 5 * ONE_MINUTE);
Antiban.SimulateBreaks();
```
*)
procedure TAntiban.SimulateBreaks(bottingDays: UInt64 = 1000);
var
  active, inactive, len, prev: Double;
  i: Int32;
  task: PBreakTask;
begin
  while (active + inactive < bottingDays * ONE_DAY) do
  begin
    task := @Self.Breaks[0];
    for I := 0 to High(Self.Breaks) do
      if Self.Breaks[i].Next < task^.Next then
        task := @Self.Breaks[i];

    len := Abs(GaussRand(task^.Length, task^.Length * task^.StdVar));
    active += task^.Next - Prev;
    inactive += len;

    for i := 0 to High(Self.Breaks) do
      Self.Breaks[i].Next += len;

    prev := task^.Next;
    task^.Next += Abs(GaussRand(task^.Interval, task^.Interval * task^.StdVar));
  end;

  inactive /= bottingDays;
  active /= bottingDays;
  WriteLn('Running for ', FormatMilliseconds(Round(active), TIME_FORMAL), ' per day, on average.');
end;


procedure TAntiban.WalkingTasks();
begin
  //overriden later.
end;

var
(*
## Antiban variable
Global {ref}`TAntiban` variable.
*)
  Antiban: TAntiban;
