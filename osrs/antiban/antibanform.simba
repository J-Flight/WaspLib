(*
# Antiban Form
This page is about {ref}`Antiban` dedicated `TLazForm` utilities.

This is meant to be used with {ref}`TScriptForm` with
{ref}`TScriptForm.CreateAntibanTab` and will setup a a `TLazTabSheet` on it
with several controls to configure {ref}`Antiban`:
```{figure} ../../images/antibanform.png
```
*)
{$DEFINE WL_ANTIBAN_FORM_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TAntibanFormHelper
The `TAntibanFormHelper` type is, as it's name implies a helper type for the
`Antiban Form`.

It has a lot of methods and callbacks that are hidden by default and won't be
mentioned on this page because they are internal methods you shouldn't touch.

Do feel free to read the source code though if you so desire.
*)
  TAntibanFormHelper = record
    TaskList, BreakList: TLazListBox;

    TaskCombobox: TLazComboBox;
    TaskIntervalSpinner, TaskVariationSpinner: TLazSpinEdit;
    TaskDelButton, TaskEditButton, TaskAddButton, TaskPresetButton: TLazButton;

    Config: TConfigJSON;
  end;

procedure TAntibanFormHelper.Setup();
begin
  Self.Config.Setup('antiban' + PATH_SEP + ToStr(PlayerIndex));
end;

procedure TAntibanFormHelper.OnTaskToggle(sender: TLazObject);
var
  checked: Boolean;
begin
  checked := TLazCheckBox(sender).IsChecked();
  Self.TaskList.Enabled := checked;
end;

{$H-}
procedure TAntibanFormHelper.OnTaskSelected(sender: TLazObject; user: Boolean);
var
  i, idx: Integer;
  strings: TStringArray;
begin
  i := TLazListBox(sender).ItemIndex;
  if i = -1 then
  begin
    Self.TaskDelButton.Enabled := False;
    Self.TaskEditButton.Enabled := False;
    Exit;
  end;

  Self.TaskDelButton.Enabled := True;
  Self.TaskEditButton.Enabled := True;

  strings := TLazListBox(sender).Items.Strings[i].Split('|');
  if Length(strings) <> 3 then
    raise GetDebugLn('AntibanForm', 'Error parsing tasks list.');

  Self.TaskCombobox.ItemIndex := Self.TaskCombobox.Items.IndexOf(strings[0].Trim());
  Self.TaskIntervalSpinner.Text := strings[1].Trim();
  Self.TaskVariationSpinner.Text := strings[2].Trim();
end;

procedure TAntibanFormHelper.OnTaskAdd(sender: TLazObject);
begin
  Self.TaskList.Items.Add(
    Self.TaskCombobox.Text.PadRight(24) + ' | ' +
    Self.TaskIntervalSpinner.Text.PadCenter(6) + ' | ' +
    String(Self.TaskVariationSpinner.Text + '%').PadLeft(6)
  );
end;

procedure TAntibanFormHelper.OnTaskEdit(sender: TLazObject);
var
  i: Integer;
begin
  i := Self.TaskList.ItemIndex;
  if i = -1 then Exit;


  Self.TaskList.Items.Strings[i] := Self.TaskCombobox.Text.PadRight(24) + ' | ' +
                                    Self.TaskIntervalSpinner.Text.PadCenter(6) + ' | ' +
                                    String(Self.TaskVariationSpinner.Text + '%').PadLeft(6);
end;

procedure TAntibanFormHelper.OnTaskRemove(sender: TLazObject);
begin
  Self.TaskList.Items.Delete(Self.TaskList.ItemIndex);
end;

procedure TAntibanFormHelper.OnTaskPreset(sender: TLazObject);
begin
  //TODO
end;
{$H+}

var
(*
## AntibanForm variable
Global {ref}`TInventoryFormHelper` variable.
*)
  AntibanForm: TAntibanFormHelper;

(*
## ScriptForm CreateInventoryTab
```pascal
function TScriptForm.CreateInventoryTab(): TLazTabSheet;
```
Sets up a `TLazTabSheet` on your `TScriptForm` to configure your `Inventory Layouts`.

Example:
```pascal
{$I WaspLib/osrs.simba}
var
  form: TScriptForm;
begin
  form.Setup();
  form.CreateInventoryTab();
  form.Run();
end.
```
```{figure} ../../images/inventoryform.gif
```
For a more complete example check out the file `Simba/Includes/WaspLib/examples/inventory_form.simba`.
*)
function TScriptForm.CreateAntibanTab(): TLazTabSheet;
var
  checkbox: TLazCheckBox;
  left, top: Integer;
begin
  AntibanForm.Setup();

  Result := Self.CreateTab('Antiban');

  left:= 150;
  top := 10;

  checkbox := TLazCheckBox.CreateEx(Result, 'Tasks', 'Enable/Disable antiban tasks.', left, top);
  checkbox.OnChange := @AntibanForm.OnTaskToggle;

  top += 40;
  AntibanForm.TaskCombobox := TLazComboBox.CreateEx(Result, 'Task: ', 'Edit the currently selected task.', left, top, 190);
  AntibanForm.TaskCombobox.Items.Add('SmallRandomMouse');
  AntibanForm.TaskCombobox.Items.Add('LargeRandomMouse');
  AntibanForm.TaskCombobox.Items.Add('SmallCameraRotation');
  AntibanForm.TaskCombobox.Items.Add('LargeCameraRotation');
  AntibanForm.TaskCombobox.Items.Add('HoverSkill');
  AntibanForm.TaskCombobox.Items.Add('HoverSkills');
  AntibanForm.TaskCombobox.Items.Add('OpenSkill');
  AntibanForm.TaskCombobox.Items.Add('OpenSkills');
  AntibanForm.TaskCombobox.Items.Add('ChatScrolling');
  AntibanForm.TaskCombobox.ItemIndex := 0;

  AntibanForm.TaskIntervalSpinner := TLazSpinEdit.CreateEx(Result, 'Interval (mins): ', 'Edit the currently selected task interval.', left+210, top, 90);
  AntibanForm.TaskIntervalSpinner.Text := '5';
  AntibanForm.TaskVariationSpinner := TLazSpinEdit.CreateEx(Result, 'Variation (%): ', 'Edit the currently selected task interval variation.', left+310, top, 90);
  AntibanForm.TaskVariationSpinner.Text := '3';

  top += 40;
  AntibanForm.TaskDelButton := TLazButton.CreateEx(Result, 'Remove', 'Remove the current task.', left+15, top);
  AntibanForm.TaskDelButton.Enabled := False;
  AntibanForm.TaskDelButton.OnClick := @AntibanForm.OnTaskRemove;

  AntibanForm.TaskEditButton := TLazButton.CreateEx(Result, 'Update', 'Update the current task.', left+115, top);
  AntibanForm.TaskEditButton.Enabled := False;
  AntibanForm.TaskEditButton.OnClick := @AntibanForm.OnTaskEdit;

  AntibanForm.TaskAddButton := TLazButton.CreateEx(Result, 'Add', 'Add a new task.', left+215, top);
  AntibanForm.TaskAddButton.OnClick := @AntibanForm.OnTaskAdd;

  AntibanForm.TaskPresetButton := TLazButton.CreateEx(Result, 'Preset', 'Reset the task list with a preset.', left+315, top);
  AntibanForm.TaskPresetButton.OnClick := @AntibanForm.OnTaskPreset;

  top += 50;
  AntibanForm.TaskList := TLazListBox.CreateEx(Result, 'Tasks:', 'Configure antiban tasks.', left, top, 400, 100);
  AntibanForm.TaskList.Font.Name := 'Courier New';
  AntibanForm.TaskList.OnSelectionChange := @AntibanForm.OnTaskSelected;
end;
