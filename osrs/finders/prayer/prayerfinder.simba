(*
# PrayerFinder
Methods to find and compare spell images
*)

{$DEFINE SRLT_PRAYERFINDER_INCLUDED}
{$IFNDEF SRLT_OSRS}
  {$I SRLT/osrs.simba}
{$ENDIF}

(*
## ERSPrayer
```pascal
type ERSPrayer = enum(
  UNKNOWN,
  THICK_SKIN, BURST_OF_STRENGTH, CLARITY_OF_THOUGHT,
  ...
)
```
An enumeration representing different prayers in RuneScape.

### Values:
- Includes various prayers such as THICK_SKIN, PROTECT_FROM_MAGIC, PIETY, etc.
- UNKNOWN is the default value
*)
type
  ERSPrayer = enum(
    UNKNOWN,
    THICK_SKIN,           BURST_OF_STRENGTH,  CLARITY_OF_THOUGHT,
    SHARP_EYE,            MYSTIC_WILL,        ROCK_SKIN,
    SUPERHUMAN_STRENGTH,  IMPROVED_REFLEXES,  RAPID_RESTORE,
    RAPID_HEAL,           PROTECT_ITEM,       HAWK_EYE,
    MYSTIC_LORE,          STEEL_SKIN,         ULTIMATE_STRENGTH,
    INCREDIBLE_REFLEXES,  PROTECT_FROM_MAGIC, PROTECT_FROM_MISSILES,
    PROTECT_FROM_MELEE,   EAGLE_EYE,          MYSTIC_MIGHT,
    RETRIBUTION,          REDEMPTION,         SMITE,
    PRESERVE,             CHIVALRY,           PIETY,
    RIGOUR,               AUGURY
  );

(*
## TRSPrayerFinder
```pascal
type TRSPrayerFinder = record
  PrayerImages: array [ERSPrayer] of TImage;
  Version: String;
  Similarity: Single;
  ImageCompare: TImageCompareUtils;
  CacheDir: String;
  const ZIP: String = {$MACRO DIR} + 'images.zip';
end;
```
A record for finding and managing prayer images.

### Fields:
- `PrayerImages`: Stores images for each prayer type
- `Version`: Stores the version hash of the image set
- `Similarity`: Threshold for image comparison
- `ImageCompare`: Utility for comparing images
- `CacheDir`: Directory for storing cached prayer images
- `ZIP`: Path to the zip file containing prayer images
*)
type
  TRSPrayerFinder = record
    PrayerImages: array [ERSPrayer] of TImage;
    Version: String;
    Similarity: Single;
    ImageCompare: TImageCompareUtils;
    CacheDir: String;
    const ZIP: String = {$MACRO DIR} + 'images.zip';
  end;

(*
## PrayerFinder.Clean
```pascal
procedure TRSPrayerFinder.Clean(image, template: TImage);
```
Cleans the template image by drawing its black-colored points.

### Parameters:
- `image`: The source image
- `template`: The template image to be cleaned

### Example Usage:
```pascal
PrayerFinder.Clean(sourceImage, templateImage);
```
*)
procedure TRSPrayerFinder.Clean(image, template: TImage);
begin
  template.DrawColor := $0;
  template.DrawTPA(image.FindColor($0, 0));
end;

(*
## PrayerFinder.Setup
```pascal
procedure TRSPrayerFinder.Setup();
```
Sets up the PrayerFinder, including cache management and similarity settings.

### Parameters:
- None

### Example Usage:
```pascal
PrayerFinder.Setup();
```
*)
procedure TRSPrayerFinder.Setup();
var
  dir, tmp: String;
begin
  Self.Version := HashFile(EHashAlgo.MD5, Self.ZIP);
  SetLength(Self.Version, 6);

  Self.CacheDir := SRLTEnv.CacheDir + 'prayerfinder' + PATH_SEP;

  //Delete old cache files if they exist
  for dir in DirList(Self.CacheDir) do
  begin
    tmp := Copy(dir, 1, Length(dir)-Length(Self.Version));
    if tmp = Self.Version then Continue;
    DirDelete(dir, False);
  end;

  Self.CacheDir += Self.Version + PATH_SEP;

  if not DirCreate(Self.CacheDir) then
    raise GetDebugLn('PrayerFinder', 'Failed to create cache directory: ' + Self.CacheDir);

  //only set similarity if the user didn't set it already
  if Self.Similarity = 0 then
    Self.Similarity := 0.97;

  Self.ImageCompare.Clean := @Self.Clean;
end;

(*
## PrayerFinder.GetImage
```pascal
function TRSPrayerFinder.GetImage(prayer: ERSPrayer): TImage;
```
Retrieves the image for a specific prayer, loading it from cache or extracting from ZIP if needed.

### Parameters:
- `prayer`: The prayer type to get an image for

### Returns:
- The TImage for the specified prayer
- Nil if the prayer is UNKNOWN

### Example Usage:
```pascal
var img: TImage := PrayerFinder.GetImage(ERSPrayer.PIETY);
```
*)
function TRSPrayerFinder.GetImage(prayer: ERSPrayer): TImage;
var
  name, path: String;
begin
  if prayer = ERSPrayer.UNKNOWN then
    Exit();

  if Self.PrayerImages[prayer] <> nil then
    Exit(Self.PrayerImages[prayer]);

  name := LowerCase(ToStr(prayer).After('.')) + '.png';
  path := Self.CacheDir + name;

  if not FileExists(path) then
    ZipExtractEntry(Self.ZIP, name, Self.CacheDir);

  Self.PrayerImages[prayer] := TImage.Create(path);
  Self.PrayerImages[prayer].FreeOnTerminate := True;

  Result := Self.PrayerImages[prayer];
end;

(*
## PrayerFinder.FindAll (First Overload)
```pascal
function TRSPrayerFinder.FindAll(prayers: array of ERSPrayer; boxes: TBoxArray; maxToFind: Integer = 0): TImageMatchArray;
```
Finds matches for specified prayers within given boxes.

### Parameters:
- `prayers`: Array of prayers to search for
- `boxes`: Boxes to search within
- `maxToFind`: Maximum number of matches to find (0 means find all)

### Returns:
- Array of image matches found

### Example Usage:
```pascal
var matches := PrayerFinder.FindAll([ERSPrayer.PIETY], boxes);
```
*)
function TRSPrayerFinder.FindAll(prayers: array of ERSPrayer; boxes: TBoxArray; maxToFind: Integer = 0): TImageMatchArray;
var
  prayer: ERSPrayer;
  img: TImage;
  i: Integer;
  skip: TBooleanArray;
  match: Single;
begin
  SetLength(skip, Length(boxes));

  for prayer in prayers do
  begin
    if prayer = ERSPrayer.UNKNOWN then
      Continue;

    img := Self.GetImage(prayer);
    for i := 0 to High(boxes) do
    begin
      if skip[i] then
        Continue;

      match := Self.Similarity;
      if Self.ImageCompare.Compare(img, boxes[i], match) then
      begin
        Result += [i, boxes[i], match];
        if (maxToFind > 0) and (Length(Result) >= maxToFind) then
          Exit;
        skip[i] := True;
      end;
    end;
  end;
end;

(*
## PrayerFinder.FindAll (Second Overload)
```pascal
function TRSPrayerFinder.FindAll(prayers: array of ERSPrayer; boxes: TBoxArray; out matches: TImageMatchArray): Boolean; overload;
```
Finds matches for specified prayers within given boxes, with an output parameter.

### Parameters:
- `prayers`: Array of prayers to search for
- `boxes`: Boxes to search within
- `matches`: Output parameter to store found matches

### Returns:
- True if matches are found
- False if no matches are found

### Example Usage:
```pascal
var matches: TImageMatchArray;
if PrayerFinder.FindAll(prayers, boxes, matches) then
  WriteLn('Matches found');
```
*)
function TRSPrayerFinder.FindAll(prayers: array of ERSPrayer; boxes: TBoxArray; out matches: TImageMatchArray): Boolean; overload;
var
  prayer: ERSPrayer;
  results: TImageMatchArray;
  i: Integer;
  img: TImage;
  match: Single;
  skip: TBooleanArray;
begin
  SetLength(skip, Length(boxes));

  for prayer in prayers do
  begin
    if prayer = ERSPrayer.UNKNOWN then
      Continue;

    img := Self.GetImage(prayer);

    for i := 0 to High(boxes) do
    begin
      if skip[i] then
        Continue;

      match := Self.Similarity;
      if Self.ImageCompare.Compare(img, boxes[i], match) then
      begin
        results += [i, boxes[i], match];
        skip[i] := True;
      end;
    end;

    if results = [] then
      Exit;

    matches += results;
    results := [];
  end;

  Result := True;
end;

(*
## PrayerFinder.Find
```pascal
function TRSPrayerFinder.Find(prayers: array of ERSPrayer; boxes: TBoxArray; out match: TImageMatch): Boolean;
```
Finds the first match for specified prayers within given boxes.

### Parameters:
- `prayers`: Array of prayers to search for
- `boxes`: Boxes to search within
- `match`: Output parameter to store the first match found

### Returns:
- True if a match is found
- False if no match is found

### Example Usage:
```pascal
var match: TImageMatch;
if PrayerFinder.Find([ERSPrayer.PIETY], boxes, match) then
  WriteLn('Match found');
```
*)
function TRSPrayerFinder.Find(prayers: array of ERSPrayer; boxes: TBoxArray; out match: TImageMatch): Boolean;
var
  prayer: ERSPrayer;
  img: TImage;
  i: Integer;
  similarity: Single;
  skip: TBooleanArray;
begin
  SetLength(skip, Length(boxes));

  for prayer in prayers do
  begin
    if prayer = ERSPrayer.UNKNOWN then
      Continue;

    img := Self.GetImage(prayer);
    for i := 0 to High(boxes) do
    begin
      if skip[i] then
        Continue;

      similarity := Self.Similarity;
      if Self.ImageCompare.Compare(img, boxes[i], similarity) then
      begin
        match := [i, boxes[i], similarity];
        Exit(True);
      end;

    end;
  end;
end;

var
  PrayerFinder: TRSPrayerFinder;

begin
  PrayerFinder.Setup();
end;
