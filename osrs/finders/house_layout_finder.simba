
{$DEFINE WL_HOUSE_LAYOUT_FINDER_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type

  TRSHouseLayoutFinder = record
    RoomImages: array [EHouseRoom] of TImageArray;
    Version: String;
    Similarity: Single;
    ImageCompare: TImageCompareUtils;
    CacheDir: String;
  end;


procedure TRSHouseLayoutFinder.Setup();
var
  path, tmp: String;
begin
  Self.Version := WLAssets.HouseLayoutFinder.Hash;
  Self.CacheDir := WLEnv.CacheDir + 'houselayoutfinder' + PATH_SEP;

  //Delete old cache files if they exist
  for path in DirList(Self.CacheDir) do
  begin
    tmp := Copy(path, Length(path) - Length(Self.Version), Length(path));
    if tmp = Self.Version then Continue;
    DirDelete(path, False);
  end;

  Self.CacheDir += Self.Version + PATH_SEP;

  if not DirCreate(Self.CacheDir) then
    raise GetDebugLn('HouseLayoutFinder', 'Failed to create cache directory: ' + Self.CacheDir);

  //only set similarity if the user didn't set it already
  if Self.Similarity = 0 then
    Self.Similarity := 0.85;
end;


property TRSHouseLayoutFinder.Images(room: EHouseRoom): TImageArray;
var
  name, path: String;
  i: Integer;
begin
  if Self.RoomImages[room] <> [] then
    Exit(Self.RoomImages[room]);

  name := LowerCase(ToStr(room).After('.')) + '.png';
  path := Self.CacheDir + name;

  if not FileExists(path) then
    ZipExtractEntry(WLAssets.HouseLayoutFinder.Path, name, Self.CacheDir);

  Self.RoomImages[room] += new TImage(path);

  if room = EHouseRoom.UNKNOWN then
    Exit(Self.RoomImages[room]);

  for i := 1 to 3 do
    Self.RoomImages[room] += Self.RoomImages[room][i-1].RotateClockWise(1);

  Result := Self.RoomImages[room];
end;

function TRSHouseLayoutFinder.Compare(bounds: TBox; out rotation: Integer): EHouseRoom;
var
  room: EHouseRoom;
  sample: TImage;
  templates: TImageArray;
  match: Single;
begin
  if Target.HasColor($3F4A53, 0.4, 1, bounds) then
  begin
    rotation := 0;
    Exit(EHouseRoom.UNKNOWN);
  end;

  sample := Target.GetImage(bounds);
  for room := EHouseRoom.PARLOUR to High(EHouseRoom) do
  begin
    templates := Self.Images[room];
    for rotation := 0 to High(templates) do
    begin
      match := templates[rotation].Compare(sample);
      if match >= Self.Similarity then
        Exit(room);
    end;
  end;
end;

var

  HouseLayoutFinder: TRSHouseLayoutFinder;
